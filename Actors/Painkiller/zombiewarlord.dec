ACTOR Painkiller_ZombieWarlord : Painkiller_AIBrain
{
	Health 129
	Radius 20
	Height 56
	Speed 5
	Mass 150
	PainChance 200
	SeeSound ""
	PainSound "Painkiller/ZombieSoldier/Hit"
	DeathSound "Painkiller/ZombieSoldier/Death"
	Obituary "%o had their soul claimed by a zombie warlord."
	States
	{
		Spawn:
			PK00 A 0
			PK00 A 0 ACS_NamedExecuteWithResult("Painkiller_MonsterLoop",3)
		Spawn2:
			PK00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 3 A_Look
			Loop
		See:
			PK01 ABCDEFGHIJKLMNOPQRS 2 A_Chase
			Loop
		Melee:
			PK02 A 0 A_FaceTarget
			PK02 ABCDEFGHIJ 3
			PK02 K 0 ACS_NamedExecuteWithResult("Painkiller_TempChangeSpecies")
			PK02 K 0 A_CustomMissile("Painkiller_ZombieWarlordMelee",height/2)
			PK02 KLMNOPQRS 3
			Goto See
		Pain:
			PK06 A 0 A_Pain
			PK06 ABCDEFGHIJ 3
			Goto See
		Death:
			PK03 A 0 A_Jump(256,"Death1","Death2","Death3")
		Death1:
			PK03 A 0 A_Scream
			PK03 A 0 A_NoBlocking
			PK03 ABCDEFGHIJKLMNOPQRSTUVW 3
			PK03 X -1
			Stop
		Death2:
			PK04 A 0 A_Scream
			PK04 A 0 A_NoBlocking
			PK04 ABCDEFGHIJKLMNOPQRSTUV 3
			PK04 W -1
			Stop
		Death3:
			PK05 A 0 A_Scream
			PK05 A 0 A_NoBlocking
			PK05 ABCDEFGHIJKLMNOPQRSTUVW 3
			PK05 X -1
			Stop
		Pain.PainkillerIce:
			"####" "#" 0 A_JumpIf(CallACS("Painkiller_FlagStorage",1,1)==1,1)
			"####" "#" 0 A_JumpIf(CallACS("Painkiller_FlagStorage",1,2)==1,1)
			"####" "#" 0 A_JumpIf(CallACS("Painkiller_FlagStorage",1,3)==1,1)
			"####" "#" 0 A_ChangeFlag("NOPAIN",true)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",false)
			"####" "#" 0 A_ChangeFlag("NOBLOOD",true)
			"####" "#" 0 A_StopSound(CHAN_BODY)
			"####" "#" 0 A_PlaySound("Painkiller/Freezer/Freeze",CHAN_BODY)
			"####" "#" 0 ACS_NamedExecuteAlways("Painkiller_Freeze",0,1)
		PainkillerIceLoop:
			"####" "#" 2
			Loop
		PainkillerIceThaw:
			"####" "#" 0 A_PlaySound("Painkiller/Freezer/Unfreeze",CHAN_BODY)
			"####" "#" 20
			"####" "#" 0 A_ChangeFlag("NOPAIN",CallACS("Painkiller_FlagStorage",2,1))
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",CallACS("Painkiller_FlagStorage",2,2))
			"####" "#" 0 A_ChangeFlag("NOBLOOD",CallACS("Painkiller_FlagStorage",2,3))
			Goto See
		Death.Painkiller:
		Death.PainkillerBullet:
		Death.PainkillerElectro:
		Death.PainkillerExplosive:
		Death.PainkillerShuriken:
		Death.PainkillerFire:
		Death.PainkillerDemon:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			Goto Death
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
			PK03 A 0 A_Jump(256,"StakeDeath1","StakeDeath2","StakeDeath3")
		StakeDeath1:
			PK03 B 2
			Loop
		StakeDeath2:
			PK04 B 2
			Loop
		StakeDeath3:
			PK05 B 2
			Loop
		XDeath:
		PainkillerGib:
			TNT1 A 0 A_PlaySound("Painkiller/Gib",CHAN_BODY)
			TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Painkiller_Blood_Spray",0,0,height*2,frandom(-4,4),frandom(-4,4),velz+frandom(4,8),0,SXF_NOCHECKPOSITION)
			TNT1 AAAAAAAA 0 A_SpawnItemEx("Painkiller_Blood",random(-8,8),random(-8,8),random(0,44),0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_ArmRight",2,28,44,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_ArmLeft",2,-28,44,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_Torso",0,0,40,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_Head",0,0,51,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_Legs",-2,0,10,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 0 A_SpawnItemEx("Painkiller_ZombieWarlord_Gib_Sword",-2,8,14,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			TNT1 A 5 A_NoBlocking
			Stop
	}
}

Actor Painkiller_ZombieWarlord_Gib_ArmLeft : Painkiller_PotatoHeadGib {}
Actor Painkiller_ZombieWarlord_Gib_ArmRight : Painkiller_PotatoHeadGib {}
Actor Painkiller_ZombieWarlord_Gib_Legs : Painkiller_PotatoHeadGib {}
Actor Painkiller_ZombieWarlord_Gib_Head : Painkiller_PotatoHeadGib {}
Actor Painkiller_ZombieWarlord_Gib_Torso : Painkiller_PotatoHeadGib {}
Actor Painkiller_ZombieWarlord_Gib_Sword : Painkiller_PotatoHeadGib {}

Actor Painkiller_ZombieWarlordMelee : Painkiller_AttackMelee	
{ 
	Damage (10) 
	Speed 20
	Height 24 
	Radius 12
	DamageType "PainkillerZombieWarlord"
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 2 Nodelay
			//TNT1 A 1 A_ChangeVelocity(0,0,-32,CVF_REPLACE)
			TNT1 A 0 A_CheckFloor("Death")
			Stop
		Death:
			TNT1 A 0 A_PlaySound("Painkiller/ZombieSoldier/HitGround",CHAN_5)
			TNT1 A 0 A_SpawnItemEx("Painkiller_Monster_HitGround")
			TNT1 A 1
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteWithResult("Painkiller_BloodGenerator_Offsets")
			TNT1 A 0 A_GiveInventory("Painkiller_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1 A_PlaySound("Painkiller/ZombieSoldier/HitPlayer",CHAN_5)
			TNT1 A 1
			Stop
	}
}