ACTOR DukeEnemyBulletPuff : DukeBulletPuff
{
	-PUFFONACTORS
	DamageType "DoomBullet"
}

ACTOR JetpackToken : Counter {}

ACTOR D3DHeightCheck //Thanks to PresidentPeople for this!
{
	Speed 1
	Projectile
	+NOCLIP
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 4 A_JumpIf(momz > 0.05,"TargetHigher")
		Stop
	TargetHigher:
		TNT1 A 10 A_GiveToTarget("JetpackToken",1)
		Stop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR TrooperLaser
{
	Radius 13
	Height 8
	Speed 35
	Damage (7)
	PROJECTILE
	+BRIGHT
	Decal DukeBulletchip
	Scale 0.3
	States
	{
	Spawn:
		ASLS A 0
		ASLS A 0 A_SpawnItemEx("TrooperLaserTrail",-20,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		ASLS A 0 A_SpawnItemEx("TrooperLaserTrail",-15,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		ASLS A 0 A_SpawnItemEx("TrooperLaserTrail",-10,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		ASLS A 1 A_SpawnItemEx("TrooperLaserTrail",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR TrooperLaserTrail
{
	+BRIGHT
	+NOINTERACTION
	+CLIENTSIDEONLY
	scale 0.3
	States
	{
	Spawn:
		ASLS A 2
		Stop
	}
}

ACTOR DukeTeleportFlash
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Scale 0.75
	States
	{
	Spawn:
		TFLA ABCD 1
		TFLA EF 2
		TFLA EDC 1
		TFLA B 2
		TFLA A 1
		Stop
	}
}

ACTOR SamsaraMixerOverlordRocketPuff : SamsaraDukeRocketPuff
{
	RenderStyle Translucent
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 2
		DKPF ABCD 4
		Stop
	}
}

ACTOR ACommanderRocket
{
	Radius 12
	Height 8
	Speed 30
	PROJECTILE
	+BRIGHT
	+FORCERADIUSDMG
	Obituary "%o was blown to bits by an Assault Commander."
	Scale 0.4
	Decal Scorch
	States
	{
		Spawn:
			RPGX A 1 A_SpawnItemEx("SamsaraDukeRocketPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Loop
		Death:
			TNT1 A 0 A_Explode(35, 111)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NORM)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Stop
	}
}

ACTOR ACommanderRocketBeach : ACommanderRocket
{
	Obituary "%o was blown to bits by an Assault Commander."
	States
	{
		Spawn:
			RPGX W 1 A_SpawnItemEx("SamsaraDukeRocketPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Loop
	}
}

ACTOR SamsaraMixerOverlordRocket : ACommanderRocket
{
	Radius 16
	Height 12
	Obituary "%o was destroyed by the Overlord."
	Scale 0.8
	States
	{
		Death:
			TNT1 A 0 A_Explode(70*Random(2,1), Random(111,131))
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NORM)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Stop
	}
}

ACTOR CyEmperorRocket : ACommanderRocket
{
	Speed 39
	Scale 0.7
	Obituary "%o was dealt with by the Cycloid Emperor's rockets."
	States
	{
		Spawn:
			RPGX A 1
			Loop
		Death:
			TNT1 A 0 A_Explode(Random(54,70)*Random(2,1), Random(111,131))
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NORM)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Stop
	}
}

ACTOR CyEmperorRocketBeach : CyEmperorRocket
{
	Obituary "%o was dealt with by the Cycloid Emperor's coconuts."
	States
	{
		Spawn:
			RPGX W 1
			Loop
	}
}

ACTOR EnforcerTurd
{
	Height 2
	Radius 2
	Scale 0.65
	-SOLID
	+NOINTERACTION
	+MOVEWITHSECTOR
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TURD A -1
			Stop
	}
}

ACTOR ProtectorDroneGibs1 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ1 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ1 E 2100
			PRJ1 E 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs2 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ2 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ2 E 2100
			PRJ2 E 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs3 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ3 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ3 E 2100
			PRJ3 E 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs4 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ4 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ4 E 2100
			PRJ4 E 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs5 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ5 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ5 E 2100
			PRJ5 E 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs6 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ6 AB 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEFGH 5
			Stop
		Death2:
			PRJ6 CDEFGH 5
		Death2Loop:
			PRJ6 H 2100
			PRJ6 H 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2Loop")
			stop
	}
}

ACTOR ProtectorDroneGibs7 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ7 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ7 A 2100
			PRJ7 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs8 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ8 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ8 A 2100
			PRJ8 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProtectorDroneGibs9 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			PRJ9 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			PRJ6 ABCDEF 5
			Stop
		Death2:
			PRJ9 A 2100
			PRJ9 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

actor DukeGibPopSpawner_Duke2
{
	States
	{
		Spawn:
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs1",true)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs2",true)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs3",true)
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnDebris("ProtectorDroneGibs4",true)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs5",true)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs6",true)
			//TNT1 A 0 A_Jump(128,2)
			//TNT1 A 0 A_SpawnItemEx("DukeBloodPool_Duke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_PlaySound("duke/enemygib")
			Stop
	}
}

ACTOR DukeJibs7 : DukeJibs1 //Assault Enforcer Head
{
	States
	{
		Spawn:
			DJ07 ABCD 5
			Loop
		Death2:
			DJ07 A 2100
			DJ07 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR DukeJibs8 : DukeJibs1 //Assault Enforcer arm
{
	States
	{
		Spawn:
			DJ08 ABCD 5
			Loop
		Death2:
			DJ08 A 2100
			DJ08 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR DukeJibs9 : DukeJibs1 //Assault Enforcer leg
{
	States
	{
		Spawn:
			DJ09 ABCD 5
			Loop
		Death2:
			DJ09 A 2100
			DJ09 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR DukeJibs10 : DukeJibs1 //Assault Trooper head
{
	States
	{
		Spawn:
			DJ10 ABCD 5
			Loop
		Death2:
			DJ10 A 2100
			DJ10 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR DukeJibs11 : DukeJibs1 //Assault Trooper arm
{
	States
	{
		Spawn:
			DJ11 ABCD 5
			Loop
		Death2:
			DJ11 A 2100
			DJ11 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR DukeJibs12 : DukeJibs1 //Assault Trooper leg
{
	States
	{
		Spawn:
			DJ12 ABCD 5
			Loop
		Death2:
			DJ12 A 2100
			DJ12 A 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

actor PigDrop : SamsaraSlotTwoDrop
{
    Scale 0.65
    +DROPPED
    +NEVERRESPAWN
    States
    {
      Spawn:
        DSGP A 0 NoDelay ACS_NamedExecuteWithResult("Samsara_SwitchableObject", 35)
        goto SpawnGameModeCheck

      SpawnActual:
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        DSGP A -1 A_ChangeFlag("NOTIMEFREEZE",false)
        stop
    }
}

actor BeachPigDrop : SamsaraSlotTwoDrop
{
    Scale 0.65
    +DROPPED
    +NEVERRESPAWN
    States
    {
      Spawn:
        SHTP B 0 NoDelay ACS_NamedExecuteWithResult("Samsara_SwitchableObject", 35)
        goto SpawnGameModeCheck

      SpawnActual:
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        SHTP B -1 A_ChangeFlag("NOTIMEFREEZE",false)
        stop
    }
}

actor AEnforcerDrop : SamsaraSlotFourDrop
{
    Scale 0.65
    +DROPPED
    +NEVERRESPAWN
    States
    {
      Spawn:
        DCCP A 0
        goto SpawnGameModeCheck

      SpawnActual:
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        DCCP A -1 A_ChangeFlag("NOTIMEFREEZE",false)
        stop
    }
}

actor BeachEnforcerDrop : SamsaraSlotFourDrop
{
    Scale 0.65
    +DROPPED
    +NEVERRESPAWN
    States
    {
      Spawn:
        CHGP B 0
        goto SpawnGameModeCheck

      SpawnActual:
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        CHGP B -1 A_ChangeFlag("NOTIMEFREEZE",false)
        stop
    }
}

actor DukeArmor : SamsaraArmorPickup
{
    RenderStyle "Normal"
    Scale 0.65
    +DROPPED
    -FLOATBOB
    States
    {
      Spawn:
        DARP A 0
        goto SpawnGameModeCheck

      SpawnActual:
        TNT1 A 0 A_ChangeFlag("THRUACTORS",false)
        DARP A -1 A_ChangeFlag("NOTIMEFREEZE",false)
        stop

      Pickup:
        TNT1 A 0 A_JumpIf(CallACS("SamsaraArmorCapCheck", -58), "PickupFail")
      PickupSuccess:
        TNT1 A 0 ACS_NamedExecuteAlways("SamsaraPickupMessages", 0, 6, 0, 0)
        TNT1 A 0 A_GiveInventory("SamsaraArmorPickupBlend")
        TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_ITEM|CHAN_MAYBE_LOCAL)
        TNT1 A 0 ACS_NamedExecuteWithResult("SamsaraArmorAdjust", -58)
        stop
    }
}

actor DukeArmorArmor : BasicArmorPickup
{
    Inventory.Icon "DARPA0"
    Armor.SavePercent 35
    Armor.SaveAmount 50
}

ACTOR RandomPigDrop : RandomSpawner
{
	+DROPPED
	DropItem "PigDrop" 255 34
	DropItem "DukeArmor" 255 6
}

ACTOR RandomBeachPigDrop : RandomSpawner
{
	+DROPPED
	DropItem "BeachPigDrop" 255 34
	DropItem "DukeArmor" 255 6
}

ACTOR RandomEnforcerDrop : RandomSpawner
{
	+DROPPED
	DropItem "AEnforcerAmmoDrop" 255 8
	DropItem "AEnforcerDrop" 255 32
}

ACTOR RandomBeachEnforcerDrop : RandomSpawner
{
	+DROPPED
	DropItem "AEnforcerAmmoDrop" 255 8
	DropItem "BeachEnforcerDrop" 255 32
}

ACTOR BattleLordMortar
{
	Height 4
	Radius 2
	Speed 8
	damage (10)
	Gravity 0.6
	PROJECTILE
	+DOOMBOUNCE
	-NOGRAVITY
	+DONTFALL
	BounceFactor 0.9
	BounceCount 10
	SeeSound "duke/pipebomb"
	Decal Scorch
	Obituary "%o was mortared by the Battlelord."
	States
	{
		Spawn:
			BLMR ABCD 2 A_SpawnItemEx("BattleLordMortarAfterImage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 0 A_Explode(50, 156)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NORM)
			TNT1 A 5 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			Stop
	}
}

ACTOR BattleLordMortarAfterImage
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 0
			BLMR ABCD 2 A_FadeOut
			Loop
	}
}

ACTOR BattleLordChaingunToken : CustomInventory { Inventory.MaxAmount 12 }
ACTOR BattleLordMortarToken : CustomInventory { Inventory.MaxAmount 4 }
ACTOR BattleLordMortar1 : BattleLordMortar { Speed 26 }
ACTOR BattleLordMortar2 : BattleLordMortar { Speed 32 }
ACTOR BattleLordMortar3 : BattleLordMortar { Speed 38 }

ACTOR TankMissileToken : Inventory { Inventory.MaxAmount 11 }

ACTOR OctabrainProjectile
{
	Radius 16
	Height 12
	Speed 16
	Damage (38)
	Scale 0.5
	PROJECTILE
	+FORCERADIUSDMG
	Obituary "%o was brainshocked by an Octabrain."
	Decal Scorch
	States
	{
		Spawn:
			TNT1 A 0 A_PlaySound("octabrain/attack", CHAN_AUTO)
			OCPR ABCDEFGHIJKLMNOPQRST 3
			Goto Death
		Death:
			TNT1 A 5 A_FadeOut
			Stop
	}
}

ACTOR OctabrainProjectileCy : OctabrainProjectile
{
    Speed 20
    Obituary "%o was brainshocked by the Cycloid Emperor."
}

Actor ProtozoidAttackOMG : Inventory { Inventory.Amount 1 Inventory.Maxamount 1 }

ACTOR DukeBloodAC : DukeBlood
{
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 1 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
			TNT1 AA 1 A_SpawnItemEx("DukeJibs6",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_NOCHECKPOSITION)
			Goto Super::Ray
	}
}

ACTOR DukeBloodBoss : DukeBlood
{
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
			TNT1 AA 0 A_SpawnItemEx("DukeJibs6",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("DukeShrapnel1",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_NOCHECKPOSITION)
			Goto Super::Ray
	}
}

ACTOR DukeBloodQueen : DukeBlood
{
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("ProtectorDroneGibs6",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("QueenShrapnel1",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_NOCHECKPOSITION)
			Goto Ray
		Ray:
			TNT1 A 0 A_Jump(184,"Ray_NoSlide")
			TNT1 A 0 A_Jump(128,"Quit")
			TNT1 A 0 A_Jump(160,1,2,3,4,5)
			TNT1 AAAAA 0 A_CustomMissile("PDroneBlood_Ray", 0, 0, angle + random(-2,2),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,pitch + random(-2,2))
			Goto Super::Quit
		Ray_NoSlide:
			TNT1 A 0 A_Jump(184,"Quit")
			TNT1 A 0 A_Jump(160,1,2,3,4,5)
			TNT1 AAAAA 0 A_CustomMissile("PDroneBlood_Ray_NoSlide", 0, 0, angle + random(-2,2),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,pitch + random(-2,2))
			Goto Super::Quit
	}
}

ACTOR QueenShrapnel1 : DukeGibs1
{
	Health 1
	States
	{
		Spawn:
			AQS2 ABCD 5
			Loop
		Death:
			TNT1 A 0 A_Gravity
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			TNT1 A 1
			Stop
		Death2:
			AQS2 B 2100
			AQS2 B 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
			stop
	}
}

ACTOR ProDroneBlood : DukeBlood
{
	+NOGRAVITY
	Scale 0.65
	States
	{
		Spawn:
			TNT1 AAA 0
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
			TNT1 A 0 A_SpawnItemEx("Switchable_FlyingBlood",0,0,0,random(-350,350)/100.00,random(-350,350)/100.00,random(500,1000)/100.00,random(-180,180),SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_FaceTarget
			Goto Ray
		Ray:
			TNT1 A 0 A_Jump(184,"Ray_NoSlide")
			TNT1 A 0 A_Jump(128,"Quit")
			TNT1 A 0 A_Jump(160,1,2,3,4,5)
			TNT1 AAAAA 0 A_CustomMissile("PDroneBlood_Ray", 0, 0, angle + random(-2,2),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,pitch + random(-2,2))
			Goto Quit
		Ray_NoSlide:
			TNT1 A 0 A_Jump(184,"Quit")
			TNT1 A 0 A_Jump(160,1,2,3,4,5)
			TNT1 AAAAA 0 A_CustomMissile("PDroneBlood_Ray_NoSlide", 0, 0, angle + random(-2,2),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,pitch + random(-2,2))
			Goto Quit
		Quit:
			PDBL ABCD 4
			Stop
	}
}

Actor PDroneBlood_Ray
{
	Projectile
	Speed 48
	Radius 2
	Height 4
	+ALWAYSPUFF
	+THRUACTORS
	+NOGRAVITY
	Decal PDroneBlood
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Die
			Stop
		Death:
			TNT1 A 0
			Stop
	}
}

Actor PDroneBlood_Ray_NoSlide : PDroneBlood_Ray { Decal PDroneBlood_NoSlide }
