Actor MMRandomizer_TriteHole
{
	Height 32
	Mass 25
	Radius 16
	Species "Trite"
	Scale 0.55
	+ISMONSTER
	-COUNTKILL
	+NODAMAGE
	+THRUSPECIES
	-SHOOTABLE
	+SOLID
	var int user_triteamount;
	var int user_loopcounter;
	States
	{
		Spawn:
			VD39 LL 0 A_SetUserVar("user_triteamount",5)
			VD39 L 5 A_Look
			Loop
		See:
			VD39 L 35
		SeeLoop:
			VD39 L 0 A_JumpIf(user_loopcounter >= user_triteamount, "Remove")
			VD39 L 0 A_SetUserVar("user_loopcounter",user_loopcounter+1)
			VD39 MN 5
			VD39 N 0 A_SpawnItemEx("MMRandomizer_Trite",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERPOINTERS,0,tid)
			VD39 L 35
			Loop
		Remove:
			VD39 LLLLLLLLLLLLLLLLLLLL 4 A_Fadeout(0.05)
			Stop
	}
}

Actor MMRandomizer_Trite : SamsaraMonsterDemonicTinyTotalGib
{
	HitObituary "%o was nipped by a Trite"
	Health 30
	Radius 20
	Height 25
	Mass 100
	Speed 6
	Scale 0.55
	PainChance 192
	BloodColor DarkGreen
	PainSound "VDR/Trite/Pain"
	SeeSound "VDR/Trite/Sight"
	DeathSound "VDR/Trite/Death"
	ActiveSound "VDR/Trite/Active"
	MeleeSound "VDR/Trite/MeleeHit"
	Species "Trite"
	MeleeDamage 2
	MeleeThreshold 64
	MaxTargetRange 160
	MinMissileChance 10
	Damage 2
	Monster
	+FLOORCLIP
	+DONTHURTSPECIES
	+NOTARGET
	Tag "Trite"
	States
	{
		Spawn:
		    VD39 A 0
		    VD39 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		    VD39 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		    VD39 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			VD39 A 1 A_Look
			Loop
		See:
			VD39 A 2 A_Chase("", "", 0)
			VD39 A 2 A_Chase
			VD39 A 0 A_PlaySoundEx("VDR/Trite/Step", "SoundSlot7", 0)
			VD39 BB 2 A_Chase("Melee", "Missile", 2)
			VD39 B 0 A_PlaySoundEx("VDR/Trite/Step", "SoundSlot7", 0)
			VD39 CC 2 A_Chase
			VD39 C 0 A_PlaySoundEx("VDR/Trite/Step", "SoundSlot7", 0)
			VD39 DD 2 A_Chase("Melee", "Missile", 2)
			VD39 D 0 A_PlaySoundEx("VDR/Trite/Step", "SoundSlot7", 0)
			Loop
		Melee:
			VD39 A 6 A_FaceTarget
			VD39 A 0 A_Jump(192, 2)
			VD39 A 0 A_PlaySound("VDR/Trite/Attack")
			VD39 E 6 A_MeleeAttack
			Goto See
		Missile:
			VD39 A 0 A_Jump(128, "See")
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			VD39 AAA 3 A_FaceTarget
			VD39 F 0 A_PlaySound("VDR/Trite/Attack")
			VD39 F 0 A_SkullAttack
			VD39 F 10 ThrustThingZ(0, 6, 0, 1)
			VD39 E 8 A_Stop
			Goto See
		Web:
			VD39 F 0
			VD39 F 0 A_ChangeFlag("LowGravity", 1)
			VD39 F 0 A_ChangeFlag("NoDamage", 1)
			VD39 F 0 A_ChangeFlag("NoPain", 1)
			VD39 F 0 A_PlaySound("VDR/Trite/WebStart")
			VD39 F 1 A_CheckFloor(1)
			Goto Web+5
			VD39 A 0 A_ChangeFlag("NoDamage", 0)
			VD39 A 0 A_ChangeFlag("NoPain", 0)
			VD39 A 0 A_ChangeFlag("LowGravity", 0)
			VD39 A 12 A_PlaySound("VDR/Trite/WebEnd")
			Goto Spawn
		Pain:
			VD39 E 3
			VD39 E 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD39 G 5 A_Scream
			VD39 H 0 A_PlaySoundEx("VDR/Trite/Splat", "SoundSlot7", 0)
			VD39 H 5 A_NoBlocking
			VD39 IJK 5    
			Stop
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			VD39 E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD39 E 16 A_Pain
			Goto See 
	}
}