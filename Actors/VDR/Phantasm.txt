actor MMRandomizer_Phantasm : LostSoul3
{
	Health 50
	Radius 16
	Height 56
	Mass 50
	Speed 10
	Damage 3
	PainChance 256
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+NOICEDEATH
	-NOBLOOD
	RenderStyle Add
	BloodColor "green"
	Obituary "%o was smoked by a Phantasm."
	Tag "Phantasm"
	States
	{
		Spawn:
			VD64 A 0
			VD64 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD64 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD64 A 0 A_GiveInventory("AlreadySpawned",1)
			VD64 A 0 A_GiveInventory("IsAFlyingActor")
			VD64 A 0
		Spawn2:
			TNT1 A 0 A_GiveInventory("IsAFlyingActor")
		Spawn3:
			VD64 AB 5 BRIGHT A_Look
			Loop
		See:
			VD64 AABB 3 BRIGHT A_Chase
			Loop
		Missile:
			VD64 C 10 BRIGHT A_FaceTarget
			VD64 D 4 BRIGHT A_SkullAttack
			VD64 CD 4 BRIGHT
			Goto Missile +2
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			VD64 E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD64 E 16 A_Pain
			Goto See	
		Pain:
			VD64 E 3 BRIGHT
			VD64 E 3 BRIGHT A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			VD64 F 6 BRIGHT
			VD64 F 6 BRIGHT A_Scream
			VD64 FFFFFFFFFFFF 3 BRIGHT A_SpawnItemEx("MMRandomizer_PhantasmPuff", random(-15,15), random(-15,15), random(12, 24), 0, 0, 1)
			VD64 G 6 BRIGHT
			TNT1 A 0 A_Explode(64, 128, XF_HURTSOURCE | XF_NOTMISSILE, false, 32)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD64 H 6 BRIGHT A_PlaySound("phantasm/explode")
			VD64 I 6 BRIGHT A_NoBlocking
			VD64 J 6
			VD64 K 6
			Stop
	}
}

Actor MMRandomizer_PhantasmPuff : DoomBulletPuff_Samsara
{
	States
	{
	Spawn:
		VD64 L 4 Bright
		PFF2 M 4
    // Intentional fall-through
	Melee:
		VD64 NO 4
		Stop
	}
}