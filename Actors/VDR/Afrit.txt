Actor MMRandomizer_Afrit : SamsaraMonsterSkeletonLarge
{
	Health 800
	Speed 6
	Radius 24
	Height 72
	PainChance 50
	Mass 500
	SeeSound "doom/Baron/Sight"
	PainSound "doom/Baron/Pain"
	DeathSound "doom/Baron/Death"
	ActiveSound "doom/Baron/Active"
	Monster
	+NOGRAVITY
	+NOBLOOD
	+FLOAT
	+DONTHURTSPECIES
	Obituary "%o was scorched by an Afrit."
	HitObituary "%o found the Afrit too hot to handle."
	Damagefactor "HellFire", 0.0
	var int user_scalex;
	var int user_scaley;	
	Tag "Afrit"
	States
	{
		Spawn:
			VD72 A 0
			VD72 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD72 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD72 A 0 A_GiveInventory("AlreadySpawned",1)
			VD72 A 0 A_GiveInventory("IsAFlyingActor")
			VD72 A 0
		Spawn2:
			TNT1 A 0 A_GiveInventory("IsAFlyingActor")
		Spawn3:
			VD72 A 0
			VD72 A 0 A_CustomMissile("MMRandomizer_OrbitComet", 44, 0, 0, 2, 0)
			VD72 A 0 A_CustomMissile("MMRandomizer_OrbitComet2", 44, 0, 180, 2, 0)
			VD72 AABB 4 Bright A_Look
			Goto Spawn+3
		Idle:
			VD72 ABCD 4 Bright A_Look
			Loop
		See:
			VD72 AABBCCDD 2 Bright A_Chase
			Loop
		Melee:
			TNT1 A 0 A_Jump(64, "Melee2")
			VD72 ST 6 Bright A_FaceTarget
			VD72 U 6 Bright A_CustomComboAttack("MMRandomizer_AfritBall", 44, 10, "Baron/Melee")
			Goto See
		Melee2:
			VD72 E 6 Bright A_FaceTarget
			VD72 F 6 Bright A_CustomMissile("MMRandomizer_HellFire", 44)
			VD72 G 6 Bright A_FaceTarget
			Goto See
		Missile:
			TNT1 A 0 A_Jump(64, "Missile3")
			TNT1 A 0 A_Jump(128, "Missile2")
			VD72 ST 6 Bright A_FaceTarget
			VD72 U 6 Bright A_CustomComboAttack("MMRandomizer_AfritBall", 44, 10, "Baron/Melee")
			Goto See
		Missile2:
			VD72 EF 6 Bright A_FaceTarget
			VD72 G 6 Bright A_CustomMissile("MMRandomizer_Comet", 44)
			Goto See
		Missile3:
			VD72 E 6 Bright A_FaceTarget
			VD72 F 6 Bright A_CustomMissile("MMRandomizer_Hellfire", 44)
			VD72 G 6 Bright A_FaceTarget
			Goto See
		Pain:
			VD72 H 4 Bright
			VD72 H 4 Bright A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			VD72 H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD72 H 16 A_Pain
			Goto See		
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_NoGravity
			VD72 I 6 Bright A_Scream
			VD72 J 5 Bright A_NoBlocking
			VD72 KLMNOPQR 4 Bright
			Stop
	}
}

Actor MMRandomizer_AfritBall
{
	Radius 6
	Height 8
	Speed 15
	Damage 8
	Projectile
	RenderStyle Add
	Alpha 0.8
	SeeSound "doom/Imp/Attack"
	DeathSound "doom/Imp/ShotX"
	Decal BaronScorch
	States
	{
		Spawn:
			VE27 AB 5 Bright
			Loop
		Death:
			VE27 CDE 6 Bright
			Stop
	}
}

Actor MMRandomizer_Comet
{
	Radius 6
	Height 8
	Speed 10
	Damage 5
	SpawnID 250
	Scale 0.55
	Projectile
	SeeSound "VDR/Afrit/CometFire"
	DeathSound "VDR/Afrit/CometHit"
	Decal Scorch
	States
	{
		Spawn:
			VE27 FFFFGGGGHHHH 1 Bright A_SpawnItemEx("MMRandomizer_CometTail", 0, 0, 0, 0, 0, 0, 0, 128)
			Loop
		Death:
			VE27 I 3 Bright A_SpawnItemEx("MMRandomizer_CometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
			VE27 J 3 Bright A_Explode(80, 80, 0)
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_CometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
			Stop
	}
}

Actor MMRandomizer_CometTail
{
	Projectile
	+NOCLIP
	RenderStyle Add
	Alpha 0.4
	States
	{
		Spawn:
			VE28 ABCDEFGHI 1 Bright
			Stop
	}
}

Actor MMRandomizer_CometDeathGlow : MMRandomizer_CometTail
{
	Scale 2.0
	Alpha 0.6
	States
	{
		Spawn:
			VE28 ABCDEFGHI 3 Bright
			Stop
	}
}

Actor MMRandomizer_CometDeath
{
	Projectile
	RenderStyle Add
	+NOCLIP
	Alpha 0.7
	States
	{
		Spawn:
			VE27 KLMN 3 Bright
			Stop
	}
}

Actor MMRandomizer_OrbitComet : MMRandomizer_Comet
{
	SeeSound ""
	+NOCLIP
	Scale 0.28
	Speed 5
	States
	{
		Spawn:
			VE27 FFGGHH 1 Bright A_SpawnItemEx("MMRandomizer_OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_CustomMissile("MMRandomizer_OrbitComet", 0, 0, 75)
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
			Stop
	}
}

Actor MMRandomizer_OrbitComet2 : MMRandomizer_OrbitComet
{
	States
	{
		Spawn:
			VE27 FFGGHH 1 Bright A_SpawnItemEx("MMRandomizer_OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
			TNT1 A 0 A_CustomMissile("MMRandomizer_OrbitComet2", 0, 0, -75)
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_OrbitCometTail", 0, 0, 0, 0, 0, 0, 0, 128)
			Stop
	}
}

Actor MMRandomizer_OrbitCometTail : MMRandomizer_CometTail
{
	Scale 0.5
}

Actor MMRandomizer_HellFire
{
	+NOBLOCKMAP
	+PAINLESS
	+BLOODLESSIMPACT
	+MISSILE
	+DROPOFF
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:
			TNT1 A 0 Bright A_CustomMissile("MMRandomizer_HellFire1", 0, 0, -180)
			TNT1 A 0 Bright A_CustomMissile("MMRandomizer_HellFire1", 0, 16, -175)
			TNT1 A 0 Bright A_CustomMissile("MMRandomizer_HellFire1", 0, 32, -170)
			TNT1 A 0 Bright A_CustomMissile("MMRandomizer_HellFire1", 0, -16, -185)
			TNT1 A 0 Bright A_CustomMissile("MMRandomizer_HellFire1", 0, -32, -190)
			TNT1 A 10 Bright A_PlaySound("VDR/Afrit/Hellfire")
			Stop
	}
}

Actor MMRandomizer_HellFire1
{
	+RIPPER
	+NOEXPLODEFLOOR
	+PAINLESS
	+BLOODLESSIMPACT
	+MISSILE
	+DROPOFF
	Speed 2
	States
	{
		Spawn:
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, -16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, -16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, -16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, -16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, 16, 0)
			TNT1 A 10 A_CustomMissile("MMRandomizer_HellFire2", 0, -16, 0)
			Stop
	}
}

Actor MMRandomizer_HellFire2
{
	+RIPPER
	+NOEXPLODEFLOOR
	+BLOODLESSIMPACT
	+DROPOFF
	+RANDOMIZE
	Scale 1.5
	Speed 1
	RenderStyle Add
	Alpha 0.67
	Damagetype "HellFire"
	Obituary "%o was scorched by an Afrit."
	States
	{
		Spawn:
			TNT1 A 8
			VE28 J 3 Bright
			VE28 KL 3 Bright A_Explode(3, 64)
			TNT1 A 0 A_Explode(3, 64)
			TNT1 A 0 A_Jump(191, 4)
			TNT1 A 0 A_CustomMissile("MMRandomizer_HellFire2", 0, 24, 0)
			TNT1 A 0 A_CustomMissile("MMRandomizer_HellFire2", 0, 0, 0)
			TNT1 A 0 A_CustomMissile("MMRandomizer_HellFire2", 0, -24, 0)
			VE28 MNOPQRSTUVWX 3 Bright
			Stop
	}
}