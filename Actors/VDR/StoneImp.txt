ACTOR MMRandomizer_StoneImp : SamsaraMonsterFlemoidMedium
{
	Health 180
	Radius 20
	Height 56
	Speed 6
	Mass 400
	Monster
	BloodType "StoneBitsSpawner"
	DamageFactor "Poison", 0
	DamageFactor "PoisonBolt", 0
	DamageFactor "PoisonCloud", 0
	DamageFactor "Electric", 0
	DamageFactor "ElectricBolt", 0
	DamageFactor "Lightning", 0
	DamageFactor "Fire", 0.5
	DamageFactor "Ice", 1.2
	RadiusDamageFactor 1.25
	SeeSound "doom/imp/sight"
	PainSound "doom/imp/pain"
	DeathSound "doom/imp/death"
	ActiveSound "doom/imp/active"
	Obituary "%o was smashed by a stone imp."
	Tag "Stone Imp"
	+FLOORCLIP
	+NOBLOODDECALS
	+NOPAIN
	States
	{
		Spawn:
			VD42 AB 10 A_Look
			Loop
		See:
			VD42 AABBCCDD 3 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			VD42 EF 8 A_FaceTarget
			VD42 G 6 A_CustomMeleeAttack(12 * random(1, 4), "VDR/stoneimp/hit", "", "Melee")
			Goto See
		Pain:
			VD42 H 2
			VD42 H 2 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			VD42 H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD42 H 16 A_Pain
			Goto See		
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD42 I 6
			VD42 J 6 A_Scream
			VD42 K 5
			VD42 L 5 A_Fall
			VD42 M 5
			VD42 N -1
			Stop
		Death.PainkillerStake:
			TNT1 A 0 ACS_NamedExecuteAlways("Painkiller_MonsterKill")
			TNT1 A 0 A_JumpIf(CallACS("Painkiller_IsItGibbed")==1,"PainkillerGib")
			TNT1 A 0 A_GiveInventory("Painkiller_RunStake",1)
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_FaceTarget
		StakeDeath:
			VD42 H 2
			Loop
		Raise:
			VD42 A 0
			"####" "#" 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			VD42 ML 8
			VD42 KJI 6
			Goto See
		ZorchRaise:
			"####" "#" 1 A_UnsetShootable
			"####" "#" 1 A_SpawnItem("SpawnFire2")
			"####" "#" 0 A_PlaySound("archvile/resummon")
			"####" "#" 1 A_TakeInventory("EnemyZorched",1)
			"####" "#" 0 A_SetTranslucent(0.1,0)
			"####" "#" 3 A_SetTranslucent(0.2,0)
			"####" "#" 3 A_SetTranslucent(0.3,0)
			"####" "#" 3 A_SetTranslucent(0.4,0)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 3 A_SetTranslucent(1,0)
			"####" "#" 1 A_SetShootable
			Goto See
	}
}

// Stone bits SFX - by Blue Shadow (the spawning code is based on Nash's nashgore code)

ACTOR MMRandomizer_StoneBitsSpawner : DoomBulletPuff_Samsara
{
	-ALLOWPARTICLES
	States
	{
		Spawn:
			TNT1 A 0
			Goto High
			TNT1 A 0
			Goto Medium
			TNT1 A 0
		Low:
			TNT1 AA 0 A_SpawnItemEx("MMRandomizer_StoneBitsSmall", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
			Goto SpawnPuff
		Medium:
			TNT1 AAA 0 A_SpawnItemEx("MMRandomizer_StoneBitsMedium", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
			Goto SpawnPuff
		High:
			TNT1 AAAAA 0 A_SpawnItemEx("MMRandomizer_StoneBitsLarge", 0, 0, 8, random(-4, 4), random(-4, 4), random(2, 5), 0, SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEMOMENTUM)
		SpawnPuff:
			PUFF A 4 Bright A_PlaySound("VDR/stoneimp/impact")
			PUFF ABCD 4
			Stop
	}
}

ACTOR MMRandomizer_StoneBitsBase
{
	Radius 2
	Height 2
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
		Fade:
			"####" "#" 1 A_FadeOut(0.25)
			Wait
	}
}

ACTOR MMRandomizer_StoneBitsSmall : MMRandomizer_StoneBitsBase
{
	States
	{
		Spawn:
			TNT1 AA 0 A_Jump(256, "SpawnA", "SpawnB")
		SpawnA:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
			VE08 A 10
			Goto Fade
			VE08 E 10
			Goto Fade
			VE08 I 10
			Goto Fade
			VE08 M 10
			Goto Fade
		SpawnB:
			TNT1 A 0 A_Jump(256, 1, 2)
			VE08 B 10
			Goto Fade
			VE08 F 10
			Goto Fade
	}
}

ACTOR MMRandomizer_StoneBitsMedium : MMRandomizer_StoneBitsBase
{
	States
	{
		Spawn:
			TNT1 AA 0 A_Jump(256, "SpawnA", "SpawnB", "SpawnC")
		SpawnA:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
			VE08 A 10
			Goto Fade
			VE08 E 10
			Goto Fade
			VE08 I 10
			Goto Fade
			VE08 M 10
			Goto Fade
		SpawnB:
			TNT1 A 0 A_Jump(256, 1, 2)
			VE08 B 10
			Goto Fade
			VE08 F 10
			Goto Fade
		SpawnC:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
			VE08 C 10
			Goto Fade
			VE08 G 10
			Goto Fade
			VE08 K 10
			Goto Fade
			VE08 O 10
			Goto Fade
	}
}

ACTOR MMRandomizer_StoneBitsLarge : MMRandomizer_StoneBitsBase
{
	States
	{
		Spawn:
			TNT1 AA 0 A_Jump(256, "SpawnB", "SpawnC", "SpawnD")
			SpawnB:
			TNT1 A 0 A_Jump(256, 1, 2)
			VE08 B 10
			Goto Fade
			VE08 F 10
			Goto Fade
		SpawnC:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
			VE08 C 10
			Goto Fade
			VE08 G 10
			Goto Fade
			VE08 K 10
			Goto Fade
			VE08 O 10
			Goto Fade
		SpawnD:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4)
			VE08 D 10
			Goto Fade
			VE08 H 10
			Goto Fade
			VE08 L 10
			Goto Fade
			VE08 P 10
			Goto Fade
	}
}