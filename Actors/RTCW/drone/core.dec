Actor RTCW_DroneBrain
{
	Height 48
	Radius 18
	Mass 500
	Health 150
	SeeSound "RTCW/Drone/Sight"
	DeathSound "RTCW/Drone/Death"
	Monster
	+NOPAIN
	+NOGRAVITY
	+NOBLOOD
	//+FLOAT this flag does nothing for a parts based enemy
	States
	{
		HoverIdle:
			"####" A 0 A_GiveToChildren("RTCWBroadCast_Drone_HoverIdle",1)
			"####" ABCDEFGHIJKLMNO 2 A_GiveInventory("RTCW_Drone_Sight",1)
			Loop
		See:
			"####" "#" 0 A_PlaySound("RTCW/Drone/Sight",CHAN_VOICE)
			"####" ABCDEFGHIJKLMNO 2 A_GiveInventory("RTCW_Drone_Float",1)
		HoverCombat:
			"####" "#" 0 A_JumpIfCloser(512,1)
			Goto HoverForward
			"####" "#" 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Drone_HoverIdle",1)
			"####" ABCDEFGHIJKLMNO 2 A_GiveInventory("RTCW_Drone_CombatSight",1)
			Loop
		HoverForward:
			"####" "#" 0 A_JumpIfCloser(512,"HoverCombat")
			"####" "#" 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			"####" "#" 0 A_GiveInventory("RTCW_Enemy_MoveOrder",1)
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Drone_HoverForward",1)
			"####" ABCDEFGHIJKLMNO 2 A_GiveInventory("RTCW_Drone_CombatSight",1)
			Loop
		Death:
			"####" "#" 0 A_Scream
			"####" "#" 0 A_StopSound(CHAN_7)
			"####" "#" 0 A_NoBlocking
			"####" "#" 17
			"####" "#" 0 A_GiveToChildren("RTCWRemoveChild",1)
			"####" "#" 0 A_SpawnItemEx("RTCW_PanzerfaustSmokeExplosion",0,0,0,frandom(-0.5,0.5),frandom(-0.5,0.5),1)
			"####" "#" 0 A_SpawnItemEx("RTCW_ExplosionSmokeDebris")
			"####" "#" 0 A_Quake(3,15,0,128)
			"####" "#" 0 A_Explode(120,120,XF_HURTSOURCE,1,0)
			"####" "#" 0 A_SpawnItemEx("RTCW_PanzerfaustRocketExplosion")
			TNT1 A 15
			Stop
	}
}

Actor RTCW_Drone_Weapon : Inventory { +INVENTORY.UNDROPPABLE Inventory.MaxAmount 3 Inventory.InterHubAmount 0 }

Actor RTCW_Drone_Float : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_ChangeVelocity(0,0,CallACS("RTCW_Decorate",36)/65536.0,CVF_REPLACE)
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,15,0,0,-frandom(2,3))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,15,0,0,-frandom(2,3))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,8,0,0,-frandom(4,6))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,8,0,0,-frandom(4,6))
			Stop
	}
}

Actor RTCW_Drone_Sight : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_ChangeVelocity(0,0,CallACS("RTCW_Decorate",36)/65536.0,CVF_REPLACE)
			TNT1 A 0 A_LookEx(0,0,4096,0,180)
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,15,0,0,-frandom(2,3))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,15,0,0,-frandom(2,3))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,8,0,0,-frandom(4,6))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,8,0,0,-frandom(4,6))
			Stop
	}
}

Actor RTCW_Drone_CombatSight : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfTargetInLOS(1,180,JLOSF_DEADNOJUMP,2048)
			Goto Pickup+2
			TNT1 A 0 A_ChangeVelocity(0,0,CallACS("RTCW_Decorate",36)/65536.0,CVF_REPLACE)
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate",12)==1,"Dismiss")
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_JumpIfTargetInLOS("DroneAttack",180,JLOSF_DEADNOJUMP,2048)
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,15,-frandom(1,1.5),0,-frandom(1,1.5))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,15,-frandom(1,1.5),0,-frandom(1,1.5))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,15,15,-frandom(2,3),0,-frandom(2,3))
			TNT1 A 0 A_SpawnItemEx("RTCW_DroneSmoke",-8,-15,15,-frandom(2,3),0,-frandom(2,3))
			TNT1 A 0 A_JumpIfInventory("RTCW_Enemy_MoveOrder",1,"MoveForward")
			Stop
		MoveForward:
			TNT1 A 0 A_ChangeVelocity(3,0,velz,CVF_RELATIVE|CVF_REPLACE)
			Stop
		DroneAttack:
			TNT1 A 0 A_JumpIfInventory("RTCW_Drone_Weapon",3,"Tesla")
			TNT1 A 0 A_JumpIfInventory("RTCW_Drone_Weapon",2,"Flamethrower")
			TNT1 A 0 A_JumpIfInventory("RTCW_Drone_Weapon",1,"MachineGun")
			Goto Pickup+5
		MachineGun:
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_GiveInventory("RTCW_Drone_MG42",1)
			TNT1 A 0
			Goto Pickup+5
		Flamethrower:
			TNT1 A 0 A_GiveInventory("RTCW_Drone_Flamethrower",1)
			Goto Pickup+5
		Tesla:
			//TNT1 A 0 A_GiveInventory("RTCW_Drone_MG42",1)
			Goto Pickup+5
		Dismiss:
			TNT1 A 0 A_ClearTarget
			TNT1 A 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyState",13)
			Stop
	}
}

Actor RTCW_Drone_MG42 : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_GiveToChildren("RTCW_Drone_FlashChild",1)
			TNT1 A 0 A_SpawnItemEx("RTCW_VenomSmoke",-8,18,0,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.5,1.5))	
			TNT1 A 0 A_PlaySound("RTCW/Drone/MG42",CHAN_WEAPON)
			TNT1 AA 0 A_CustomMissile("RTCW_CasingBig",0,18,-random(85,105),CMF_AIMDIRECTION)
			TNT1 AA 0 A_CustomMissile("RTCW_EnemyVenomTracer",0,18,frandom(-8.0,8.0),CMF_AIMDIRECTION,frandom(-8.0,8.0))	
			Stop
	}
}

Actor RTCW_Drone_Flamethrower : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FaceTarget(5,5)
			TNT1 A 0 A_CustomMissile("RTCW_Drone_FlamethrowerFire",0,10,angle+(CallACS("RTCW_Decorate",30)/16384.0),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,pitch+(CallACS("RTCW_Decorate",30)/32768.0))				Stop
	}
}

Actor RTCW_Drone_FlamethrowerFire : RTCW_FlamethrowerFire
{
	States
	{
		Spawn:
			TNT1 A 2
			Goto Death	
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_FlamethrowerStreamRotate",16,0,2)
			TNT1 A 1
			stop
	}
}

Actor RTCW_Drone_FlashChild : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_Decorate",37)
			Stop
	}
}

Actor RTCW_DroneSmoke : RTCW_SmokePuff
{
	Scale 0.667
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			RW00 AAAAAAAAAA 1 A_Fadeout(0.1)
			Stop
	}
}

Actor RTCW_Gewehrdrohne : RTCW_DroneBrain
{
	DamageFactor "Fire", 0.0
	DamageFactor "RTCWFire", 0.0
	PainChance "Fire", 0
	PainChance "RTCWFire", 0
	Tag "Gewehrdrohne"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_Drone",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyPartsPicker",13)
			TNT1 A 0 A_PlaySound("RTCW/Drone/Loop",CHAN_7,1.0,1)
			TNT1 A 0 A_GiveInventory("RTCW_Drone_Weapon",1)
			TNT1 A 2
			Goto HoverIdle
	}
}

Actor RTCW_Feuerdrohne : RTCW_DroneBrain
{
	DamageFactor "Fire", 0.0
	DamageFactor "RTCWFire", 0.0
	PainChance "Fire", 0
	PainChance "RTCWFire", 0
	Tag "Feuerdrohne"
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_Drone",1)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyPartsPicker",13)
			TNT1 A 0 A_PlaySound("RTCW/Drone/Loop",CHAN_7,1.0,1)
			TNT1 A 0 A_GiveInventory("RTCW_Drone_Weapon",2)
			TNT1 A 2
			Goto HoverIdle
	}
}