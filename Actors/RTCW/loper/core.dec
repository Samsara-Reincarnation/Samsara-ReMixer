Actor RTCW_LoperBrain
{
	Height 48
	Radius 18
	Mass 2500
	Monster
	DeathSound "RTCW/Loper/Death"
	Tag "Loper"
	+NOPAIN
	+NODROPOFF
	States
	{
		TorsoIdle:
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_TorsoIdle",1)
			"####" ABCDEFGHIJKLMNOPQRSTU 2 A_GiveInventory("RTCW_Loper_Sight",1)
			Loop
		See:
		Combat:
			"####" A 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			"####" A 0 A_JumpIfTargetInLOS("TorsoAttack5",0,JLOSF_DEADNOJUMP,200)
			"####" A 0 A_JumpIfTargetInLOS("TorsoLeap",180,JLOSF_DEADNOJUMP,400)
			"####" A 0 A_Jump(224,"CombatRun")
			"####" A 0 A_GiveToChildren("RTCWBroadCast_Loper_TorsoIdle",1)
		CombatIdle:
			"####" ABCDE 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			"####" A 0 A_JumpIfTargetInLOS("TorsoAttack5",0,JLOSF_DEADNOJUMP,200)
			"####" A 0 A_JumpIfTargetInLOS("TorsoLeap",180,JLOSF_DEADNOJUMP,400)
			"####" FGHIJ 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			"####" A 0 A_JumpIfTargetInLOS("TorsoAttack5",0,JLOSF_DEADNOJUMP,200)
			"####" A 0 A_JumpIfTargetInLOS("TorsoLeap",180,JLOSF_DEADNOJUMP,400)
			"####" KLMNO 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			"####" A 0 A_JumpIfTargetInLOS("TorsoAttack5",0,JLOSF_DEADNOJUMP,200)
			"####" A 0 A_JumpIfTargetInLOS("TorsoLeap",180,JLOSF_DEADNOJUMP,400)
			"####" PQRSTU 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			Goto Combat
		CombatRun:
			"####" A 0 A_GiveInventory("RTCW_Enemy_MoveOrder",1)
			"####" A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE|CVF_REPLACE)
			"####" A 0 A_GiveToChildren("RTCWBroadCast_Loper_LegsRun",1)
			"####" ABCDEFGH 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			"####" A 0 A_JumpIfTargetInLOS("TorsoAttack5",0,JLOSF_DEADNOJUMP,200)
			"####" A 0 A_JumpIfTargetInLOS("TorsoLeap",180,JLOSF_DEADNOJUMP,400)
			"####" IJKLMNOP 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			Goto Combat
		CombatTurn:
			"####" A 0 A_GiveToChildren("RTCWBroadCast_Loper_LegsTurn",1)
			"####" ABCDEFGHIJKLMNOPQRSTU 2 A_GiveInventory("RTCW_TurnLeft")
			"####" A 0 A_Warp(AAPTR_Default,2,0,0,0,WARPF_TESTONLY,"CombatRun")
			Loop
		TorsoLeap:
			"####" "#" 0 A_Jump(128,"TorsoAttack4")
		TorsoAttack3:
			"####" "#" 0 A_ChangeVelocity(20,0,4,CVF_RELATIVE|CVF_REPLACE)
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_TorsoAttack4",1)
			"####" ABCDEFGHIJ 2 A_GiveInventory("RTCW_Loper_ZapForward",1)
			Goto Combat
		TorsoAttack4:
			"####" "#" 0 A_ChangeVelocity(20,0,4,CVF_RELATIVE|CVF_REPLACE)
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_TorsoAttack4",1)
			"####" "ABCDE" 2 A_GiveInventory("RTCW_Loper_ZapForward",1)
			"####" FGHIJKLMNOPQRSTU 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			Goto Combat
		TorsoAttack5:
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_TorsoAttack5",1)
			"####" ABCDEFG 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			"####" "HIJKLMNOPQRSTUVWXYZ[\]" 2 A_GiveInventory("RTCW_Loper_ZapGround",1)
			"####" ABA 2 A_GiveInventory("RTCW_Loper_ZapGround",1)
			"####" "]\[ZYXWVUTSRQPONMLKJIH" 2 A_GiveInventory("RTCW_Loper_ZapGround",1)
			"####" GFEDCBA 2 A_GiveInventory("RTCW_Loper_CombatSight",1)
			Goto Combat
		Death:
			"####" "#" 0 A_Scream
			"####" "#" 0 A_Jump(256,"Death1","Death2","Death3")
		Death1:
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_Death1",1)
			"####" "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			"####" ABC 2
			"####" D 0 A_KillMaster
			"####" D -1
			Stop
		Death2:
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_Death2",1)
			"####" "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]" 2
			"####" A 0 A_KillMaster
			"####" A -1
			Stop
		Death3:
			"####" "#" 0 A_GiveToChildren("RTCWBroadCast_Loper_Death3",1)
			"####" "ABCDEFGHIJKLMNOPQRSTUVWXYZ[\" 2
			"####" "]" 0 A_KillMaster
			"####" "]" -1
			Stop
	}
}

Actor RTCW_Loper_Sight : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_LookEx(0,0,4096,0,180)
			TNT1 A 0 A_CustomMissile("RTCW_LoperTeslaProjectile",0,0,random(0,360),CMF_AIMDIRECTION,random(-15,45))
			Stop
	}
}

Actor RTCW_Loper_CombatSight : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIf(CallACS("RTCW_Decorate",12)==1,"Dismiss")
			TNT1 A 0 A_JumpIfTargetInLOS(1,0,JLOSF_DEADNOJUMP,2048)
			Goto Pickup+3
			TNT1 A 0 A_FaceTarget(15)
			TNT1 A 0 A_CustomMissile("RTCW_LoperTeslaProjectile",0,0,random(0,360),CMF_AIMDIRECTION,random(-15,45))
			TNT1 A 0 A_JumpIfInventory("RTCW_Enemy_MoveOrder",1,"MoveForward")
			Stop
		MoveForward:
			TNT1 A 0 A_Warp(AAPTR_Default,2,0,0,0,WARPF_TESTONLY,1)
			Goto CombatStand
			TNT1 A 0 A_FaceTarget(random(1,15))
			TNT1 A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE|CVF_REPLACE)
			Stop
		CombatStand:
			TNT1 A 0 A_SpawnItemEx("RTCW_DoorUser",radius,0,0,0,0,0,angle,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_DEADNOJUMP,2048)
			Goto CombatTurn
			TNT1 A 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyState",13,1)
			Stop
		CombatTurn:
			TNT1 A 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyState",13,2)
			Stop
		Dismiss:
			TNT1 A 0 A_ClearTarget
			TNT1 A 0 A_TakeInventory("RTCW_Enemy_MoveOrder",8)
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyState",13)
			Stop
	}
}

Actor RTCW_Loper_ZapForward : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_PlaySound("RTCW/Loper/Electrocute",CHAN_WEAPON)
			TNT1 AA 0 A_CustomMissile("RTCW_LoperTeslaProjectile2",0,0,random(0,360),CMF_AIMDIRECTION,random(-45,45))
			TNT1 A 0 A_CustomMissile("RTCW_LoperTeslaProjectile",0,0,random(0,360),CMF_AIMDIRECTION,random(-105,105))
			Stop
	}
}

Actor RTCW_Loper_ZapGround : RTCWTrigger
{
	States
	{
		Pickup:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Jump(128,2)
			TNT1 A 0 A_PlaySound("RTCW/Loper/Electrocute",CHAN_WEAPON)
			TNT1 AA 0 A_CustomMissile("RTCW_LoperTeslaProjectile2",0,0,random(0,360),CMF_AIMDIRECTION,random(-45,45))
			TNT1 A 0 A_CustomMissile("RTCW_LoperTeslaProjectile",0,0,random(0,360),CMF_AIMDIRECTION,random(-15,45))
			Stop
	}
}

Actor RTCW_LoperTeslaProjectile : FastProjectile
{
	Speed 16
	Damage (0)
	DamageType "RTCWTesla"
	Radius 2
	Height 4
	+THRUACTORS
	+SEEKERMISSILE
	+SCREENSEEKER
	+SKYEXPLODE
	+DONTSPLASH
	+CANNOTPUSH
	+BLOODLESSIMPACT
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			Goto Death	
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_TeslaArc",36,12,2)
			TNT1 A 1
			stop
	}
}

Actor RTCW_LoperTeslaProjectile2 : RTCW_LoperTeslaProjectile
{
	Speed 50
	Damage (10)
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AA 1 A_SeekerMissile(60,90,SMF_LOOK,256,8)
			Goto Death	
		Death:
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_TeslaArc",72,4,3)
			TNT1 A 1
			stop
	}
}

Actor RTCW_LoperTeslaSegment
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 1.0
	States
	{
		Spawn:
			TNT1 A 0
			RW00 ABCDEFGH 1 Bright A_Fadeout(0.05)
			stop
	}
}

Actor RTCW_Loper_Gib_Skull : RTCW_Gib {}
Actor RTCW_Loper_Gib_Abdomen : RTCW_Gib {}
Actor RTCW_Loper_Gib_Intestine : RTCW_Gib {}
Actor RTCW_Loper_Gib_Forearm : RTCW_Gib {}
Actor RTCW_Loper_Gib_Leg : RTCW_Gib {}
Actor RTCW_Loper_Gib_Foot : RTCW_Gib {}

Actor RTCW_Loper : RTCW_LoperBrain
{
	Health 500
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("RTCW_Loper")
			TNT1 A 0 ACS_NamedExecuteWithResult("RTCW_EnemyPartsPicker",14)
			TNT1 A 2
			Goto TorsoIdle
	}
}