ACTOR RobotGuard {
   Health 125
   Radius 20
   Height 48
   Speed 3
   Mass 5000
   PainChance 0
   MONSTER   
   +NOBLOOD
   +FLOORCLIP
   +NOPAIN
   +BOSSDEATH
   +NOINFIGHTING
   +NOICEDEATH
   Species "Enemies"
   SCALE 0.65
   SeeSound "Robot/see"
   DeathSound "Robot/die"
   Obituary "%o was blown up by a Patrol Robot."
   DamageFactor "normal", 0.5
   	DamageFactor "HLBullet", 0.5
	DamageFactor "GEBullet", 0.5
   DamageFactor "ROBTtMissile", 0.0
   Tag "Patrol Robot"   
   RadiusDamageFactor 12.0
   States {
		Spawn:
			ROBT A 0
			ROBT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			ROBT A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			ROBT A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			ROBT A 10 A_Look
			Loop
		Idle:
			ROBT A 1 A_Wander
			TNT1 A 0 A_Look
			Loop
		See:
			ROBT A 1 A_PlaySound("Robot/move",CHAN_VOICE,1.0,1,ATTN_NORM)
		SeeLoop:	   
			ROBT AAAAAAAAAAAAAAAA 1 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_StopSound(CHAN_VOICE)
			ROBT A 8 A_FaceTarget
		KeepFiring:
			ROBT A 8 A_FaceTarget
			TNT1 A 0 A_PlaySound("Robot/fire")
			ROBT A 8 A_CustomMissile("RobotMissile", 26, 0, 0)
			ROBT AAAAAAAAAAAAAAAAAAAAAAA 1  A_SetAngle(angle + 15)
			TNT1 A 0 A_GiveInventory("AttackDecider_3", 1)
			ROBT A 1 A_SetAngle(angle + 15)
			TNT1 A 0 A_JumpIfInventory("DoAttack", 1, "Repeat")
			Goto See
		Repeat:
			TNT1 A 0 A_TakeInventory("DoAttack", 1)
			TNT1 A 0 A_JumpIf(1 == ACS_NamedExecuteWithResult("RoTT808", 0), "See")
			Goto KeepFiring
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 A_KillMaster			
			"####" "#" 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
			"####" "#" 3 A_SpawnItem("ZorchEffectSmall")
			"####" "#" 0 A_GiveInventory("EnemyZorched",1)
			"####" "#" 3 A_SetTranslucent(0.9,0)			
			"####" "#" 0 A_Scream
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 3 A_SetTranslucent(0.4,0)
			"####" "#" 3 A_SetTranslucent(0.3,0)
			"####" "#" 3 A_SetTranslucent(0.2,0)
			"####" "#" 3 A_SetTranslucent(0.1,0) 
			TNT1 A 1 A_SetTranslucent(1,0)
			stop
		Death:   
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_VOICE)   
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("ash1", 0.0, 0.0, 16.0, random(5.0,10.0), random(5.0,10.0), random(3.0,8.0), random(0,360))
			TNT1 A 0 A_Scream
			ROBT B 3 A_Fall
			ROBT CDEFGHIJ 3 
			ROBT K -1 A_KillMaster
			Stop  
		Death.Vodoo:
		Death.VoodooPuff:
		Death.DynamiteCoop:
		Death.Dynamite:
		Death.CalebNapalm:
		Death.CalebFire:	  
		Death.Shock:	  
		Death.LifeLeech:	  
		Death.Tesla:
		Death.CalebBullet:	  
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			TNT1 A 0
			Goto Death
		Death.EradExplosive:
		Death.EradNova:
		Death.EradShock:
		Death.EradDisc:
		Death.EradPiercingDart:
		Death.EradExplosive2:	  
		EleenaDeath: 
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
			TNT1 A 0
			Goto Death
		Death.DukeDamage:
		Death.DukePistol:
		Death.DukeIce:		
		Death.DukeExplosive:		
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeRobotKill",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death
		Death.PSRAPower:
		Death.PSRAPowerCoop:
		Death.PSExplosive:
		Death.PSExplosiveCoop:
		Death.PSBullet:
		Death.PSFire:	
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("JonDMTaunt1", 1)
			Goto Death
		Death.LeonardDamage2:
		Death.LeonardDamage3:
		Death.LeonardDamage0:
		Death.LeonardDamage1:	
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("LeonardDMTaunt1", 1)
			Goto Death
		Death.SWFist:
		Death.SWKatana:
		Death.SWShuriken:
		Death.SWSticky:
		Death.SWStickyCoop:		
		Death.SWGrenade:
		Death.SWNuke:
		Death.SWRocket:
		Death.SWRailgun:
		Death.SWRailgunCoop:
		Death.SWNukeCoop:
		Death.SWFlame:
		Death.SWFlame2:
		Death.SWFlame2Coop:
		Death.SWFlame3:	
		Death.SWBullet:	  
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
			goto Death
		Death.RottDisintegrate:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Pain
			"####" "#" 0 A_NoBlocking	
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_SpawnItem("RGodModeDeathSmall")
			"####" "#" 0 A_KillMaster			
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			stop
		Disintegrate:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_ChangeFlag("SOLID",FALSE)
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_Stop
			"####" "#" 3 A_SpawnItem("DisintEffectSmall",0,10)
			"####" "#" 0 A_KillMaster			
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			stop
		Death.Nuke:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking		
			"####" "#" 0 A_KillMaster			
			"####" "#" 0 A_SpawnItem("WTNukeDeathSmall")
			"####" "#################" 3 A_Fadeout(0.05)
			stop				
	}
}

actor RobotMissile {
    Radius 8
    Height 6
    Speed 24
    Damage (0)
    SCALE 0.55
    PROJECTILE
    +EXTREMEDEATH
    +BRIGHT
    damagetype "ROBTtMissile"
    DeathSound "gen/expl2"
    States {
    Spawn:  
        BSTA ABCD 2 Bright
        Loop
    Death:
        EXPL AAB 1 Bright
		TNT1 A 0 A_RadiusThrust(1000, 64)
		EXPL BCC 1 Bright
		TNT1 A 0 A_Explode(50, 64, 0)
		EXPL DDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTT 1 Bright
        Stop
    }
}