Script "RoTTKrist" (void) //800
{
	int thisactor = ActivatorTID();
	SetActivatorToTarget(0);
	int target = ActivatorTID();
	int xdiff = GetActorX(target) - GetActorX(thisactor);
	int ydiff = GetActorY(target) - GetActorY(thisactor);
	int faceangle = VectorAngle(xdiff, ydiff);
	int angdiff = (GetActorAngle(thisactor) - faceangle) >> 8;
	
	if(angdiff > 12)
		SetResultValue(true);
	else if(angdiff < -12)
		SetResultValue(false);
	else
		GiveActorInventory(thisactor, "ChargeSignal", 1);
	SetResultValue(false);
}

Script "RoTTBossScript" (int basetid, int type) //801
{
	switch(basetid)
	{
		case KRIST_BASE:
			if(!type)
			{
				//What the hell is this? You should NEVER EVER assign arbitrary tids to monsters at run time!
				//Thing_ChangeTID(0, basetid + krist_id);
				krist_id++;
			}
			else if(type == 1)
				AmbientSound("Krist/See", 127);
			else
			{
				SetActivatorToTarget(0);
				GiveInventory("RoTT_BossDeathAnnouncer_Krist", 1);
			}	
		break;
		case NME_BASE:
			if(!type)
			{
				//Thing_ChangeTID(0, basetid + nme_id);
				nme_id++;
			}
			else if(type == 1)
				AmbientSound("NME/See", 127);
			else {
				SetActivatorToTarget(0);
				GiveInventory("RoTT_BossDeathAnnouncer_NME", 1);
			}	
		break;
		case TOM_BASE:
			SetActivatorToTarget(0);
			GiveInventory("RoTT_BossDeathAnnouncer_Tom", 1);
		break;
		case TOMSNAKE_BASE:
			SetActivatorToTarget(0);
			GiveInventory("RoTT_BossDeathAnnouncer_TomSnake", 1);
		break;
		case BIGJOHN_BASE:
			SetActivatorToTarget(0);
			GiveInventory("RoTT_BossDeathAnnouncer_BigJohn", 1);
		break;
	}
}

// 802 is used by Half Life class

Script "RoTT808" (void) //808
{
    SetActivatorToTarget(0);
	if(GetActorProperty(0, APROP_HEALTH) <= 0)
	  SetResultValue(1);
	else
	  SetResultValue(0);
}

Script "RoTTKristAngle" (void) //809
{
	int thisactor = ActivatorTID();
	SetActivatorToTarget(0);
	int target = ActivatorTID();
	int xdiff = GetActorX(target) - GetActorX(thisactor);
	int ydiff = GetActorY(target) - GetActorY(thisactor);
	int faceangle = VectorAngle(xdiff, ydiff) - GetActorAngle(thisactor);
	
	SetActorAngle(thisactor, faceangle + 0.5);
}

Script "RoTTBossDeathBroadcast" (int id, int mode) CLIENTSIDE //810
{
	if(!mode) {
		switch (id) {
			case KRIST_BASE:
				Log(s:"\cfKrist Defeated!\c-");
			break;
			case NME_BASE:
				Log(s:"\cfNME Defeated!\c-");
			break;
			case TOM_BASE:
				Log(s:"\cfEl Oscuro Defeated!\c-");
			break;
			case TOMSNAKE_BASE:
				Log(s:"\cfOscuro Defeated!\c-");
			break;
			case BIGJOHN_BASE:
				Log(s:"\cfBig John Defeated!\c-");
			break;
		}
	}
	else
		LocalAmbientSound("RoTT/BossDeath", 35);
}

Script "RoTTOscuroModeSwitch" (void) //811
{
	if(CheckInventory("OscuroVulnerable"))
		terminate;
	GiveInventory("OscuroVulnerable", 1);
	
	for(int i = 0; i < OSCURO_MODE_TIME; i++)
		Delay(35);
	TakeInventory("OscuroVulnerable", 1);
}

Script "RoTT814" Respawn //814
{
	TakeInventory("NoWeapon", 1);
	HudMessage(s:"";0,602,0,107.0,100.0,0);
}

Script "LightningGuardSteal" (int action) // Lightning Guard steal script. Trust me, it's better this way
{
	Switch(action)
	{
	Case 1: // check to steal
		SetActivator(0,AAPTR_TARGET);
		if(GetCVar("samsara_moddedgame"))
		{
			SetResultValue(0);
			break;
		}
		for(int i = 0; i < CLASSCOUNT; i++)
		{
			if(CheckInventory(gameClassesSTR[i]) > 0)
			{
				for(int j = 0; j < 10; j++)
				{
					if(CheckInventory(ClassWeapons[i][j]) == true)
					{
						SetResultValue(1);
						break;
					}
					else
					{
						SetResultValue(0);
					}
				}
			}
		}
		break;
	Case 2: //stealing mechanic
		SetActivator(0,AAPTR_TARGET);
		for(int k = 0; k < CLASSCOUNT; k++)
		{
			if(CheckInventory(gameClassesSTR[k]) > 0)
			{
				for(int l = 9; l > -1; l--)
				{
					//print(d:l);
					if(CheckInventory(ClassWeapons[k][l]) == true)
					{
						//print(s:gameClassesSTR[k],s:" ",s:ClassWeapons[k][l]);
						int result = (l+1);
						int dummy = (l-3);
						TakeInventory(SlotDummies[dummy],1);
						TakeInventory(ClassWeapons[k][l], 1);
						SetResultValue(result);
						break;
					}
					else
					{
						//print(s:"Failure");
						SetResultValue(0);
					}
				}
			}
		}
		break;
	Case 3: //death
		if(CheckInventory("StoleWeapon")-1 >= 0)
			SpawnSpotForced(StoleDummies[CheckInventory("StoleWeapon")-1],0,0,0);
		break;
	}
}

Script "LightningGuardDeadDrop" (void)
{
	until(ClassifyActor(0) & ACTOR_DEAD) { Delay(1); }
	
	if(CheckInventory("StoleWeapon")-1 >= 0)
		SpawnSpotForced(StoleDummies[CheckInventory("StoleWeapon")-1],0,0,0);
}
