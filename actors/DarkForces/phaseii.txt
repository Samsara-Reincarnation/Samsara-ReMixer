//===========================================
//@author:	Ryan Cordell
//@desc:	Phase II Dark Trooper
//===========================================

//Assault cannon spamming barstids

ACTOR phase2_DF : SamsaraMonsterRobotic
{
	Health		 750
	Radius		 20
	Height		 54
	Speed		 10
	Mass		 400
	Scale		 0.7
	Tag 		 "Phase II"	
	Damagefactor "Falling", 0
	SeeSound	 "enemies/phase2/see"
	PainSound	 "enemies/phase2/pain"
	DeathSound	 "enemies/phase2/death"
	obituary 	 "%o was destroyed by a Dark Trooper Phase 2."
	PainChance 	 "ShrinkerDMG", 0
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0	
	DamageFactor "PoisonBolt", 0.0
	DamageFactor "ElectricBolt", 2.0
	+DROPOFF
	+NOBLOCKMONST
	-NODROPOFF
	+MISSILEMORE
	+DONTRIP	
	+NOBLOOD
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			PHA2 A 0
			PHA2 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PHA2 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			PHA2 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PHA2 A 5 A_Look
			Loop
		See:
			PHA2 A 0 A_ChangeFlag("NOGRAVITY",0)
			PHA2 U 0 A_ChangeFlag("FLOAT",0)			
			TNT1 A 0 A_Jump(10,"JetPack")
			PHA2 AAA 2 A_Chase("", "")
			PHA2 BBB 2 A_Chase
			PHA2 CCC 2 A_Chase("", "")
			PHA2 DDD 2 A_Chase
			PHA2 EEE 2 A_Chase("", "")
			PHA2 FFF 2 A_Chase
			Goto See+2
		JetPack:
		    TNT1 A 0 A_ChangeFlag("FLOAT", 1)
            TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)		
			TNT1 A 0 ThrustThingZ(0,12,0,0)			
			PHA2 RRRSSSTTTUUU 3 A_Chase("","")
		Flying:
			PHA2 UUUUU 1 A_Chase("","MFlying")
		    TNT1 A 0 A_JumpIf(z - floorz < 24, "Landing")			
			Loop
		Landing:
		    TNT1 A 0 A_JumpIf(z - floorz > 25, "Flying")					
			PHA2 VVV 1 A_Chase("","MLanding")
			TNT1 A 0 A_JumpIf(z <= floorz+4,"Land")
			Loop
		Land:
			PHA2 V 0 A_ChangeFlag("NOGRAVITY",0)
			PHA2 U 0 A_ChangeFlag("FLOAT",0)						
			PHA2 VVV 1 A_Chase("","")
			Goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PHA2 F 0 A_Jump(20,"MRocket")
			PHA2 FF 4 A_FaceTarget
			PHA2 G 8 BRIGHT A_CustomMissile("plasma_shot_2",43)
			PHA2 G 0 A_PlaySound("katarn/assshot",CHAN_AUTO,1.0,0,0.45)
			PHA2 G 1 A_Jump(200,"See")
			TNT1 A 0 A_CPosRefire
			Goto Missile+2
		MFlying:
			PHA2 U 0 A_Jump(20,"RocketFlying")
			PHA2 UU 4 A_FaceTarget
			PHA2 U 8 BRIGHT A_CustomMissile("plasma_shot_2",43)
			PHA2 U 0 A_PlaySound("katarn/assshot",CHAN_AUTO,1.0,0,0.45)
			PHA2 U 1 A_Jump(200,"Flying")
			TNT1 A 0 A_CPosRefire
			Goto MFlying+2
		MLanding:
			PHA2 V 0 A_Jump(20,"RocketLanding")
			PHA2 VV 4 A_FaceTarget
			PHA2 V 8 BRIGHT A_CustomMissile("plasma_shot_2",43)
			PHA2 V 0 A_PlaySound("katarn/assshot",CHAN_AUTO,1.0,0,0.45)
			PHA2 V 1 A_Jump(200,"Landing")
			TNT1 A 0 A_CPosRefire
			Goto MLanding+2
		MRocket:
			PHA2 HH 4 A_FaceTarget
			PHA2 I 8 BRIGHT A_CustomMissile("assault_missile_2",43,0,random(-1,1),32,random(-1,1))
			PHA2 I 0 A_PlaySound("katarn/asssho2",CHAN_AUTO,1.0,0,0.45)
			PHA2 JK 8
			Goto See
		RocketFlying:
			PHA2 UU 4 A_FaceTarget
			PHA2 U 8 BRIGHT A_CustomMissile("assault_missile_2",43,0,random(-1,1),32,random(-1,1))
			PHA2 U 0 A_PlaySound("katarn/asssho2",CHAN_AUTO,1.0,0,0.45)
			PHA2 U 16
			Goto Flying
		RocketLanding:
			PHA2 VV 4 A_FaceTarget
			PHA2 V 8 BRIGHT A_CustomMissile("assault_missile_2",43,0,random(-1,1),32,random(-1,1))
			PHA2 V 0 A_PlaySound("katarn/asssho2",CHAN_AUTO,1.0,0,0.45)
			PHA2 V 16
			Goto Landing
		Pain:
			PHA2 L 3
			PHA2 M 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			PHA2 M 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			PHA2 M 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			PHA2 M 4 A_Scream
			PHA2 N 4 A_Gravity
			PHA2 O 4 A_Fall
			PHA2 P 4 
			PHA2 Q -1 A_KillMaster
			Stop
	}
}

Actor plasma_shot_2 : plasma_shot { Speed 18 Damage (Random(12,23)) }

ACTOR assault_missile_2 : assault_missile
{
  Speed   15
  Damage  (10)
  Species "Enemies"
  DONTHURTSHOOTER  
  +DONTHARMCLASS
  -FORCERADIUSDMG
  States
  {
    Death:
      TNT1 A 0 A_PlaySound("katarn/exmiss",CHAN_VOICE)
      TNT1 A 0 A_SpawnItemEx("assmissile_explosion",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
      TNT1 A 1 A_Explode(random(40,54),130)
      stop
  }
}
