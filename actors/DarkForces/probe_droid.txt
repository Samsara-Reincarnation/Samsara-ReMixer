//===========================================
//@author:	Ryan Cordell
//@desc:	Probe Droid
//===========================================

ACTOR probe_droid : SamsaraMonsterRobotic
{
	Health		     375
	Radius			 20
	Height			 46
	Mass			 400
	Painchance		 75
	Speed			 6
	SeeSound		 "enemies/pdroid/see"
	ActiveSound		 "enemies/pdroid/see"
	DeathSound		 "probalm1"
	PainSound		 "probalm2"
	obituary 		 "%o was shot down by a probe droid."
	PainChance 		 "ShrinkerDMG", 0
	MinMissileChance 255
	Tag 			 "Probe Droid"	
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0	
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	Scale				0.8
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			PROB A 0
			PROB A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PROB A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			PROB A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PROB AABBCCDDEEFFGGHHIIJJKKLL 2 A_Look
			PROB KKJJIIHHGGFFEEDDCCBB 2 A_Look
			Loop
		See:
			PROB AABBCCDDEEFFGGHHIIJJKKLL 2 A_Chase
			PROB KKJJIIHHGGFFEEDDCCBB 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PROB L 7 A_FaceTarget
			PROB M 0 A_PlaySound("enemies/pdroid/fire",CHAN_AUTO,1.0,0,ATTN_NORM)
			PROB M 5 BRIGHT A_CustomMissile("bryar_laser2_DFB",44,random(-1,1),random(-2,2),32,random(-2,2))
			Goto See
		Pain:
			PROB N 3
			PROB N 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			PROB N 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			PROB N 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			TNT1 A 0 A_PlaySound("probalm",CHAN_BODY,1.0,0,ATTN_NORM)
			PROB N 5 A_Scream
			PROB O 5 A_ChangeFlag("SOLID",false)
			PROB P 5 A_NoBlocking
			PROB QR 5
			TNT1 A 0 A_PlaySound("katarn/exmort",CHAN_WEAPON,1.0,0,ATTN_NORM)
			PROB S 1 A_SpawnItemEx("explosion_probe",0,0,0)
			PROB S -1
			Stop
	}
}

ACTOR explosion_probe
{
  Radius  6
  Height  6
  Scale   1.0
  YScale  0.75
  +NOGRAVITY
  +CLIENTSIDEONLY
  States
  {
    Spawn:
      PROB T 2 BRIGHT
      PROB U 2 BRIGHT
      PROB VWXYZ 3 BRIGHT
      PROB "[" 2 BRIGHT
	  PROB "]" 2 BRIGHT
      Stop
  }
}