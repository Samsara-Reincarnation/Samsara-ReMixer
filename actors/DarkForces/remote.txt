ACTOR DFRemote : SamsaraMonsterRobotic
{
	Health			45
	Radius			10
	Height			24
	Mass			40
	Painchance		85
	Speed			8
	Scale			0.18
	SeeSound		"enemies/idroid/see"
	DeathSound		"enemies/remote/die"
	obituary 		"%o was killed by a small remote. Drats!"
	PainChance 		"ShrinkerDMG", 0
	Tag 			"Remote"	
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0	
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			REMO A 0
			REMO A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			REMO A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			REMO A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			REMO A 5 A_Look
			Loop
		See:
			REMO A 2 A_Chase("", "")
			REMO A 2 A_Chase
			REMO A 2 A_Chase("", "")
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			REMO A 6 A_FaceTarget
			REMO A 4 BRIGHT A_CustomMissile("blaster_laser2_REM",8,random(-1,1),random(-2,2),32,random(-2,2))
			REMO A 0 A_PlaySound("katarn/bryshot",CHAN_AUTO,1.0,0,ATTN_NORM)
			Goto See
		Pain:
			REMO A 3
			REMO A 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			REMO A 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			REMO A 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			REMO B 4 bright A_Scream
			REMO CDEF 4 bright
			TNT1 A -1 A_Fall
			Stop
	}
}