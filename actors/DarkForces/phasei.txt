//===========================================
//@author:	Ryan Cordell
//@desc:	Phase I Dark Trooper
//===========================================

ACTOR phase1_DF : SamsaraMonsterRobotic
{
	Health		475
	Radius		30
	Height		56
	Mass		400
	PainChance	220
	Speed       10
	Scale       0.75
	Tag 		"Phase I"	
	SeeSound	"enemies/phase1/see"
	PainSound	"enemies/phase1/pain"
	DeathSound	"enemies/phase1/death"
	obituary 	"%o was sliced in half by a Dark Trooper Phase 1."
	PainChance 	"ShrinkerDMG", 0
	Meleerange	74
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0	
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.0
	+BOSSDEATH
	+DONTRIP	
	+NOBLOOD
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			PHA1 P 0
			PHA1 P 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PHA1 P 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			PHA1 P 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PHA1 P 5 A_Look
			Loop
		See:
			PHA1 A 0 A_UnsetReflectiveInvulnerable
			PHA1 AA 2 A_Chase("", "")
			PHA1 BB 2 A_Chase
			PHA1 CC 2 A_Chase("", "")
			PHA1 DD 2 A_Chase
			PHA1 EE 2 A_Chase("", "")
			PHA1 FF 2 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PHA1 G 7 A_FaceTarget
			PHA1 HI 4 A_FaceTarget
			PHA1 J 7 A_CustomMeleeAttack(17,"enemies/phase1/sword","","DFMelee",1)
			Goto See
		Pain:
			PHA1 K 3 A_Jump(168,"Reflect")
			PHA1 K 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			PHA1 K 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			PHA1 K 16 A_Pain
			Goto See
		Reflect:
			TNT1 A 0 A_ChangeFlag(NOPAIN,1) //Safe checks
			PHA1 Q 4 A_SetReflectiveInvulnerable
			PHA1 RRR 15 A_CentaurDefend
			PHA1 Q 4 A_UnsetReflectiveInvulnerable
			TNT1 A 0 A_ChangeFlag(NOPAIN,0)		
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			PHA1 K 5 A_Scream
			PHA1 L 5 A_Fall
			PHA1 MN 5 
			PHA1 O -1 A_KillMaster
			Stop
	}
}