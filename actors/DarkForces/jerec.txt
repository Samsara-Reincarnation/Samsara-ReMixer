// Jerec from Dark Forces II - the DF faction needed a second boss. 
ACTOR jerec : SamsaraMonsterHumanoidBoss
{
    Tag "Jerec"
	Health		 3650
	Radius		 22
	Height		 56
	yscale 		 2.75
	xscale 		 3.25
	speed 		 16
	Mass		 100
	PainChance	 150
	SeeSound	 "jerec/sight"
	PainSound	 "jerec/pain"
	DeathSound	 "jerec/death"
	activesound  "jerec/active"
	meleesound   "jerec/saber/swing"
	meleerange   80
	meleedamage  15 
	painchance   15
	obituary 	 "%o could not defeat Jerec."
	Damagefactor "Falling", 0
	Species 	 "DFEnemies"
	+BOSS
	+BOSSDEATH
	+MISSILEMORE
	+MISSILEEVENMORE
	+DROPOFF
	+DONTRIP
	+DONTGIB
    DamageFactor "Mace", 1.4
	DamageFactor "CorvusHellstaffTomed", 0.1
    DamageFactor "CorvusCrossbow", 1.1	
    DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonShotgun", 0.8
	DamageFactor "MarathonPlasmaFire", 0.88
	DamageFactor "MarathonFusion", 2.0	
    DamageFactor "WMCNade", 0.86
    DamageFactor "BJSpear", 0.5	
	DamageFactor "DCannonProj", 0.7	
	DamageFactor "Tesla", 0.6
	DamageFactor "Shock", 0.3	
	DamageFactor "LifeLeech", 1.0		
	DamageFactor "CalebFire", 0.8	
	DamageFactor "Vodoo", 0.5
	DamageFactor "DisruptorWall", 0.5
	DamageFactor "EradNova", 0.4
    DamageFactor "EradGreenFire", 0.2
    DamageFactor "EradCoopGreenFire", 0.2
	DamageFactor "RottFire", 0.7
	DamageFactor "HLLaser", 0.6
	DamageFactor "WTRangedFire", 0.5
	DamageFactor "Nuke", 0.3
	DamageFactor "SWNuke", 0.3
	DamageFactor "SWNukeCoop", 0.3	
	DamageFactor "SWFlame", 0.1
	DamageFactor "SWFlameCoop", 0.1	
	DamageFactor "SWFlame2", 0.1
	DamageFactor "SWFlame2Coop", 0.1
	DamageFactor "SWFlame3", 0.7
    DamageFactor "RMR-BigPlasma", 0.3		
	DamageFactor "CMDMGSHIT", 0.8
	DamageFactor "Disintegrate", 0.7	
	DamageFactor "C7Disintegrate", 0.5	
	PainChance   "ShrinkerDMG", 0
	MinMissileChance	85
	MONSTER
	+FLOORCLIP
	+NOINFIGHTING
	States
	{
		Spawn:
			JERE A 0
			JERE A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			JERE A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			JERE A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			JERE ABCDEFGHIJKLMNOPQRSTUV 4 A_Look
			Goto Spawn+1
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:	
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_Playsound("jerec/saber/hum",CHAN_BODY,1.0, true)
			TNT1 A 0 A_Jump(16,"Jump")
			JER1 ABCDEFGHIJK 4 A_Chase
			Goto See2+1
		Jump:
			TNT1 A 0
			TNT1 A 0 A_playsound("Jerec/Jump")
			JER2 RS 4 ThrustThingZ(0,120,0,0)
			TNT1 A 0 ThrustThing(angle*256/360,10,1,0)
			JER2 T 4 A_JumpIf(momz == 0, "See")
			Goto MidJump
		MidJump:
			JER2 UVWXY 4 A_JumpIf(momz == 0, "See")
			Goto EndJump
		EndJump:
			JER3 A 1 A_JumpIf(momz == 0, "See")
			JER3 BCD 4 ThrustThing(angle*256/360,1,0,0)
			Loop
		JumpAttack:
			TNT1 A 0
			TNT1 A 0 A_playsound("Jerec/Jump")
			JER2 RS 4 ThrustThingZ(0,120,0,0)
			TNT1 A 0 ThrustThing(angle*256/360,10,1,0)
			JER2 T 4 A_JumpIf(momz == 0, "See")
			Goto MidJumpAttack
		MidJumpAttack:
			JER2 UVWXY 4 A_JumpIf(momz == 0, "See")
			Goto EndJumpAttack
		EndJumpAttack:
			JER3 A 1 A_JumpIf(momz == 0, "StrongAttack")
			JER3 BCD 4 ThrustThing(angle*256/360,1,0,0)
			Loop		
		StrongAttack:
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			JER5 A 4 A_FaceTarget
			JER5 BCDEF 2
			TNT1 A 0 A_CustomMeleeAttack(45,"jerec/saber/swing","Saber/Hit")
			JER5 GHI 2 bright 
			Goto See2		
		Melee:
			TNT1 A 0 A_Jump(96,"Shred")
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			JER5 A 2 A_FaceTarget
			JER5 BCDEF 2
			TNT1 A 0 A_CustomMeleeAttack(15,"jerec/saber/swing","Saber/Hit")
			JER5 GHI 2 bright 
			Goto See2
		Shred:
			TNT1 A 0 A_Jump(96,"Shred")
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			JER4 L 2 A_FaceTarget
			JER4 MNOP 2
			TNT1 A 0 A_CustomMeleeAttack(15,"jerec/saber/swing","Saber/Hit")
			JER4 QRST 2
			TNT1 A 0 A_CustomMeleeAttack(15,"jerec/saber/swing","Saber/Hit")
			JER4 UV 2 bright 
			Goto See2
		Pain:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_StopSound(CHAN_6)		
			TNT1 A 0 A_Jump(188,"Reflect")
			JER3 KLMNOP 4 A_Pain
			Goto See2
		Missile:
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			TNT1 A 0 A_JumpifCloser(128,"Grip")
			TNT1 A 0 A_JumpIfCloser(224,"Pull")
			TNT1 A 0 A_JumpIfCloser(320,"Lightning")
			TNT1 A 0 A_JumpIfCloser(640,"JumpAttack")
			TNT1 A 0 A_PlaySound("jerec/fire")
			TNT1 A 0 A_FaceTarget
			JER3 QRSTUV 4 bright A_FaceTarget		
			TNT1 A 0 A_CustomMissile("ForceDestructionProjectile",40)
			JER3 WXYZ 4 bright 
			Goto See
		Pull:	
			TNT1 A 0 A_Jump(128,"See")
			JER3 QRSTU 4 A_FaceTarget	
			JER3 A 0 A_Playsound("Jerec/Pull")
			TNT1 A 0 A_custommissile("sithpull")
			JER3 VWXY 4 A_FaceTarget
			JER3 Z 4
			goto see2
		Lightning:
			JER4 ABCDEFG 2 A_FaceTarget
			TNT1 A 0 A_playsound("jerec/lightning/start",CHAN_6)
			TNT1 A 0 A_CustomRailGun(15,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,0,"none",0,0,0,35,16.0,0,"JerecLightning")
			JER4 HIJK 2 A_FaceTarget
			goto see
			TNT1 A 0 A_StopSound(CHAN_BODY)	
			goto see2
		Reflect:
			TNT1 A 0 A_FaceTarget
			JER3 A 0 A_Playsound("Jerec/Push")
			JER3 EFGHI 4 A_SetReflectiveInvulnerable
			JER3 E 0 A_Playsound("Sith/Deflect")
			JER3 JJJ 15 A_CentaurDefend
			JER3 IHGFE 4 A_UnsetReflectiveInvulnerable
			Goto See2
		Grip:
			JERE A 0 A_JumpIfCloser(384, "GripStart")
			Goto Missile+3
		GripStart:	
			JER6 ABCD 4 A_FaceTarget
			TNT1 A 0 A_Playsound("Jerec/Grip",CHAN_6,1.0,1)
			JER6 EFGHIJ 4 bright A_CustomBulletAttack(0, 0, 1, 1, "SithGripProj")
			TNT1 A 0 A_Jump(32,"StopGrip")
			Goto GripContinue
		GripContinue:
			JER6 JJJJ 4 A_FaceTarget
			JER6 JJJJJJ 4 A_CustomBulletAttack(0, 0, 1, 1, "SithGripProj")
			TNT1 A 0 A_JumpIfTargetInLOS(1, 180, JLOSF_DEADNOJUMP, 256)
			Goto StopGrip
			TNT1 A 0 A_Jump(32,"StopGrip")
			Goto GripContinue
		StopGrip:	
			TNT1 A 0 A_StopSound(CHAN_6)
			JER6 KLM 4
			Goto See2						
		Death:
			TNT1 A 0 A_StopSound(CHAN_BODY)		
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_SpawnItemEx("JerecTrick",0,0,0,0,0,0,0,SXF_TRANSFERAMBUSHFLAG|SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERPOINTERS)
			TNT1 A 0 A_Scream
			TNT1 A 5 A_Noblocking
			TNT1 A -1 A_KillMaster
			Stop
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			TNT1 A 0 A_Scream
			TNT1 A 0 A_KillMaster
			TNT1 A 0 ACS_NamedExecuteAlways("DFJerecDeathFX")
			JER3 A 0 A_Playsound("Jerec/Push",CHAN_BODY)
			JER7 JJJKKKLLLMMMNNNOOOPPP 2 A_Fadeout(0.05)
			TNT1 A 0 A_CustomMissile("JerecSithSpirit", 32, 0, 0, CMF_AIMDIRECTION, -90)
			TNT1 A 0 A_playsound("jerec/lightning/impact",CHAN_6,1.0,true)
			TNT1 A 0 A_Noblocking
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 5
			Stop
		Crush:
			TNT1 A -1
			Stop
	}
}

Actor Sithpull : Loreshot
{
	Speed 64
	SeeSound ""
	ActiveSound ""
	states
	{
		spawn:
			TNT1 A 10
			stop
		Death:
			TNT1 A 1
			Stop
	}
}

Actor SithGripProj : BulletPuff
{
  +NoClip
  +PuffOnActors
  -BloodSplatter
  +BloodLessImpact
  Damagetype "Grip"
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1 A_SpawnItem("SithGripProj2")
    Stop
  }
}

Actor SithGripProj2
{
  Radius 10
  Height 20
  -Solid
  +NonShootable
  -Shootable
  -NoGravity
  DamageType "Grip"
  States
  {
  Spawn:
    TNT1 A 2
	TNT1 A 0 A_ChangeFlag("SOLID", 1)
	TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
	TNT1 A 20
    Stop
  }
}

ACTOR JerecTrick : jerec
{
	Health			1
	Radius			20
	Height			46
	Painchance		75
	Speed			0
	obituary "%o joined the dark side."
	+INVULNERABLE
	+NOTARGETSWITCH
	+DONTGIB
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_playsound("Jerec/Gasp",CHAN_VOICE,1.0,TRUE,ATTN_NONE)
			JER5 Y 160 
			TNT1 A 0 A_Changeflag("Invulnerable",FALSE)
		Spawn2:	
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_PlaySound("Jerec/Taunt",CHAN_VOICE,1.0,0,ATTN_NONE)
			TNT1 A 0 A_JumpIFInventory("FakeAttackTimer",50,"LastStand")
			TNT1 A 0 A_GiveInventory("FakeAttackTimer",1)
			JER5 Y 4
			Goto Spawn2+2
		LastStand:
			JER5 YX 4
			JER5 X 4
			TNT1 A 0 A_Die("LastStand")
			Goto Death.LastStand
		Death.LastStand:
			TNT1 A 1 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("JerecLastStand",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERPOINTERS) 	
			stop
		Death:
		XDeath:	
			TNT1 A 0 ACS_NamedExecuteAlways("DFDarkSide")
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			TNT1 A 0 A_Scream
			TNT1 A 0 ACS_NamedExecuteAlways("DFJerecDeathFX")
			JER3 A 0 A_Playsound("Jerec/Push",CHAN_BODY)
			JER7 JJJKKKLLLMMMNNNOOOPPP 2 A_Fadeout(0.05)
			TNT1 A 0 A_CustomMissile("JerecSithSpirit", 32, 0, 0, CMF_AIMDIRECTION, -90)
			TNT1 A 0 A_playsound("jerec/lightning/impact",CHAN_6,1.0,true)
			TNT1 A 0 A_Noblocking
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 AAAAAAAAAAA 5 A_CustomRailGun(0,0,"none","",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING|RGF_EXPLICITANGLE,0,0,"none",random(0,360),-15,0,35,16.0,0,"JerecDeathLightning",16)
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 0 A_GiveToTarget("JuggernautDeathFlash")
			TNT1 A 5 A_KillMaster
			Stop
		Crush:
			TNT1 A -1
			Stop

		// SamsaraMonsterHumanoidBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.DukePlasma
		Death.RottDisintegrate:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.RottDisintegrate
		Death.Disintegrate:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.Disintegrate
		Death.Nuke:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" A 0 ACS_NamedExecuteAlways("DFDarkSide")
			Goto Super::Death.Ice			
	}
}

ACTOR JerecLastStand : jerec
{
	Health			1000
	Radius			20
	Height			46
	Painchance		75
	Speed			16
	SeeSound	"jerec/attack"
	DeathSound	"jerec/death"
	obituary "%o fell for Jerec's trick."
	+NOTARGETSWITCH
	+DONTGIB
	States
	{
		Death:
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			TNT1 A 0 A_Scream
			TNT1 A 0 ACS_NamedExecuteAlways("DFJerecDeathFX")
			JER3 A 0 A_Playsound("Jerec/Push",CHAN_BODY)
			JER7 JJJKKKLLLMMMNNNOOOPPP 2 A_Fadeout(0.05)
			TNT1 A 0 A_CustomMissile("JerecSithSpirit", 32, 0, 0, CMF_AIMDIRECTION, -90)
			TNT1 A 0 A_playsound("jerec/lightning/impact",CHAN_6,1.0,true)
			TNT1 A 0 A_Noblocking
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 5 A_KillMaster
			Stop
		Crush:
			TNT1 A -1
			Stop
	}
}

Actor DarkSideDummy : Inventory { Inventory.MaxAmount 1 }
Actor FakeAttackTimer : Inventory { Inventory.MaxAmount 100 }

actor ForceLightningProjectile : FastProjectile
{
  RenderStyle Add
  Alpha 1.0
  Scale 1.0
  Height 4
  Radius 2
  Damage 15
  Projectile
  Speed 256
  +NOINTERACTION
  +CLIENTSIDEONLY
  MissileType "JerecLightning"
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 5
	loop
  }
}

actor JerecLightning
{
  RenderStyle Add
  Alpha 1.0
  Scale 5.0
  Height 1
  Radius 1
  Speed 64
  +NOINTERACTION
  +CLIENTSIDEONLY
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Jump(128,"Mode2")
  Mode1:
	JERE A 3 bright
	stop
  Mode2:
	JERE B 3 bright
	stop
  }
}

Actor ForceDestructionProjectile : plasma_shot 
{
 Speed 40
 RenderStyle Add
 Alpha 1
 Scale 0.33
 States
  {
    Spawn:
      FDES A 3 BRIGHT
      Loop
    Death:
      TNT1 A 0
	  TNT1 A 1 A_PlaySound("jerec/destruction/impact",5,1.0,0,ATTN_NORM)
	  TNT1 A 0 A_Explode(30, 40)
	  FDES BCDEFGHI 3 bright 
      Stop
  }
}

Actor JerecDeathProjectile : plasma_shot 
{
 Speed 40
 RenderStyle Add
 Alpha 1
 Scale 0.33
 States
  {
    Spawn:
      FDES A 3 BRIGHT
      Loop
    Death:
      TNT1 A 0
	  TNT1 A 1 A_PlaySound("jerec/destruction/impact",5,1.0,0,ATTN_NORM)
	  FDES BCDEFGHI 3 bright 
      Stop
  }
}

Actor JerecSithSpirit
{
 Speed 2
 RenderStyle Add
 Projectile
 Alpha 1
 Scale 0.33
 +NOGRAVITY
 +FLOATBOB
 States
  {
    Spawn:
      FDES J 225 BRIGHT
      stop
  }
}

actor JerecDeathLightning
{
  RenderStyle Stencil
  StencilColor "Yellow"
  Alpha 1.0
  Scale 5.0
  Height 1
  Radius 1
  +NOINTERACTION
  +CLIENTSIDEONLY
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_Jump(128,"Mode2")
  Mode1:
	JERE A 3 bright
	stop
  Mode2:
	JERE B 3 bright
	stop
  }
}

ACTOR JerecDeathFlash : CustomInventory
{
    Inventory.MaxAmount 0
    +AUTOACTIVATE
    States
    {
    Spawn:
        TNT1 A 0
        stop
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_SetBlend("fdffc3",1,175)
        stop
    UseStop:
        TNT1 A 0
        stop
    }
}