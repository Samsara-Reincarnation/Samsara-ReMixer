ACTOR DF_BattleDroid : SamsaraMonsterRobotic
{
	Health		50
	Radius		20
	Height		56
	Mass		100
	Painchance	75
	Speed		6
	Tag 		"Battle Droid"
	SeeSound	"enemies/battledroid/see"
	PainSound	"enemies/remote/die"
	DeathSound	"katarn/exsmal"
	obituary 	"%o was shot by a battle droid."
	Scale				0.4
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
	+NOBLOOD
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	States
	{
		Spawn:
			B1BD W 0
			B1BD W 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			B1BD W 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			B1BD W 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			B1BD W 5 A_Look
			Loop
		See:
			B1BD AAA 2 A_Chase("", "")
			B1BD BBB 2 A_Chase
			B1BD CCC 2 A_Chase("", "")
			B1BD DDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			B1BD E 3 A_FaceTarget
			B1BD F 3 A_FaceTarget
			B1BD G 0 A_PlaySound("katarn/rifshot",CHAN_AUTO,1.0,0,ATTN_NORM)		
			B1BD G 5 BRIGHT A_CustomMissile("blaster_laser2_DFB",33,random(-1,1),random(-2,2),32,random(-2,2))
			B1BD G 5
			Goto See
		Pain:
			B1BD H 3
			B1BD H 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			B1BD J 5 bright A_Scream
			B1BD K 5 bright
			B1BD LMN 5
			B1BD O -1 A_Fall
			Stop
		Crush:
			TNT1 A -1
			Stop
	}
}