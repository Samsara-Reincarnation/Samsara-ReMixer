ACTOR blue_interrogation_droid : SamsaraMonsterRobotic
{
	Health			1000
	Radius			20
	Height			36
	Mass			400
	Painchance		85
	Speed			10
	SeeSound		"enemies/idroid/see"
	ActiveSound		"enemies/idroid/see"
	DeathSound		"katarn/exsmal"
	MeleeRange		128
	Tag 			"Armored Interrogation Droid"	
	obituary 		"%o did not survive interrogation."
	PainChance 		"ShrinkerDMG", 0
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	Scale				0.8
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
	+DONTRIP
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			1NTD A 0
			1NTD A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			1NTD A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			1NTD A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			1NTD A 5 A_Look
			Loop
		See:
			1NTD A 2 A_Chase("", "")
			1NTD A 2 A_Chase
			1NTD A 2 A_Chase("", "")
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			1NTD BB 4 A_FaceTarget
			1NTD CC 4 A_FaceTarget
			1NTD D 8 A_CustomMeleeAttack(9,"enemies/idroid/melee","",DFMelee)
			Goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			1NTD E 6 A_FaceTarget
			1NTD F 0 A_PlaySound("enemies/pdroid/fire",CHAN_AUTO,1.0,0,ATTN_NORM)			
			1NTD F 4 BRIGHT A_CustomMissile("blue_idroid_blast",8,random(-1,1),random(-2,2),32,random(-2,2))
			Goto See
		Pain:
			1NTD D 3
			1NTD D 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			1NTD D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			1NTD D 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			1NTD G 5 bright A_Scream
			TNT1 A 0 A_Fall
			1NTD HI 5 bright 
			1NTD JKLM 4 bright
			1NTD N -1 
			Stop
	}
}

ACTOR blue_idroid_blast
{
	Radius		4
	Height		4
	Speed		30
	Damage		(25)
	Scale		0.8
	PROJECTILE
	States
	{
		Spawn:
			WIDB ABCDEF 4 BRIGHT
			Loop
		Death:
			FUEX A 2 BRIGHT A_PlaySound("weapon/explosion/tiny",5,0.8,0,1.5)
			FUEX BCD 2 BRIGHT
			FUEX EFG 2 
			Stop
	}
}