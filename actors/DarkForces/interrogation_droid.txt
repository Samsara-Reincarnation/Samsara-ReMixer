//===========================================
//@author:	Ryan Cordell
//@desc:	Interrogation Droid
//===========================================

ACTOR interrogation_droid : SamsaraMonsterRobotic
{
	Health			250
	Radius			20
	Height			36
	Mass			400
	Painchance		85
	Speed			8
	SeeSound		"enemies/idroid/see"
	ActiveSound		"enemies/idroid/see"
	DeathSound		"katarn/exsmal"
	MeleeRange		128
	Tag 			"Interrogation Droid"	
	obituary 		"%o did not survive interrogation."
	PainChance 		"ShrinkerDMG", 0
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	Scale				0.8
	MinMissileChance	85
	MONSTER
	Species 			"DFEnemies"
	+FLOORCLIP
	+NOINFIGHTING
    DamageFactor 		"Mace", 1.1
    DamageFactor 		"DragonClaw", 1.5
    PainChance 			"MightyBoot", 255
	PainChance 			"MarathonStun", 255
	States
	{
		Spawn:
			INTD A 0
			INTD A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			INTD A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			INTD A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			INTD A 5 A_Look
			Loop
		See:
			INTD A 2 A_Chase("", "")
			INTD A 2 A_Chase
			INTD A 2 A_Chase("", "")
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			INTD BB 4 A_FaceTarget
			INTD CC 4 A_FaceTarget
			INTD D 8 A_CustomMeleeAttack(6,"enemies/idroid/melee","",DFMelee)
			Goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			INTD E 6 A_FaceTarget
			INTD F 0 A_PlaySound("enemies/pdroid/fire",CHAN_AUTO,1.0,0,ATTN_NORM)			
			INTD F 4 BRIGHT A_CustomMissile("idroid_blast",8,random(-1,1),random(-2,2),32,random(-2,2))
			Goto See
		Pain:
			INTD D 3
			INTD D 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			INTD D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			INTD D 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)			
			INTD G 5 bright A_Scream
			TNT1 A 0 A_Fall
			INTD HI 5 bright 
			INTD JKLM 4 bright
			INTD N -1 
			Stop
	}
}

actor InterrogantDDisintegrate : ZombiemanDisintegrate
{
  Scale	0.8
  States
  {
  Spawn:
    TNT1 A 0
    INTD D 3 A_SpawnItem("DisintEffectSmallThin",0,10)
    INTD D 3 A_SetTranslucent(0.9,0)
    INTD D 3 A_SetTranslucent(0.8,0)
    INTD D 3 A_SetTranslucent(0.7,0)
    INTD D 3 A_SetTranslucent(0.6,0)
    INTD D 3 A_SetTranslucent(0.5,0)
    INTD D 3 A_SetTranslucent(0.4,0)
    INTD D 3 A_SetTranslucent(0.3,0)
    INTD D 3 A_SetTranslucent(0.2,0)
    INTD D 3 A_SetTranslucent(0.1,0)
    Stop
  }
}

ACTOR idroid_blast
{
	Radius		4
	Height		4
	Speed		20
	Damage		(15)
	Scale		0.8
	PROJECTILE
	States
	{
		Spawn:
			WIDB ABCDEF 4 BRIGHT
			Loop
		Death:
			FUEX A 2 BRIGHT A_PlaySound("weapon/explosion/tiny",5,0.8,0,1.5)
			FUEX BCD 2 BRIGHT
			FUEX EFG 2 
			Stop
	}
}