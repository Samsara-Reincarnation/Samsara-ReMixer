Actor C7SectorProbe : SamsaraMonsterRobotic {
	Health 100
	Height 44
	Speed 7
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	SeeSound "aprobe/see"
	PainSound ""
	DeathSound "aprobe/death1"
	ActiveSound "aprobe/see"
	Obituary "%o was spotted by a Sector Probe."
	Tag "Sector Probe"
	DropItem C7EnemyDropClip, 60, 1	
	Radius 16
	PainChance 200
	Species "C7"
	MONSTER	
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	+DONTGIB	
	+NOINFIGHTING
	Mass 250
	ActiveSound ""
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "C7EnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Decal BulletChip
	States
	{
		Spawn:
			7PRP A 0
			7PRP A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			7PRP A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			7PRP A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			7PRP A 5 A_Look
			Loop
		See:
			7PRP AA 2 A_Chase("","")
			7PRP AA 2 A_Chase
			7PRP AA 2 A_Chase("","")
			7PRP AA 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			7PRP AA 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("ailo/fire",CHAN_WEAPON)
			TNT1 A 0 A_JumpIfCloser(118, "Bullet1")
			TNT1 A 0 A_JumpIfCloser(246, "Bullet2")
			TNT1 A 0 A_JumpIfCloser(374, "Bullet3")
			TNT1 A 0 A_JumpIfCloser(502, "Bullet4")
			TNT1 A 0 A_JumpIfCloser(630, "Bullet5")
			TNT1 A 0 A_JumpIfCloser(758, "Bullet6")
			Goto Bullet7
		Bullet1:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD1",32,0,random(-4,4),8)
			Goto FireEnd
		Bullet2:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD2",32,0,random(-5,5),8)
			Goto FireEnd
		Bullet3:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD3",32,0,random(-6,6),8)
			Goto FireEnd
		Bullet4:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD4",32,0,random(-7,7),8)
			Goto FireEnd
		Bullet5:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD5",32,0,random(-8,8),8)
			Goto FireEnd
		Bullet6:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD6",32,0,random(-9,9),8)
			Goto FireEnd
		Bullet7:
			7PRP K 5 Bright A_CustomMissile("C7EnemyBulletD7",32,0,random(-10,10),8)
			Goto FireEnd
		FireEnd:
			7PRP A 5 A_FaceTarget
			Goto See
		Pain:
			TNT1 A 0 A_Jump(128,"Pain2","Pain3")
			7PRP J 3   
			7PRP J 3 A_Pain
			Goto See 
		Pain2:
			TNT1 A 0 A_Jump(90,1)
			7PRP B 3   
			7PRP C 3 A_Pain
			Goto See 	
		Pain3:
			TNT1 A 0 A_Jump(90,1)
			7PRP C 3   
			7PRP B 3 A_Pain
			Goto See 			
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)  
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			7PRP D 4 A_Scream
			7PRP E 4 A_NoBlocking
			7PRP F 4 
			7PRP G 4 
			7PRP H -1
			Stop
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag(NOPAIN,1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			7PRP J 3 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			7PRP J 16 A_Pain		
			Goto See
	}
}