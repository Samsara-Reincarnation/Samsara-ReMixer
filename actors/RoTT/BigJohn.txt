ACTOR BigJohn : RoTTBoss {
   Health 5000
   Radius 18
   Height 50
   PainChance 16
   DamageFactor "normal", 0.75
   RadiusDamageFactor 1.25
   SCALE 0.75
   Speed 3
   +BOSSDEATH
   +DONTRIP
   SeeSound "BigJohn/Act"
   AttackSound "enforcer/fire"
   ActiveSound "BigJohn/Act"
   PainSound "BigJohn/Pain"
   DeathSound "BigJohn/Pain"
   Species "ROTTEnemies"
   Obituary "%o couldn't stand the might of Big John."
   Tag "Big John"  
   Dropitem "TriadEnforcerDrop", 256
   States {
		Spawn:
			BIGJ A 0
			BIGJ A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			BIGJ A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			BIGJ A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			BIGJ A 10 A_Look
			Loop
		Idle:
			BIGJ A 10 A_Look
			Loop   
		See:
			TNT1 A 0 A_JumpIfInventory("Shooting", 1, "See2")
			BIGJ B 3 A_Chase
			BIGJ B 3 A_Chase
			BIGJ C 3 A_Chase
			BIGJ C 3 A_Chase("", "")
			BIGJ D 3 A_Chase
			BIGJ D 3 A_Chase
			BIGJ E 3 A_Chase
			BIGJ E 3 A_Chase("", "")
			loop
		See2:
			TNT1 A 0 A_TakeInventory("Shooting", 1)
			BIGJ I 5
			Goto See
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			BGJT A 1 A_jumpifcloser(496,"Grenade")
			BGJT A 3 a_facetarget
			BIGJ I 4
			goto MachineGun
		Grenade:
			TNT1 A 0 A_Jump(96, "Machinegun2")
			BGJT A 1 a_playsound("BigJohn/Act")
			BGJT B 4 
			BGJT C 4 
			BGJT D 4 a_facetarget
			BGJT E 4 A_CustomMissile("TriadEnforcerGrenade", 36)
			BGJT F 4
			BGJT G 4
			BGJT H 8
			goto Machinegun2	  
		MachineGun:
			BIGJ I 0 a_facetarget  
			BGJR A 4 A_jumpifcloser(496,"Grenade")
			BIGJ J 3 Bright A_CustomBulletAttack(10.0,7.0,1,24,"RottPuff", 0, CBAF_NORANDOM)
			TNT1 A 0 A_GiveInventory("Shooting", 1)
			BIGJ I 0 a_monsterrefire(135,"see")
			TNT1 A 0 A_Jump(24, "See")
			Loop
		MachineGun2:  
			BGJR A 4 a_facetarget
			BIGJ J 3 Bright A_CustomBulletAttack(10.0,7.0,1,24,"RottPuff", 0, CBAF_NORANDOM)
			TNT1 A 0 A_GiveInventory("Shooting", 1)
			BIGJ I 0 a_monsterrefire(135,"see")
			TNT1 A 0 A_Jump(24, "See")
			Loop
		Pain:
			TNT1 A 0 A_TakeInventory("Shooting", 1)
			TNT1 A 0 a_jump(128, "Pain2")
			BIGJ F 3
			BIGJ F 4 A_Pain
			Goto See
		Pain2:
			BIGJ G 3
			BIGJ G 4 A_Pain
			Goto See
		Death:
		XDeath:
		Death.RottDisintegrate:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffectForBosses")
			BIGJ K 5 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			BIGJ L 6 A_Scream
			BIGJ M 7 A_Fall
			BIGJ N 8
			BIGJ O -1 A_KillMaster
			Stop

		// SamsaraMonsterDemonicBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			Goto Super::Death.DukePlasma
		Death.Disintegrate:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			Goto Super::Death.Disintegrate
		Death.Nuke:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" A 0 ACS_NamedExecuteAlways("RoTTBossScript", 0, BigJohn_ID, 2)
			"####" A 0 A_GiveToTarget("RoTT_BossDeathPlayer", 1)
			Goto Super::Death.Ice
	}
}