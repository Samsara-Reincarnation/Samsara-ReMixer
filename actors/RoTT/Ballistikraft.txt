ACTOR Ballistikraft : SamsaraMonsterRobotic {
   Tag "Ballistikraft"
   Health 250
   Radius 24
   Height 64
   Speed 2
   Mass 5000
   PainChance 0
   MONSTER   
   Species "ROTTEnemies"
   +NOBLOOD
   +FLOORCLIP
   +NOPAIN
   +BOSSDEATH
   +DONTRIP
   +NOINFIGHTING
   +NOICEDEATH
   SCALE 0.65
   SeeSound "robot/see"
   DeathSound "robot/die"
   Obituary "%o was blown off by a Ballistikraft."
   DamageFactor "normal", 0.5
   	DamageFactor "HLBullet", 0.5
	DamageFactor "GEBullet", 0.5
   DamageFactor "MarathonFusion", 2.0
   DamageFactor "Tesla", 2.0
   DamageFactor "ElectricBolt", 2.0
   DamageFactor "PoisonBolt", 0.0	
   RadiusDamageFactor 4.5
   States {
		Spawn:
			BCRA A 0
			BCRA A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			BCRA A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			BCRA A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			BCRA A 10 A_Look
			Loop
		Idle:
			BCRA A 1 A_Wander
			TNT1 A 0 A_Look
			Loop	   
		See:
			BCRA A 1 A_PlaySound("robot/move",CHAN_VOICE,1.0,1,ATTN_NORM)
		SeeLoop:   
			BCRA AAAAAAAAAAAAAAAA 1 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_StopSound(CHAN_VOICE)
			BCRA A 10 A_FaceTarget
		KeepFiring:
			BCRA A 10 A_FaceTarget
			TNT1 A 0 A_PlaySound("robot/fire")
			TNT1 A 0 A_CustomMissile("BcraftMissile", 56)
			BCRA A 12 A_CustomMissile("RobotMissile", 32, 0, 0)
			TNT1 A 0 A_PlaySound("robot/fire")
			BCRA A 12 A_CustomMissile("RobotMissile", 32, 0, 0)
			BCRA A 1 A_GiveInventory("AttackDecider_3", 1)
			BCRA A 7 A_JumpIfInventory("DoAttack", 1, "Repeat")
			Goto See
		Repeat:
			TNT1 A 0 A_TakeInventory("DoAttack", 1)
			TNT1 A 0 A_JumpIf(1 == ACS_NamedExecuteWithResult("RoTT808", 0), "See")
			Goto KeepFiring
		Death:   
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_VOICE)   
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx ("ash1", 0.0, 0.0, 40.0, random(5.0,10.0), random(5.0,10.0), random(3.0,8.0), random(0,360))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("ash1", 0.0, 0.0, 16.0, random(5.0,10.0), random(5.0,10.0), random(3.0,8.0), random(0,360))
			TNT1 A 0 A_Scream  
			ROBT B 3 A_Fall
			ROBT CDEFGHIJ 3 
			ROBT K -1 A_KillMaster
			Stop  				
	}
}

actor BcraftMissile {
    Radius 10
    Height 12
    Speed 20
    Damage (0)
    SCALE 0.6
    PROJECTILE
    +EXTREMEDEATH
    +BRIGHT
    damagetype "flame" 
    DeathSound "god/bang"
    States {
    Spawn:  
        M1SS A 1 Bright
        Loop
    Death:
        EXPL AAB 1 Bright
		TNT1 A 0 A_RadiusThrust(2000, 96)
		EXPL BCC 1 Bright
		TNT1 A 0 A_Explode(50, 96, 0)
		EXPL DDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTT 1 Bright
        Stop
    }
}