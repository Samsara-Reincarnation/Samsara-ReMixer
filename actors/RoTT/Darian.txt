ACTOR GeneralDarian : RoTTHumanoidLarge
{
	Health 766
	Height 64
	Speed 5
	Mass 5000
	PainChance 64
	SCALE 0.55
	AttackSound ""
	Activesound "darian/act"
	SeeSound "darian/see"
	PainSound "darian/ouch"
	DeathSound "darian/die"
	Species "ROTTEnemies"
	+DONTRIP
	Tag "General John Darian"
	Obituary "%o was buried in a lunch box by General Darian."
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	States
	{
		Spawn:
			DARI A 0
			DARI A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			DARI A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			DARI A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			DARI A 10 A_Look
			Loop
		Idle:
			DARI A 0 A_ChangeFlag("NOPAIN", 0)
			DARI A 0 A_ChangeFlag("SOLID",true)
			DARI A 0 A_SetShootable
			DARI A 10 A_Look
			Loop
		See:
			DARI B 3 A_Chase
			DARI B 2 A_Chase
			DARI C 3 A_Chase
			DARI C 2 A_Chase("", "", CHF_NOPLAYACTIVE)
			DARI D 3 A_Chase
			DARI D 2 A_Chase
			DARI E 3 A_Chase
			DARI E 2 A_Chase("", "", CHF_NOPLAYACTIVE)
			Loop
		Wackamole:
			TNT1 A 0 A_TakeInventory("DoWackaMole", 1)
			TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
			DARH ABCD 2
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_PlaySound("Darian/Hide")
			DARH EFGH 2
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
			TNT1 AAAAA 0 A_Chase("", "", CHF_NOPLAYACTIVE)
			TNT1 A 10
			TNT1 A 0 A_ChangeFlag("SOLID",true)
			DARH HGFE 2
			TNT1 A 0 A_SetShootable
			DARH DCBA 2
			TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
			Goto Missile
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			DARS AB 8 A_FaceTarget
			TNT1 A 0 A_PlaySound("bazooka/shoot")
			DARS C 8 Bright A_CustomMissile("DarianMissile", 59, -8)
			DARS D 1 A_GiveInventory("WackamoleChecker", 1)
			DARS D 7 A_JumpIfInventory("DoWackamole", 1, "Wackamole")
			Goto See
		Pain:
			DARI F 3 A_Jump(128, "Pain2")
			DARI F 3 A_Pain
			TNT1 A 0 A_GiveInventory("WackamoleChecker", 1)
			DARI F 1 A_JumpIfInventory("DoWackamole", 1, "Wackamole")
			Goto See
		Pain2:
			DARI G 3
			DARI G 3 A_Pain
			TNT1 A 0 A_GiveInventory("WackamoleChecker", 1)
			DARI G 1 A_JumpIfInventory("DoWackamole", 1, "Wackamole")
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			TNT1 A 0 A_Jump(128, "BootPain2")
			DARP B 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			DARP B 16 A_Pain
			Goto See
		BootPain2:
			DARP C 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			DARP C 16 A_Pain
			Goto See
		Death.RottFire:
		Death.RottExplosive:
		XDeath.RottExplosive:
		XDeathForROTT:
		Death:
		XDeath:
			DARI H 5 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			DARI I 6 A_Scream
			DARI J 7 A_Fall
			DARI K 24
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
			GUTS DDDDDDDDDDDDDDDDDDDDDDD 0 A_SpawnItemEx ("giblets",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE , 0)
			DARI LMNOPQ 4
			TNT1 A 0 A_KillMaster
			DARI R -1
			Stop
		Death.CalebFire:
		Death.LifeLeech:
			TNT1 A 0 A_Jump(218, 2)
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			TNT1 A 0 A_SpawnItemEx("BloodBurningCorpse_Generic", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_Stop
			TNT1 A 5 A_NoBlocking
			TNT1 A -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
			Stop
	}
}

actor DarianMissile : BcraftMissile
{
	Speed 28
	Damage (0)
	Scale 0.5
	ActiveSound "missile/fly"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_LoopActiveSound
	SpawnLoop:
		M1SS AAAAAAAAAAAAAA 1 Bright A_CustomMissile("MissileTrail",0,0,0,0)
		Loop
	Death:
		EXPL AAB 1 Bright
		TNT1 A 0 A_RadiusThrust(2000, 96)
		EXPL BCC 1 Bright
		TNT1 A 0 A_Explode(50, 96, 0)
		EXPL DDEEFFGGHHIIJJKKLLMMNNOOPPQQRRSSTT 1 Bright
		Stop
	}
}