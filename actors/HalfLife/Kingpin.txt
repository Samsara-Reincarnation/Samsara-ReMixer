Actor HLKingpin : SamsaraMonsterAlienBoss
{
	Species "HLEnemies"
	Mass 200
	Radius 24
	Height 56
	SeeSound "kingpin/see"
	Activesound "kingpin/idle"
	Deathsound "kingpin/die"
	Painsound "kingpin/pain"
	Monster
	Scale 0.850
	Health 2500
	BloodColor "Khaki"
	BloodType "HalfLifeAlienBlood"
	Meleerange 56
	+MISSILEMORE
	+NOINFIGHTING
	+NOPAIN
	+NOBLOODDECALS
	+DONTHARMCLASS
	+DONTGIB
	Speed 6
	Tag "Kingpin"
    Obituary "%o was killed by a Kingpin."
	Painchance 256
	var int user_oldangle;
	var int user_oldpitch;
	States
	{
		Spawn:
			HL05 A 0
			HL05 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			HL05 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			HL05 A 0 A_GiveInventory("HLSpeedTokens", 6)
			HL05 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			HL05 ABCDEFGHIJKLMNOPQRSTUVWXYZ 3 A_Look
			HL06 ABCDEFGHIJKLMNOPQRSTUVWXYZ 3 A_Look
			HL07 ABCDEFGH 3 A_Look
			Loop
		Idle:
			HL04 A 1
			HL04 A 0 ACS_NamedExecuteAlways("HalfLifeAllyWalkRunSpeed", 0, 0)
			Goto IdleLoop
		IdleLoop:
			HL04 A 0 A_JumpIfInventory("HLKingPinShieldUp", 1, "LowerShieldIdle")
			HL04 AA 2 A_Wander		HL04 B 0 A_Look
			HL04 BB 2 A_Wander		HL04 C 0 A_Look
			HL04 CC 2 A_Wander		HL04 D 0 A_Look
			HL04 DD 2 A_Wander		HL04 E 0 A_Look
			HL04 EE 2 A_Wander		HL04 F 0 A_Look
			HL04 FF 2 A_Wander		HL04 G 0 A_Look
			HL04 GG 2 A_Wander		HL04 H 0 A_Look
			HL04 HH 2 A_Wander		HL04 I 0 A_Look
			HL04 II 2 A_Wander		HL04 J 0 A_Look
			HL04 JJ 2 A_Wander		HL04 K 0 A_Look
			HL04 KK 2 A_Wander		HL04 L 0 A_Look
			HL04 LL 2 A_Wander		HL04 M 0 A_Look
			HL04 MM 2 A_Wander		HL04 N 0 A_Look
			HL04 NN 2 A_Wander		HL04 O 0 A_Look
			HL04 OO 2 A_Wander		HL04 P 0 A_Look
			HL04 PP 2 A_Wander		HL04 Q 0 A_Look
			HL04 QQ 2 A_Wander		HL04 R 0 A_Look
			HL04 RR 2 A_Wander		HL04 S 0 A_Look
			HL04 SS 2 A_Wander		HL04 T 0 A_Look
			HL04 TT 2 A_Wander		HL04 U 0 A_Look
			HL04 UU 2 A_Wander		HL04 A 0 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		SetRun:
			HL03 A 1
			HL03 A 0 ACS_NamedExecuteAlways("HalfLifeAllyWalkRunSpeed", 0, 1)
			Goto SeeLoop
		SeeLoop:	
			HL03 A 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 B 2 A_Chase
			HL03 C 2 A_Chase
			HL03 D 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 E 2 A_Chase
			HL03 F 2 A_Chase
			HL03 G 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 H 2 A_Chase
		    HL03 I 2 A_Chase
			HL03 J 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 K 2 A_Chase
			HL03 L 2 A_Chase
			HL03 M 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 N 2 A_Chase
			HL03 O 2 A_Chase
			HL03 P 2 A_Chase("","",CHF_NOPLAYACTIVE)
			HL03 Q 2 A_Chase
			HL03 R 2 A_Chase
			HL03 S 2 A_Chase("","",CHF_NOPLAYACTIVE)			
			Loop
		Melee:
			HL01 ABCDEFG 2 A_FaceTarget
			HL01 H 2 A_CustomMeleeAttack(random(10,14)*Random(2,1),"HLZombie/Claw","HLZombie/Miss")
			HL01 IJKLMNOPQ 2 A_FaceTarget
			HL01 R 2 A_CustomMeleeAttack(random(10,14)*Random(2,1),"HLZombie/Claw","HLZombie/Miss")
			HL01 STUVWXY 2
			Goto SeeLoop
		RaiseShield:
			TNT1 A 0 ACS_NamedExecuteAlways("KingPinShield", 0, 512)
			TNT1 A 0 A_SpawnItemEx("HLKingPinShield",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("HLKingPinPsychicFlare1",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("HLKingPinPsychicFlare2",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("HLKingPinPsychicFlare3",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
			TNT1 A 0 A_ChangeFlag("Reflective", true)
			TNT1 A 0 A_SetReflectiveInvulnerable
			TNT1 A 0 A_GiveInventory("HLKingPinShieldUp", 1)
			Goto SeeLoop
		LowerShield:
			TNT1 A 0 A_RemoveChildren(true)
			TNT1 A 0 A_ChangeFlag("Reflective", false)
			TNT1 A 0 A_UnsetReflectiveInvulnerable
			TNT1 A 0 A_TakeInventory("HLKingPinShieldUp", 1)
			Goto SeeLoop
		LowerShieldIdle:
			TNT1 A 0 A_RemoveChildren(true)
			TNT1 A 0 A_ChangeFlag("Reflective", false)
			TNT1 A 0 A_UnsetReflectiveInvulnerable
			TNT1 A 0 A_TakeInventory("HLKingPinShieldUp", 1)
			Goto Idle
		Missile:
			TNT1 A 0 A_JumpIfInventory("HLKingPinShieldUp", 1, 2)
			TNT1 A 0 A_Jump(216, "MissileSeeker", "TeleportAway", "RaiseShield")
			TNT1 A 0 A_PlaySound("kingpin/att",CHAN_WEAPON)
			HL02 ABCDEF 2 A_FaceTarget
			HL02 G 0 A_SetUserVar("user_oldangle", angle)
			HL02 G 0 A_SetUserVar("user_oldpitch", pitch)
			HL02 GHIJ 2
			HL02 K 0 A_CustomRailgun(10,0,"none","purple",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,8,"HL_Kingpinpuff",user_oldangle>>16,user_oldpitch>>16,8192,5,0.0)
			HL02 K 0 A_CustomRailgun(10,0,"none","purple",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,8,"HL_Kingpinpuff",user_oldangle>>16,user_oldpitch>>16,8192,5,0.0)
			HL02 K 0 A_CustomRailgun(10,0,"none","purple",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,8,"HL_Kingpinpuff",user_oldangle>>16,user_oldpitch>>16,8192,5,0.0)
			HL02 K 0 A_CustomRailgun(10,0,"none","purple",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,8,"HL_Kingpinpuff",user_oldangle>>16,user_oldpitch>>16,8192,5,0.0)
			HL02 K 0 A_CustomRailgun(10,0,"none","purple",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,8,"HL_Kingpinpuff",user_oldangle>>16,user_oldpitch>>16,8192,5,0.0)
			HL02 KLMNO 2 A_FaceTarget
			HL02 PQRST 2
			Goto SeeLoop
		MissileSeeker:
			TNT1 A 0 A_JumpIfInventory("HLKingPinShieldUp", 1, 2)
			TNT1 A 0 A_PlaySound("kingpin/att",CHAN_WEAPON)
			HL02 ABCDEF 2 A_FaceTarget
			HL02 G 0 A_SetUserVar("user_oldangle", angle)
			HL02 G 0 A_SetUserVar("user_oldpitch", pitch)
			HL02 GHIJ 2
			HL02 K 0 A_CustomMissile("HLKingPinSeeker", 52)
			HL02 KLMNO 2 A_FaceTarget
			HL02 PQRST 2
			Goto SeeLoop
		TeleportAway:
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", true)
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", false)
			HL02 A 2 A_SetTranslucent(alpha-0.05)		HL02 B 0 ThrustThingZ(0, 1, 0, 0)
			HL02 B 2 A_SetTranslucent(alpha-0.05)		HL02 C 0 ThrustThingZ(0, 1, 0, 0)
			HL02 C 2 A_SetTranslucent(alpha-0.05)		HL02 D 0 ThrustThingZ(0, 1, 0, 0)
			HL02 D 2 A_SetTranslucent(alpha-0.05)		HL02 E 0 ThrustThingZ(0, 1, 0, 0)
			HL02 E 2 A_SetTranslucent(alpha-0.05)		HL02 F 0 ThrustThingZ(0, 1, 0, 0)
			HL02 F 2 A_SetTranslucent(alpha-0.05)		HL02 G 0 ThrustThingZ(0, 1, 0, 0)
			HL02 G 2 A_SetTranslucent(alpha-0.05)		HL02 H 0 ThrustThingZ(0, 1, 0, 0)
			HL02 I 2 A_SetTranslucent(alpha-0.05)		HL02 J 0 ThrustThingZ(0, 1, 0, 0)
			HL02 J 2 A_SetTranslucent(alpha-0.05)		HL02 K 0 ThrustThingZ(0, 1, 0, 0)
			HL02 K 2 A_SetTranslucent(alpha-0.05)		HL02 L 0 ThrustThingZ(0, 1, 0, 0)
			HL02 M 2 A_SetTranslucent(alpha-0.05)		HL02 N 0 ThrustThingZ(0, 1, 0, 0)
			HL02 N 2 A_SetTranslucent(alpha-0.05)		HL02 O 0 ThrustThingZ(0, 1, 0, 0)
			HL02 O 2 A_SetTranslucent(alpha-0.05)		HL02 P 0 ThrustThingZ(0, 1, 0, 0)
			HL02 P 2 A_SetTranslucent(alpha-0.05)		HL02 Q 0 ThrustThingZ(0, 1, 0, 0)
			HL02 Q 2 A_SetTranslucent(alpha-0.05)		HL02 R 0 ThrustThingZ(0, 1, 0, 0)
			HL02 R 2 A_SetTranslucent(alpha-0.05)		HL02 S 0 ThrustThingZ(0, 1, 0, 0)
			HL02 S 2 A_SetTranslucent(alpha-0.05)		HL02 T 0 ThrustThingZ(0, 1, 0, 0)
			HL02 T 2 A_SetTranslucent(alpha-0.05)		HL02 T 0 ThrustThingZ(0, 1, 0, 0)
			HL02 T 2 A_SetTranslucent(0.0)
			TNT1 A 0 A_PlaySound("kingpin/teleport", CHAN_BODY, 1.0)
			TNT1 A 0 A_SpawnItemEx("HLKingpinTeleport",0,0,40,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_ChangeVelocity(random(-512,512),random(-512,512),CVF_RELATIVE,CVF_REPLACE)
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(random(-512,512),random(-512,512),CVF_RELATIVE,CVF_REPLACE)
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(random(-512,512),random(-512,512),CVF_RELATIVE,CVF_REPLACE)
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(random(-512,512),random(-512,512),CVF_RELATIVE,CVF_REPLACE)
			TNT1 A 2
			TNT1 A 0 A_ChangeVelocity(random(-512,512),random(-512,512),CVF_RELATIVE,CVF_REPLACE)
			TNT1 A 2
			TNT1 A 0 A_Stop
			TNT1 A 0 A_ChangeFlag("NOGRAVITY", false)
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", true)
			TNT1 A 0 A_SetTranslucent(1.0)
			TNT1 A 0 A_PlaySound("kingpin/teleport", CHAN_BODY, 1.0)
			TNT1 A 0 A_SpawnItemEx("HLKingpinTeleport",0,0,40,0,0,0,0,SXF_NOCHECKPOSITION)
			Goto SeeLoop
		Death:
			TNT1 A 0 A_Stopsound(CHAN_WEAPON)
			TNT1 A 0 A_RemoveChildren(TRUE)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)							
			TNT1 A 0 A_Noblocking			
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoGravity
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_ChangeVelocity(0,0,0.1,CVF_REPLACE)
			HL00 ABCDEFGHIJKLMNOPQRSTU 2 A_SetScale(scalex+0.025,scaley+0.025)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)						
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,16,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,-16,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,-16,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,16,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,16,64,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,-16,64,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,-16,64,0,0,0,0,0)
			TNT1 A 5 A_SpawnItemEx("HLAlienGibSpawner",16,16,64,0,0,0,0,0)
			stop

		// SamsaraMonsterAlienBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.DukePlasma
		Death.RottDisintegrate:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.RottDisintegrate
		Death.Disintegrate:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Disintegrate
		Death.Nuke:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Ice	
	}
}

Actor HL_Kingpinpuff : BulletPuff
{
	DamageType "Electric"
	+NOGRAVITY
	+ALWAYSPUFF
	-ALLOWPARTICLES
	-RANDOMIZE
	Radius 1
	Height 1
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Death:	
		XDeath:
		Crash:
			TNT1 A 1 A_Playsound("slv_impact")
			Stop
	}
}

Actor HLKingPinShieldUp : Inventory { Inventory.Maxamount 1}

Actor HLKingpinShield
{
	+NOINTERACTION
	+ISMONSTER
	-SHOOTABLE
	-COUNTKILL
	Scale 0.95
	RenderStyle Stencil
	StencilColor Purple
	Alpha 0.25
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 2 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		HL00 A 2 Bright A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		loop
	}
}

Actor HLKingpinPsychicFlare1
{
	+NOINTERACTION
	+ISMONSTER
	-SHOOTABLE
	-COUNTKILL
	Scale 0.25
	RenderStyle Add
	Alpha 0.4
	States
	{
		Spawn:
		TNT1 A 0
		GWGL ABCD 2 Bright A_Warp(AAPTR_MASTER,16,0,48,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		loop
	}
}

Actor HLKingpinPsychicFlare2 : HLKingpinPsychicFlare1
{
	States
	{
		Spawn:
		TNT1 A 0
		GWGL ABCD 2 Bright A_Warp(AAPTR_MASTER,0,-16,64,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		loop
	}
}

Actor HLKingpinPsychicFlare3 : HLKingpinPsychicFlare1
{
	States
	{
		Spawn:
		TNT1 A 0
		GWGL ABCD 2 Bright A_Warp(AAPTR_MASTER,0,16,64,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		loop
	}
}

Actor HLKingpinTeleport
{
	+NOINTERACTION
	RenderStyle Add
	Alpha 0.9
	States
	{
		Spawn:
		TNT1 A 0
		KPTP ABCDEFGHIJKLMNOPQRSTUVWXY 3 Bright
		stop
	}
}

actor HLKingPinSeeker
{
	Renderstyle add
	+SEEKERMISSILE
	+NOGRAVITY
	damage (30)
	Speed 8
	Height 6
	Radius 3
	Projectile
	DamageType Electric
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("kingpin/seekeramb", CHAN_6, 1.0, true)
		SpawnLoop:
			NHTH ABCDEFGHIJK 2 bright A_SeekerMissile(0, 10, SMF_PRECISE)
			loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_6)
			TNT1 A 0 A_PlaySound("kingpin/seekerexp", CHAN_6, 1.0)
			TNT1 A 2 A_SpawnItemEx("KingPinWave_Effect",0,0,0,0,0,-128,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Explode(30, 48, 0, 0, 32)
			TNT1 A 2 A_SpawnItemEx("KingPinWave_Effect",0,0,0,0,0,-128,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Explode(30, 96, 0, 0, 64)
			stop
	}
}

Actor KingPinWave_Effect
{
	Renderstyle Add
	+THRUACTORS
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Alpha 0.6
	States
	{
	Spawn:
		TNT1 A 0
		//TNT1 A 0 A_ChangeVelocity(0,0,-128,CVF_REPLACE)
		HL00 ABCDEFG 1
		stop
	}
}