Actor Geneworm : SamsaraMonsterAlienBoss
{
	Radius 128
	Height 100
	Scale 1.0
	tag "Gene Worm"
    Obituary "%o was killed by the Gene Worm."
	ActiveSound "Geneworm/Idle"
	Deathsound "GeneWorm/Death"
	Monster
	Mass 50000
	Health 3000
	MeleeRange 512
	+MISSILEMORE
	+MISSILEEVENMORE
	+LOOKALLAROUND
	+DONTHARMCLASS
	+BOSS
	+BOSSDEATH
	+NOICEDEATH
	+NOINFIGHTING
	+LOOKALLAROUND
	+NOBLOODDECALS
	+DONTGIB
	RenderStyle Translucent
	//Species "Enemies"
	BloodColor "Khaki"
	BloodType "HalfLifeAlienBlood"
	Speed 4
	Scale 5
	PainChance "ShrinkerDMG", 0
	DamageFactor "Poison", 0.0
	DamageFactor "PoisonBolt", 0.0
	States
	{
	Spawn:
		HL00 A 0
		HL00 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		HL00 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		HL00 A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		TNT1 A 0
		/*TNT1 A 0 A_ChangeFlag("Invulnerable",True)
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm2")
		TNT1 A 0 A_CustomMissile("GeneSpriteRay",80,0,angle,CMF_AIMDIRECTION)
		TNT1 A 0 A_CustomMissile("GeneSpriteRay",80,0,angle+90,CMF_AIMDIRECTION)
		TNT1 A 0 A_CustomMissile("GeneSpriteRay",80,0,angle-90,CMF_AIMDIRECTION)
		TNT1 A 0 A_CustomMissile("GeneSpriteRay",80,0,angle+180,CMF_AIMDIRECTION)
		TNT1 A 0 A_SetTranslucent(0.0)
		TNT1 A 448
		HL00 AAAAAAAAAAAAAAA 4
		HL00 AAAAAAAAAAA 4
		HL00 AAAAAAAAA 4 A_SetTranslucent(alpha+0.1)
		TNT1 A 0 A_SetTranslucent(1.0)
		TNT1 A 0 A_ChangeFlag("Invulnerable",False)*/
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm2")
		Goto SpawnLoop
	SpawnLoop:
		TNT1 A 0 A_Jump(16,2)
		TNT1 A 0 A_Playsound("Geneworm/Idle")
		HL00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 5 A_Look
		HL01 ABCDE 5 A_Look
		loop
	Idle:
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:		
		TNT1 A 0 A_Jump(16,2)
		TNT1 A 0 A_Playsound("Geneworm/Idle", CHAN_VOICE, 1.0, false, ATTN_NONE)
		HL00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 5 A_Chase("Melee", "Missile")
		HL01 ABCDE 5 A_Chase("Melee", "Missile")
		loop
	Missile:
		TNT1 A 0 A_JumpIfCloser(384, "Melee")
		//TNT1 A 0 A_Playsound("GeneWorm/Breath",CHAN_6)
		TNT1 A 0 A_Playsound("GeneWorm/BreathLoop",CHAN_6,1.0,false,ATTN_NONE)
		TNT1 A 0 A_FaceTarget
		HL01 ABCDEFGHIJKLM 5
	MissileLoop:
		HL01 N 0 A_SpawnItemEx("GeneWormBreathSprayer", 128, 0, 100, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER)
		HL01 NOPQRSTUVWXYZ 5 
		HL02 ABCDEFGHIJKLM 5
		Goto See
	Melee:
		HL02 N 0 A_FaceTarget
		HL02 NOPQRSTUVWXYZ 5
		HL03 A 0 A_SpawnItemEx("Geneworm_Smash",cos(angle)*128,sin(angle)*128,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)//A_CustomMissile("Geneworm_Smash")
		//TNT1 A 0 A_Explode(100,540,XF_NOTMISSILE|!XF_HURTSOURCE)
		TNT1 A 0 A_Quake(8, 15, 256, 512, "world/quake")
		HL03 ABCDEFGHI 5
		Goto See
	PainMod1:
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm1",0,1)
		Goto BigPain1
	PainMod2:
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm1",0,2)
		Goto BigPain1
	PainMod3:
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm1",0,3)
		Goto BigPain1
	PainMod4:
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm1",0,4)
		Goto BigPain1
	BigPain1:
		TNT1 A 0 A_PlaySound("GeneWorm/Pain1", CHAN_VOICE, 1.0, false, ATTN_NONE)
		TNT1 A 0 A_ChangeFlag("Invulnerable",True)
		HL03 JKLMNOPQRSTUVWXYZ 5
		HL04 ABCDEFGHIJKMNO 5
		HL04 P 0 A_SpawnItemEx("GeneWorm_MinionSpawner",0,0,40,10,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
	BigPain2:
		HL04 P 0 A_PlaySound("GeneWorm/Pain2", CHAN_VOICE, 1.0, false, ATTN_NONE)
		HL04 PQRSTUVWXYZ 5
		HL05 ABCDEFGHIJKLMNOPQRSTUVW 5
		Goto BigPain3
	BigPain3:
		HL06 A 0 A_PlaySound("GeneWorm/Pain3", CHAN_VOICE, 1.0, false, ATTN_NONE)
		//HL05 XYZ 5
		HL06 ABCDEFGHIJKLMNOPQRSTUVWXYZ 5
		HL07 ABCDE 5
		Goto BigPain4
	BigPain4:
		HL07 F 0 A_PlaySound("GeneWorm/Pain4", CHAN_VOICE, 1.0, false, ATTN_NONE)
		HL07 FGHIJKLMNOPQRSTUVWXYZ 5
		HL08 ABCDEFGHIJK 5
		HL08 L 0 A_RemoveChildren(TRUE)
		HL08 LMNOPQRSTU 5
		TNT1 A 0 A_ChangeFlag("Invulnerable",FALSE)
		Goto See		
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		HL08 VWXYZ 8
		TNT1 A 0 A_SpawnItemEx("GeneWorm_Death",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("GeneWorm_PortalStorm",64,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		HL09 ABCDEFGHIJKLMNOPQRSTUVWXYZ 16
		TNT1 A 0 A_SpawnItemEx("Geneworm_PortalDestroy",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		HL10 ABCDEFGHIJK 8
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,16,0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,-16,0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,-16,0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,16,0,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,16,64,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",-16,-16,64,0,0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",16,-16,64,0,0,0,0,0)
		TNT1 A 5 A_SpawnItemEx("HLAlienGibSpawner",16,16,64,0,0,0,0,0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_RemoveChildren(TRUE)
		TNT1 A 0 A_KillMaster
		TNT1 A 5 ACS_NamedExecuteAlways("HLGeneWorm5")
		stop

		// SamsaraMonsterAlienBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.DukePlasma
		Death.RottDisintegrate:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.RottDisintegrate
		Death.Disintegrate:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Disintegrate
		Death.Nuke:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" A 0 A_RemoveChildren(TRUE)
			Goto Super::Death.Ice	
	}
}

Actor GeneWormOrbToken : Inventory { Inventory.MaxAmount 4 }
Actor StopSeqToken : Inventory { Inventory.MaxAmount 1 }
Actor StopSeqToken2 : Inventory { Inventory.MaxAmount 1 }

Actor GeneWorm_Death 
{
	+CLIENTSIDEONLY
	+THRUACTORS
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAA 1 A_CustomMissile("GeneSprite",48,0,random(0,180),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,random(0,180))
		TNT1 A 16 A_CustomMissile("HLDisplacerProjectile",48,0,random(0,180),CMF_ABSOLUTEANGLE|CMF_ABSOLUTEPITCH,random(0,180))
		loop
	}
}

Actor GeneWormBreathSprayer
{
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+THRUACTORS
	+NOGRAVITY
	+NOBLOCKMAP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_FaceTarget(0,0)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 2 A_CustomMissile("GeneWormBreath", 0, 0, angle, CMF_ABSOLUTEPITCH|CMF_ABSOLUTEANGLE , pitch)
			stop
	}
}

Actor GeneWormBreath 
{
	Height 32
	Radius 16
	Speed 48
	Scale 0.75
	Alpha 0.9
	Projectile
	+THRUACTORS
	+EXTREMEDEATH
	DamageType "Poison"
	RenderStyle Add
	States
	{
	Spawn:
		TNT1 A 0
		GENE ABCD 1 A_Explode(20,24,!XF_HURTSOURCE,FALSE,24)
	SpawnLoop:
		GENE EFG 1 A_Explode(20,24,!XF_HURTSOURCE,FALSE,24)
		loop
	Death:
		GENE HIJKLM 1 A_Fadeout(0.17)
		stop
	}
}

Actor GeneWorm_PortalDestroy
{
	Scale 1.0
	Radius 100
	Height 100
	+THRUSPECIES
	Species "HLEnemies"
	States
	{
	Spawn:
		HL00 A 70
		HL00 AAAAAAAAAAAAAA 4 A_SpawnItemEx("HLMegaExplosionBoom",random(-64,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

Actor GeneWorm_PortalStorm
{
	Renderstyle add
	+THRUACTORS
	+NOBLOCKMAP
	Scale 2.0
	States
	{
	Spawn:
		TNT1 A 0
		GWSC ABCDEFGHIJKLMNOPQRSTUVWXY 4 Bright
		stop
	}
}

Actor GeneWorm_MinionSpawner
{
	Renderstyle add
	+NOGRAVITY
	-SHOOTABLE
	-SOLID
	+ISMONSTER
	States
	{
	Spawn:
		TNT1 A 0 
		TNT1 A 0 ACS_NamedExecuteAlways("HLGeneWorm3")
		TNT1 A 0 A_Playsound("GeneWorm/PortalFire", CHAN_6)
		GWSC ABCDEFGHIJKLMNOPQRS 3 Bright
		TNT1 AAAAAAAAAAAAAA 0 A_CustomRailgun(0,0,"none","orange",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_EXPLICITANGLE,0,frandom(7,12),"none",frandom(-50,50),frandom(-50,50),256,2,0.0)
		TNT1 A 0 A_Playsound("GeneWorm/PortalSpawn", CHAN_6)
		TNT1 A 0 A_SpawnItemEx("ShockTrooper",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		GWSC TUVWXY 2 Bright
		Goto WaitLoop
	WaitLoop:
		TNT1 A 1
		loop
	Quit:
		TNT1 A 0 
		stop
	}
}

Actor GeneSpriteRay
{
	Speed 256
	Damage 0
	+THRUACTORS
	Projectile
	Height 4
	Radius 2
	States
	{
	Spawn:
		TNT1 A 1 
		loop
	Death:
		TNT1 AAAAAAAAAAAAAAAA 16 A_CustomMissile("GeneSprite",0,0,-angle,CMF_ABSOLUTEANGLE)
		stop
	}
}
	
Actor GeneSprite
{
	Height 4
	Radius 4
	Projectile
	Speed 5
	Renderstyle add
	Scale 0.6
	+THRUACTORS
	+NOINTERACTION
	+CLIENTSIDEONLY
	-SOLID
	States
	{
	Spawn:
		TNT1 A 0
		GWSP ABC 3
		loop
	Death:
		"####" "#" 2 A_Fadeout(0.1)
		stop
	}
}

Actor Geneworm_Smash
{
	Projectile
	Height 4
	Radius 4
	Speed 64
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1
			TNT1 A 0 A_Explode(30, 256,0,0,96)
			stop
	}
}

Actor HLGenewormCeilingDetector
{
	Height 64
	Radius 128
	+ISMONSTER
	-SHOOTABLE
	-COUNTKILL
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_CheckCeiling("Failure")
			goto Success
		Success:
			TNT1 A 0 A_GiveInventory("HLPitWormSpawnToken", 1, AAPTR_MASTER)
			stop
		Failure:
			TNT1 A 0
			stop
	}
}