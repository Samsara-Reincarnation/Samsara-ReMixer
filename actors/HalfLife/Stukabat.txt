actor Stukabat : SamsaraMonsterAlien
{
	Species "Enemies"
	+FLOORCLIP
	+NOINFIGHTING
	+FLOAT
	+NOGRAVITY
	+SPAWNFLOAT
	+SPAWNCEILING
	+CEILINGHUGGER
	+NOBLOODDECALS
	+DONTGIB
	Monster
	Height 20
	Radius 24
	Speed 8
	Floatspeed 8
	Health 100
	Tag "Stukabat"
    Obituary "%o was killed by a Stukabat."
	Scale 0.9
	Seesound ""
	Painsound ""
	Deathsound ""
	Activesound ""
	Painchance 32
	BloodColor "Khaki"
	BloodType "HalfLifeAlienBlood"
	MeleeRange 56
	PainChance "ShrinkerDMG", 0
	States
	{
		Spawn:
			HL06 A 0
			HL06 A 1 A_JumpIfInventory("AlreadySpawned",1,"SpawnStandard")
			HL06 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			HL06 A 0 A_GiveInventory("AlreadySpawned",1)
		SpawnStandard:
			HL00 A 0 A_Jump(128, "SpawnCeiling") 
			Goto Drop
		SpawnLoop:	
			HL06 A 0 A_Jump(64,"Spawn2")
			HL06 A 0
			HL06 ABCDEFGHIJK 3 A_LookEx(0,0,0,0,180,"SeeGround")
			Loop
		Spawn2:
			HL07 ABCDEFGHIJKLMNOPQRSTUVW 3 A_LookEx(0,0,0,0,180,"SeeGround")
			Goto SpawnLoop
		SpawnCeiling:
			HL00 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			HL00 A 0 A_Jump(64,"Spawn2Ceiling")
			HL00 ABCDEFGHIJK 3 A_Look
			Loop
		Spawn2Ceiling:
			HL01 ABCDEFGHIJKLMNOP 3 A_Look
			Goto SpawnCeiling
		Drop:
			HL00 A 0 A_ChangeFlag("CEILINGHUGGER", false)
			HL00 A 0 A_ChangeFlag("NOVERTICALMELEERANGE", false)
			HL00 A 0 A_ChangeFlag("SPAWNCEILING", false)
			HL00 A 0 A_ChangeFlag("NOGRAVITY", false)
			HL00 A 0 A_ChangeFlag("SPAWNFLOAT", false)
			Goto SpawnLoop
		See:
			HL00 A 0 A_ChangeFlag("CEILINGHUGGER", false)
			HL00 A 0 A_ChangeFlag("NOVERTICALMELEERANGE", false)
			HL00 A 0 A_ChangeFlag("SPAWNCEILING", false)
			HL11 ABCDEF 3 A_Chase
			HL11 G 0 A_PlaySound("Stukabat/Flap", CHAN_BODY)
			HL11 GHIJKLM 3 A_Chase
			Loop
		SeeGround:
			HL11 A 0 A_ChangeFlag("NOPAIN", 0)
			HL11 A 0 A_Jump(64,"TakeOff")
			HL11 ABCDEFGHI 3 A_Chase("","")
			Loop
		TakeOff:
			HL13 ABCDEFGHIJKLM 3
			HL13 N 0 A_ChangeFlag("NoGravity", true)
			HL13 NOP 3
		Melee:
			HL02 ABCD 3
			HL02 E 0 A_CustomMeleeAttack(random(4,5),"HLZombie/Claw","HLZombie/Miss")
			HL02 EG 3
			Goto See
		Pain:
			TNT1 A 0 A_ChangeFlag(NOPAIN,1)
			TNT1 A 0 A_CheckFlag("NOGRAVITY", 1)
			Goto PainGround
			HL05 A 0 A_ChangeFlag("NOGRAVITY",0)
			HL05 ABCDEFGHIJK 3 A_CheckFloor("SeeGround")
			Loop
		PainGround:
			TNT1 A 0 A_Pain
			HL09 ABCDE 3
			TNT1 A 0 A_ChangeFlag(NOPAIN,0)			
			Goto SeeGround
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag(NOPAIN,1)
			TNT1 A 0 A_CheckFlag("NOGRAVITY", 1)
			Goto BootPainGround
			HL05 A 0 A_ChangeFlag("NOGRAVITY",0)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			HL09 A 3 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			HL05 ABCDEFGHIJK 3 A_CheckFloor("SeeGround")
			Goto See
		BootPainGround:
			TNT1 A 0 A_Pain
			HL09 ABCDE 3
			TNT1 A 0 A_ChangeFlag(NOPAIN,0)			
			Goto SeeGround
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)					
			TNT1 A 0 A_Scream
			TNT1 A 0 A_Fall
			TNT1 A 0 A_CheckFlag("NOGRAVITY", 1)
			Goto DeathGround
			HL03 ABCDEFGHIJKLMNOPQRST 3
			HL03 U -1
			Stop
		DeathGround:
			HL04 ABCDEFGHIJKLMNOPQRST 3
			HL04 U -1
			Stop
		Death.HLLaser:
		Death.HLExplosive:
		Death.HLRPGExplosive:	
			TNT1 A 0 A_JumpIfHealthLower(-20,1)
			Goto Death
		Death.HLEgon:
		Death.FreemanGibs:
		XDeath:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)			
			TNT1 A 0 A_SpawnItemEx("HLAlienGibSpawner",0,0,0,0,0,0,0,SXF_USEBLOODCOLOR)
			TNT1 A 5 A_NoBlocking
			Stop
	}
}