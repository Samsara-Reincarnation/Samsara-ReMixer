ACTOR IPOGSentrySphere : SamsaraMonsterRobotic
{
	Health 350
	Radius 31
	Height 56
	Mass 400
	Speed 16
	PainChance 30
	Monster
	+NOBLOOD
	+FLOAT
	+NOGRAVITY
	+NOINFIGHTING
	SeeSound "ipogsentry/sight"
	PainSound "ipogsentry/pain"
	DeathSound "ipogsentry/death"
	ActiveSound "ipogsentry/active"
	Obituary "%o was neutralized by a Sentry Sphere."
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	Tag "Sentry Sphere"
	Species "IPOGEnemies"
	States
	{
	Spawn:
		IPOC A 0
		IPOC A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		IPOC A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		IPOC A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		IPOC A 5 A_Look
		Loop
	See:
		IPOC A 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		IPOC A 5 A_FaceTarget
		IPOC B 3 Bright A_FaceTarget
		IPOC A 0 A_PlaySound("ipogsentry/fire")
		IPOC B 0 Bright A_CustomMissile("IPOGSentryShot",16,0,0,8)
		IPOC B 0 Bright A_CustomMissile("IPOGSentryShot",48,0,0,8)
		IPOC B 0 Bright A_CustomMissile("IPOGSentryShot",32,-18,0,8)
		IPOC B 6 Bright A_CustomMissile("IPOGSentryShot",32,18,0,8)
		IPOC A 2
		Goto See
	Pain:
		IPOC A 3
		IPOC A 3 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		IPOC A 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		IPOC A 16 A_Pain
		Goto See
	Death:
		IPOC G 1 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		IPOC G 1 A_Scream
		IPOC G 0 A_SpawnItemEx("PGRocketPuff",0,0,0,0,0,0,4)
		IPOC DEF 2 A_SpawnItemEx("PGRocketSmallPuff00",random(-32,32),random(-32,32),random(-24,24),0,0,random(1,3),0)
		IPOC G 3 A_SpawnItemEx("PGRocketSmallPuff00",random(-32,32),random(-32,32),random(-24,24),0,0,random(1,3),0)
		TNT1 AAAAA 0 A_CustomMissile("ipogbigguardgib1",random(25,75),0,random(-180,180),2,random(10,25))
		TNT1 AAAAA 0 A_CustomMissile("ipogbigguardgib2",random(25,75),0,random(-180,180),2,random(10,25))
		TNT1 AAAAA 0 A_CustomMissile("ipogbigguardgib3",random(2,75),0,random(-180,180),2,random(10,25))
		TNT1 AAAAA 0 A_CustomMissile("ipogbigguardgib4",random(25,75),0,random(-180,180),2,random(10,25))
		IPOC HIJ 2 A_SpawnItemEx("PGRocketSmallPuff00",random(-32,32),random(-32,32),random(-24,24),0,0,random(1,3),0)
		IPOC K 5
		TNT1 A 1 A_NoBlocking
		TNT1 A -1
		Stop	
	}
}

Actor PGRocketSmallPuff00 : PGRocketSmallPuff { Scale 0.5 }

ACTOR IPOGSentryShot
{
	Radius 6
	Height 8
	Speed 25
	Damage (Random(1,4))
	Projectile 
	+RANDOMIZE
	States
	{
	Spawn:
		IPOC LMLN 4 Bright
		Loop
	Death:
		IPOC L 4 Bright
		Stop
	}
}

ACTOR IPOGSentrySphere0 : SamsaraMonsterRobotic
{
	Health 150
	Radius 16
	Height 56
	Mass 50
	Speed 20
	PainChance 80
	Scale 0.60
	Monster
	+NOBLOOD
	+FLOAT
	+NOGRAVITY
	SeeSound "ipogsentry/sight"
	PainSound "ipogsentry/pain"
	DeathSound "ipogsentry/death"
	ActiveSound "ipogsentry/active"
	Obituary "%o did not expect to find a Sentry Sphere prototype."
	Tag "Prototype Sentry Sphere"
	States
	{
	Spawn:
		IPOC A 5 A_Look
		Loop
	See:
		IPOC A 3 A_Chase
		Loop
	Missile:
		IPOC A 5 A_FaceTarget
		IPOC B 3 Bright A_FaceTarget
		IPOC A 0 A_PlaySound("ipogsentry/fire")
		IPOC B 6 Bright A_CustomMissile("IPOGSentryShot",32,0,0,8)
		IPOC A 2
		Goto See
	Pain:
		IPOC A 3
		IPOC A 3 A_Pain
		Goto See
	Death:
	XDeath:
		IPOC G 1 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		IPOC G 1 A_Scream
		IPOC K 3 A_SpawnItemEx("PGRocketPuff",0,0,0,0,0,0,4)
		TNT1 A 3 A_SpawnItemEx("PGRocketPuff",0,0,0,0,0,0,4)
		TNT1 A 3 A_SpawnItemEx("PGRocketPuff",0,0,0,0,0,0,4)
		TNT1 A 3 A_NoBlocking
		TNT1 A -1
		Stop
	}
}

Actor IPOGSentrySphereSmallDisintegrate : ZombiemanDisintegrate
{
  Scale 0.60
  States
  {
  Spawn:
    TNT1 A 0
    IPOC A 3 A_SpawnItem("DisintEffectSmall",0,10)
    IPOC A 3 A_SetTranslucent(0.9,0)
    IPOC A 3 A_SetTranslucent(0.8,0)
    IPOC A 3 A_SetTranslucent(0.7,0)
    IPOC A 3 A_SetTranslucent(0.6,0)
    IPOC A 3 A_SetTranslucent(0.5,0)
    IPOC A 2 A_SetTranslucent(0.4,0)
    IPOC A 2 A_SetTranslucent(0.3,0)
    IPOC A 2 A_SetTranslucent(0.2,0)
    IPOC A 2 A_SetTranslucent(0.1,0)
    Stop
  }
}