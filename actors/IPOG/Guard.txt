Actor IPOGGuard : SamsaraMonsterRobotic
{
	Health 350
	Radius 22
	Height 64
	Speed 9
	Mass 500
	PainChance 30
	Scale 0.50
	Monster
	+FLOORCLIP
	+NOBLOOD
	+BOSSDEATH
	+DONTRIP	
	+NOINFIGHTING
	SeeSound "ipogguard/sight"
	PainSound "ipogguard/pain"
	DeathSound "ipogguard/death"
	ActiveSound "ipogguard/active"
	Obituary "%o was pacified by a Robot Guard."
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Tag "Robot Guard"
	Species "IPOGEnemies"
	States
	{
	Spawn:
		IPO2 B 0
		IPO2 B 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		IPO2 B 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		IPO2 B 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		IPO2 B 5 A_Look
		Loop
	See:
		IPO2 AABBCCBB 4 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
	    IPO2 A 0 A_Jump(98,2)
	    IPO2 A 0 A_Jump(98,"Missile2","Missile3","Missile4")
		IPO2 D 4 A_FaceTarget
		IPO2 D 0 A_FaceTarget
		IPO2 D 0 A_PlaySound("ipogguard/fire")
		IPO2 D 2 bright A_CustomMissile("IPOGGuardShot",32,0,0,8)
		IPO2 E 8
		Goto See
    Missile2:
	    IPO2 D 4 A_FaceTarget
		IPO2 D 0 A_PlaySound("ipogguard/fire")
		IPO2 D 2 bright A_CustomMissile("IPOGGuardShot",32,0,random(-8,8),8)
		IPO2 E 8
		Goto See
	Missile3:
	    IPO2 D 4 A_FaceTarget
		IPO2 D 0 A_PlaySound("ipogguard/fire")
		IPO2 D 2 bright A_CustomMissile("IPOGGuardShot",32,0,random(-12,12),8)
		IPO2 E 8
		Goto See
	Missile4:
	    IPO2 D 4 A_FaceTarget
		IPO2 D 0 A_PlaySound("ipogguard/fire")
		IPO2 D 2 bright A_CustomMissile("IPOGGuardShot",32,0,random(-4,4),8)
		IPO2 E 8
		Goto See
	Pain:
		IPO2 F 3
		IPO2 F 3 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		IPO2 F 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		IPO2 F 16 A_Pain
		Goto See
	Death:
	    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		IPO2 G 4
		IPO2 H 4 A_Scream
		IPO2 I 4 A_NoBlocking
		IPO2 J 4
		IPO2 K 4
		IPO2 L 4
		IPO2 M 4
		IPO2 N 4
		IPO2 N -1 A_KillMaster
		Stop
	}
}

Actor IPOGGuardShot
{
	Radius 12
	Height 8
	Speed 20
	Damage (Random(12,32)-random((Random(4,8)),0))
	Projectile 
	+RANDOMIZE
	DamageType Fire
	DeathSound "greed/flamehit"
	States
	{
	Spawn:
		IPO2 P 4 Bright
		Loop
	Death:
		TNT1 A 1 Bright A_CustomMissile("IPOGGuardFake",0,0,0)
		Stop
	}
}

Actor IPOGGuardFake
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.65
	States
	{
	  Spawn:
	    GP31 ABCD 3 bright
        Stop
    }
}