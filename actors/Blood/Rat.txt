Actor BloodRat : SamsaraMonsterBeastTotalGib
{
	Tag "Rat"
	Monster
	Mass 20
	Height 8
	Radius 10
	Speed 12
	Health 2
	MeleeRange 32
	Species "BloodEnemies"
	Seesound "BloodRat/See"
	Deathsound "BloodRat/Die"
	Activesound "BloodRat/Act"
	PainSound "BloodRat/Pain"
	Scale 0.40
	Obituary "%o was bitten by a rat."
	BloodType "NashGore_Blood_Monolith"
	+DROPOFF
	+FLOORCLIP
	+NOTDMATCH
	+NOINFIGHTING
	+THRUGHOST
	+NOBLOODDECALS
	-COUNTKILL
	PainChance "ShrinkerDMG", 0
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
	Idle:
		BRAT A 2 A_Wander
		BRAT A 0 A_Look
		BRAT B 2 A_Wander
		BRAT A 0 A_Look
		BRAT C 2 A_Wander
		BRAT A 0 A_Look
		BRAT D 2 A_Wander
		BRAT A 0 A_Look
		loop
	Spawn:
		BRAT A 0
		BRAT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		BRAT A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		BRAT A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		BRAT A 5 A_Look
		Loop
	See:
		BRAT AABBCCDD 2 A_Chase
		Loop
	Pain:
	Pain.Shock:
	Pain.Tesla:
		BRAT A 2 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		BRAT A 2 A_FaceTarget
		TNT1 A 0 A_Recoil(10)
		BRAT A 16 A_Pain
		Goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		BRAT AA 1 A_FaceTarget
		TNT1 A 0 A_CustomMeleeAttack(4)
		BRAT A 20
		Goto See
	XDeathForCaleb:
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
		TNT1 A 0 A_Jump(128,1,2)
		TNT1 AAA 0 A_SpawnItemEx("NashGore_FlyingBlood_Monolith", 0, 0, 0, FRandom(0.1, 6.0) * Random(-1, 1), FRandom(0.1, 6.0) * Random(-1, 1), FRandom((Random(3.0,1.0)), (Random(8.0,6.0))) * FRandom((Random(3,2)), 1), 0, NASHGORE_MONOLITH_BLOODFLAGS1, 64)
		BRAT E 5 A_Scream
		BRAT F 5 A_Fall
		BRAT GHIJ 4
		BRAT J -1
		Stop
	Death.CalebNapalm:
	Death.Vodoo:
	Death.VoodooPuff:
	Death.DynamiteCoop:
	Death.Dynamite:
	Death.Shock:
	Death.Tesla:
	Death.CalebBullet:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
	Burn:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		BRAT K 0 A_PlaySound("BloodRat/Burn",5,1.0,0,ATTN_NORM)
		TNT1 A 0 A_PlaySound("Blood/BurningBody",6,1.0,0,ATTN_NORM)
	LoopBurn:
		BRAT KKKKKKKKKKKKKKKKKKKK 2 A_SetAngle(angle - 15)
		BRAT A 0 A_Jump(120,"LoopBurn")
		TNT1 A 0 A_PlaySound("Blood/CrispyBody",CHAN_BODY,1.0,0,ATTN_NORM)
		BRAT K 0 A_Fall
		BRAT LMNLMNLMNLMNLMNLMNLMNLMNLMN 4 Bright
		BRAT LMNLMNLMNLMNLMNLMNLMNLMNLMNO 4
		BRAT O -1
		Stop
	}
}