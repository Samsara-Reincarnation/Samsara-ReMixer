Actor BloodBat : SamsaraMonsterBeast
{
	Health 2
	MONSTER
	SeeSound "Bat/Active"
	ActiveSound "Bat/Active"
	DeathSound "bat/die"
	PainSound "bat/pain"
	AttackSound ""
	Speed 15
	Tag "Bat"
	PainChance 10
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Height 36
	Species "BloodEnemies"
	Radius 20
	Obituary "%o was bitten by a bat."
	Mass 30
	// +NOPAIN
	+NOGRAVITY
	+FLOAT
	-COUNTKILL
	+FLOATBOB
	+FLOORCLIP
	+NOTARGET
	+SPAWNFLOAT
	+NOINFIGHTING
	+NOBLOODDECALS
	BloodType "NashGore_Blood_Monolith"
	Scale 0.7
	PainChance "ShrinkerDMG", 0
	States
	{
	Spawn:
		BBAT A 0
		BBAT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		BBAT A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		BBAT A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		BBAT ABCDE 3 A_Look
		Loop
	See:
		BBAT A 3 A_Chase
		TNT1 A 0 A_Chase("Melee","",CHF_NOPLAYACTIVE)
		BBAT B 3 A_Chase("Melee","Missile",CHF_NOPLAYACTIVE)
		BBAT C 3 A_Chase
		TNT1 A 0 A_Chase("Melee","",CHF_NOPLAYACTIVE)
		BBAT D 3 A_Chase("Melee","Missile",CHF_NOPLAYACTIVE)
		BBAT E 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		BBAT A 1 A_FaceTarget
		BBAT A 1 A_SkullAttack
		BBAT A 1 A_FaceTarget
		BBAT BCDE 3 A_FaceTarget
	LoopMyMissile:
		BBAT ABCDE 3 A_FaceTarget
		Loop
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		BBAT A 2 A_FaceTarget
		TNT1 A 0 A_CustomMeleeAttack(4)
		BBAT B 2 A_FaceTarget
		Goto GoAway
	GoAway:
		BBAT A 3 A_Chase("","")
		TNT1 A 0 A_Chase("","",CHF_NOPLAYACTIVE)
		BBAT B 3 A_Chase("","",CHF_NOPLAYACTIVE)
		BBAT C 3 A_Chase("","")
		TNT1 A 0 A_Chase("","",CHF_NOPLAYACTIVE)
		BBAT D 3 A_Chase("","",CHF_NOPLAYACTIVE)
		BBAT E 3 A_Chase("","")
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_ChangeFlag(FLOATBOB,0)
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
	ADSF:
		TNT1 A 0 A_Jump(128,1,2)
		TNT1 AAA 0 A_SpawnItemEx("NashGore_FlyingBlood_Monolith", 0, 0, 0, FRandom(0.1, 6.0) * Random(-1, 1), FRandom(0.1, 6.0) * Random(-1, 1), FRandom((Random(3.0,1.0)), (Random(8.0,6.0))) * FRandom((Random(3,2)), 1), 0, NASHGORE_MONOLITH_BLOODFLAGS1, 64)
 		BBAT F 4 A_Scream
		BBAT G 4 A_NoBlocking
		BBAT HI 5
		BBAT J 4
		TNT1 A -1
		Stop
	Pain:
	Pain.Shock:
	Pain.Tesla:
		TNT1 A 0 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		BBAT B 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		BBAT B 16 A_Pain
		Goto See
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_ChangeFlag(FLOATBOB,0)
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
		Goto ADSF
	Death.CalebNapalm:
	Death.Vodoo:
	Death.VoodooPuff:
	Death.DynamiteCoop:
	Death.Dynamite:
	Death.Shock:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeathForCaleb")
	Death.Tesla:
	Death.CalebBullet:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death
	XDeathForCaleb:
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1",1)
		Goto XDeath
	Death.CalebFire:
	Death.LifeLeech:
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
	Burn:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_ChangeFlag(FLOATBOB,0)
		TNT1 A 0 A_ChangeFlag("SOLID",false)
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_PlaySound("Blood/BurningBody",CHAN_BODY,1.0,0,ATTN_NORM)
		TNT1 A 0 A_Scream
	KeepBurning:
		BBAT KLMNKLMNKLMNKLMNKLMNKLMNKLMNKLMNKLMN 4 Bright
		TNT1 A 0 A_Jump(40,"KeepBurning")
		TNT1 A 0 A_Gravity
		TNT1 A 0 A_PlaySound("Blood/CrispyBody",CHAN_BODY,1.0,0,ATTN_NORM)
		TNT1 A 0 A_Fall
		BBAT OPQRS 4 Bright
		BBAT S -1
		Stop
	}
}