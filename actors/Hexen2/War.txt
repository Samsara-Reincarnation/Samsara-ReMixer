Actor Hexen2War : Hexen2BossBase
{
	Tag "War"
	Health 6000
	Radius 48
	Height 64
	Speed 6
	Mass 30000
	PainChance 96
	DeathSound "Hexen2/War/Die"
	ActiveSound "Hexen2/War/LaughBig"
	States
	{
		Spawn:
			HX07 A 2 A_Look
			Loop
		See:
			HX06 A 0 A_Jump(26, "AttackStand")
			HX06 A 0 A_Jump(256, "SeeSlow", "SeeMid", "SeeFast")
			Goto SeeSlow
		SeeSlow:
			HX06 A 1 
//			HX06 B 0 A_GiveInventory("Hexen2SpeedToken", 8)
			HX04 B 0 A_Jump(26, "AttackSlow")
			HX04 B 0 A_Jump(26, "RearStart")
			HX06 B 0 A_Jump(38, "SeeMid")
			HX06 B 0 ACS_NamedExecuteAlways("Hexen2_Decorate_Monster", 0, 12)
			HX06 B 0 A_PlaySound("Hexen2/War/Trot3", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX06 BCDEFGHIJK 2 A_Chase
			HX06 LMNOPQRST 2 A_Chase
			Loop
		SeeMid:
			HX05 A 1 
//			HX05 B 0 A_GiveInventory("Hexen2SpeedToken", 10)
			HX04 B 0 A_Jump(26, "AttackMid")
			HX05 B 0 A_Jump(26, "RearStart")
			HX05 B 0 A_Jump(38, "SeeSlow", "SeeFast")
			HX05 B 0 ACS_NamedExecuteAlways("Hexen2_Decorate_Monster", 0, 13)
			HX05 B 0 A_PlaySound("Hexen2/War/Trot2", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX05 BCDEFGH 2 A_Chase
			HX05 IJKLMNOP 2 A_Chase
			Loop
		SeeFast:
			HX04 A 1 
//			HX04 B 0 A_GiveInventory("Hexen2SpeedToken", 12)
			HX04 B 0 A_Jump(26, "AttackFast")
			HX04 B 0 A_Jump(26, "RearStart")
			HX04 B 0 A_Jump(38, "SeeMid")
			HX04 B 0 ACS_NamedExecuteAlways("Hexen2_Decorate_Monster", 0, 14)
			HX04 B 0 A_PlaySound("Hexen2/War/Trot1", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX04 BCDEF 2 A_Chase
			HX04 GHIJKL 2 A_Chase
			Loop
		Pain:
			HX06 A 0 A_PlaySound("Hexen2/War/Laugh", CHAN_VOICE, 1.0, false, ATTN_NONE)
			Goto See
		AttackStand:
			HX03 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX03 A 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 3)
			HX03 BCDEFGH 2 
			HX03 I 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -3)
			HX03 J 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -1)
			HX03 KL 2 
			HX03 M 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -1)
			HX03 NO 2
			HX03 O 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto See
		AttackSlow:
			HX00 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX00 A 0 A_PlaySound("Hexen2/War/Trot3", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX00 A 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 3) HX00 B 0 A_Wander
			HX00 BCDEFG 2 A_Wander
			HX00 H 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -3) HX00 I 0 A_Wander
			HX00 I 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -3) HX00 J 0 A_Wander
			HX00 JKL 2 A_Wander
			HX00 A 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto SeeSlow
		AttackMid:
			HX01 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX01 A 0 A_PlaySound("Hexen2/War/Trot2", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX01 A 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 3) HX01 B 0 A_Wander
			HX01 BCDEFGHI 2 A_Wander
			HX01 J 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -3) HX01 K 0 A_Wander
			HX01 K 2 A_Wander
			HX01 L 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 0) HX01 M 0 A_Wander
			HX01 M 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 0) HX01 N 0 A_Wander
			HX01 NOP 2 A_Wander
			HX01 P 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto SeeMid
		AttackFast:
			HX02 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX02 A 0 A_PlaySound("Hexen2/War/Trot1", CHAN_BODY, 1.0, false, ATTN_NONE)
			HX02 A 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 3) HX02 B 0 A_Wander
			HX02 BCDEFGHIJK 2 A_Wander
			HX02 L 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 2) HX02 M 0 A_Wander
			HX02 M 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 2) HX02 N 0 A_Wander
			HX02 N 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, 0) HX02 O 0 A_Wander
			HX02 O 2 A_CustomMissile("Hexen2WarAxe1", 40, 3, -3) HX02 P 0 A_Wander
			HX02 PQRSTU 2 A_Wander
			HX02 U 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto SeeFast
		RearStart:
			HX09 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX09 ABCDEFG 2
			HX09 HIJKLMNO 2 
			HX09 J 2 A_PlaySound("Hexen2/War/WhinnyBig", CHAN_BODY)
			Goto RearAttack
		RearAttack:
			HX07 A 2 A_PlaySound("Hexen2/War/FireBig", CHAN_BODY)
			HX07 BCDEFGHIJKLMN 2 A_FaceTarget
			HX07 OPQRSTUVWXYZ 2 A_FaceTarget
			HX08 ABCDEFGHIJKL 2 A_FaceTarget
			HX08 M 2 A_CustomMissile("Hexen2WarAxe2", 40, 1, 2)
			HX08 N 2 A_FaceTarget
		RearEnd:
			HX10 ABCD 2
			HX10 EFGHI 2
			HX10 I 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto See
		XDeath:	
		Death:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking
			"####" "#" 0 A_NoGravity
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			"####" "########" 2
			"####" "#######################" 2
			"####" "################################################################" 2
			"####" "#" 0 A_GiveInventory("Hexen2PraevusFlashToken",1)
			"####" "####" 2
			TNT1 A 105
			TNT1 A 0 A_KillMaster
			Stop

		// SamsaraMonsterDemonicBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Death.DukePlasma
		Death.RottDisintegrate:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Death.RottDisintegrate
		Death.Disintegrate:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Disintegrate
		Death.Nuke:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")	
			Goto Super::Death.Ice
	}		
}	

Actor Hexen2WarAxe1
{
	Projectile 
	Damage (Random(20,30))
	Speed 8
	Radius 7
	Height 10
	Scale 0.5
	+SEEKERMISSILE	
	SeeSound "Hexen2/War/Fire"
	DamageType "Hexen2MezzoDamage"
	States
	{
		Spawn:
			HX00 A 1 
			HX00 A 0 ACS_NamedExecuteAlways("Hexen2_Decorate_Monster", 0, 15)
			HX00 A 0 A_Jump(128, 2)
			HX00 A 0 A_SetPitch(Random(65, 80))
			Goto SpawnLoop
			HX00 A 0 A_SetPitch(Random(100, 115))
		SpawnLoop:
			HX00 AAA 1 A_SetPitch(pitch-15)
			HX00 AAA 1 A_SetPitch(pitch-15)
			HX00 A 1 A_SetPitch(pitch-15)
			HX00 A 1 A_SeekerMissile(5,36, SMF_LOOK|SMF_PRECISE) 
			Loop
		Death:
			HX00 A 5 A_PlaySound("Hexen2/Slash", CHAN_BODY)
			Stop
	}
}

Actor Hexen2WarAxe2 : Hexen2WarAxe1
{
	Damage (Random(60,80))
	States
	{
		Spawn:
			HX00 A 1 
			HX00 A 0 ACS_NamedExecuteAlways("Hexen2_Decorate_Monster", 0, 15)
			HX00 A 0 A_Jump(128, 2)
			HX00 A 0 A_SetPitch(Random(70, 80))
			Goto SpawnLoop
			HX00 A 0 A_SetPitch(Random(100, 110))
		SpawnLoop:
			HX00 AAA 1 A_SetPitch(pitch-15)
			HX00 A 1 A_SeekerMissile(5,36, SMF_LOOK|SMF_PRECISE)
			Loop		
	}
}	