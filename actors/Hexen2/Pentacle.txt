Actor Hexen2BrownPentacle : Hexen2ConstructBaseSmall
{
	Tag "Stone Pentacle"
	Speed 8
	Health 60 //normal difficulty health
	Radius 15
	Height 18
	PainChance 70
	Mass 250
	Scale 1.2
	+FLOORCLIP
	+NOINFIGHTING
	+THRUSPECIES
	+LOOKALLAROUND
	+MISSILEMORE
	+MISSILEEVENMORE
	+DROPOFF
	-COUNTKILL
	GibHealth 20
	PainSound "Hexen2/Pentacle/Pain"
	DeathSound "Hexen2/Pentacle/Death"
	Obituary "%o didn't see the Pentacle clinging to the walls."
	DamageType "Hexen2Damage"
	MinMissileChance 8
	States
	{
		Spawn:
			HX01 A 0
			HX01 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			HX01 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			HX01 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:		
			HX02 ABCDEFGH 2 A_Look
			HX02 IJKLMNO 2 A_Look
			Loop
		See:
			HX04 A 0 A_Jump(7, "RandomLeap")
			HX04 A 2 A_Chase
			HX04 B 0 A_Jump(7, "RandomLeap")
			HX04 B 2 A_Chase
			HX04 C 0 A_Jump(7, "RandomLeap")
			HX04 C 2 A_Chase
			HX04 D 0 A_Jump(7, "RandomLeap")
			HX04 D 2 A_Chase
			HX04 E 0 A_Jump(7, "RandomLeap")
			HX04 E 2 A_Chase
			HX04 F 0 A_Jump(7, "RandomLeap")
			HX04 F 2 A_Chase
			HX04 G 0 A_Jump(7, "RandomLeap")
			HX04 G 2 A_Chase
			HX04 H 0 A_Jump(7, "RandomLeap")
			HX04 H 2 A_Chase
			HX04 I 0 A_Jump(7, "RandomLeap")
			HX04 I 2 A_Chase
			HX04 J 0 A_Jump(7, "RandomLeap")
			HX04 J 2 A_Chase
			HX04 K 0 A_Jump(7, "RandomLeap")
			HX04 K 2 A_Chase
			HX04 L 0 A_Jump(7, "RandomLeap")
			HX04 L 2 A_Chase
			HX04 M 0 A_Jump(7, "RandomLeap")
			HX04 M 2 A_Chase
			HX04 N 0 A_Jump(7, "RandomLeap")
			HX04 N 2 A_Chase
			Loop
		SeeWall:
			HX04 A 0 A_SetPitch(90)
			HX04 A 0 A_Jump(7, "RandomLeap")
			HX04 A 2 A_Chase
			HX04 B 0 A_Jump(7, "RandomLeap")
			HX04 B 2 A_Chase
			HX04 C 0 A_Jump(7, "RandomLeap")
			HX04 C 2 A_Chase
			HX04 D 0 A_Jump(7, "RandomLeap")
			HX04 D 2 A_Chase
			HX04 E 0 A_Jump(7, "RandomLeap")
			HX04 E 2 A_Chase
			HX04 F 0 A_Jump(7, "RandomLeap")
			HX04 F 2 A_Chase
			HX04 G 0 A_Jump(7, "RandomLeap")
			HX04 G 2 A_Chase
			HX04 H 0 A_Jump(7, "RandomLeap")
			HX04 H 2 A_Chase
			HX04 I 0 A_Jump(7, "RandomLeap")
			HX04 I 2 A_Chase
			HX04 J 0 A_Jump(7, "RandomLeap")
			HX04 J 2 A_Chase
			HX04 K 0 A_Jump(7, "RandomLeap")
			HX04 K 2 A_Chase
			HX04 L 0 A_Jump(7, "RandomLeap")
			HX04 L 2 A_Chase
			HX04 M 0 A_Jump(7, "RandomLeap")
			HX04 M 2 A_Chase
			HX04 N 0 A_Jump(7, "RandomLeap")
			HX04 N 2 A_Chase
			Loop			
		RandomLeap:
			HX01 A 0 A_ChangeFlag("NOPAIN", 1)
			HX01 A 0 A_FaceTarget
			HX01 A 0 A_Playsound("Hexen2/Pentacle/Jump", CHAN_BODY)		
			HX01 A 0 ThrustThingZ(0,random(20,55),0,0)	
			HX01 A 0 ThrustThing(random(1,360), Random(10,50), 0, 0)
			HX01 ABCDEFGHIJ 2 A_FaceTarget
			Goto Falling
		Missile:
			TNT1 A 0 A_JumpIfCloser(100,"DecideMelee")
			TNT1 A 0 A_JumpIfCloser(384,"DecideNear")
			TNT1 A 0 A_JumpIfCloser(512,"DecideMid")
			Goto DecideFarthest
		DecideMelee:
			TNT1 A 0 A_Jump(230, "BloodShot")
			Goto Leap		
		DecideNear:	
			TNT1 A 0 A_Jump(76, "BloodShot")
			Goto Leap
		DecideMid:
			TNT1 A 0 A_Jump(38, "BloodShot")
			Goto Leap
		DecideFarthest:	
			TNT1 A 0 A_Jump(12, "BloodShot")
			Goto Leap		
		Leap:
			HX01 A 0 A_ChangeFlag("NOPAIN", 1)
			HX01 A 0 A_FaceTarget
			HX01 A 0 A_Playsound("Hexen2/Pentacle/Jump", CHAN_BODY)		
			HX01 A 0 ThrustThingZ(0,random(20,45),0,0)	
			HX01 A 0 ThrustThing(angle * 256 / 360, Random(10,40), 0, 0)
			HX01 A 2 A_CustomMissile("Hexen2PentacleSlammer", 18,0,0,CMF_AIMDIRECTION) 
			HX01 B 0 A_CheckFloor("Land") 
			HX01 B 0 A_CheckCeiling("LandCeiling") 
			HX01 B 0 A_JumpIfInventory("Hexen2PentacleHitWall", 1, "LandWall") 
			HX01 B 0 A_JumpIfInventory("Hexen2PentacleHitTarget", 1, "SelfDestruct") 			
			HX01 BCDEFGHIJ 2 A_FaceTarget
		Falling:
			HX01 J 2 A_CustomMissile("Hexen2PentacleSlammer", 18,0,0,CMF_AIMDIRECTION) 
			HX01 J 0 A_CheckFloor("Land") 
			HX01 J 0 A_CheckCeiling("LandCeiling") 
			HX01 J 0 A_JumpIfInventory("Hexen2PentacleHitWall", 1, "LandWall") 
			HX01 J 0 A_JumpIfInventory("Hexen2PentacleHitTarget", 1, "SelfDestruct") 
			Loop
		Land:
			HX01 J 0 A_TakeInventory("Hexen2PentacleHitWall", 1)
			HX01 J 0 A_Stop
			HX01 J 0 A_Gravity
			HX01 KLM 1
			HX01 M 0 A_ChangeFlag("NOPAIN", 0)
			HX01 M 0 A_PlaySound("Hexen2/Pentacle/Latch", CHAN_BODY)
			Goto See
		LandWall:
			HX01 J 0 A_TakeInventory("Hexen2PentacleHitWall", 1)
			HX01 J 0 A_Stop
			HX01 J 0 A_NoGravity
			HX01 J 0 A_SetPitch(90)
			HX01 KLM 1
			HX01 M 0 A_ChangeFlag("NOPAIN", 0)
			HX01 M 0 A_PlaySound("Hexen2/Pentacle/Latch", CHAN_BODY)
			Goto See			
		LandCeiling:
			HX01 J 0 A_TakeInventory("Hexen2PentacleHitWall", 1)
			HX01 J 0 A_NoGravity
			HX01 J 0 A_Stop
			HX01 J 0 A_SetPitch(180)
			HX01 KLM 1
			HX01 M 0 A_ChangeFlag("NOPAIN", 0)
			HX01 M 0 A_PlaySound("Hexen2/Pentacle/Latch", CHAN_BODY)
			Goto See		
	//	Melee:
	//		TNT1 A 0 A_Jump(256,"XDeath")
	//		Goto XDeath
		BloodShot:	
			HX03 ABCDEFGHI 2 A_FaceTarget
			HX03 K 0 A_PlaySound("Hexen2/Pentacle/Spit", CHAN_BODY)
			HX03 K 0 A_CustomMissile("Hexen2BloodFireProjPent", 18, 0)
			HX03 JK 1 A_FaceTarget
			Goto See
		Pain:
			HX02 A 2 A_Pain
			Goto See
		Death:
	//		TNT1 A 0 A_JumpIfHealthLower(-20,"XDeath")
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			HX01 A 0 A_NoBlocking
			HX00 A 0 A_Scream
			TNT1 A 0 A_SpawnItemEx("Hexen2BrownPentacleDeath",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)		
			TNT1 A 5
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_PlaySound("Hexen2/Explode", CHAN_BODY)
			TNT1 A 0 A_Explode(50, 50, XF_NOTMISSILE, false, 1)		
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 2 A_SpawnItemEx("Hexen2FakeExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)		
			Stop
		SelfDestruct:
			TNT1 A 0 A_PlaySound("Hexen2/Explode", CHAN_BODY)
			TNT1 A 0 A_Explode(50, 50, XF_NOTMISSILE, false, 1)		
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 2 A_SpawnItemEx("Hexen2FakeExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)		
			Stop		
	}
}		

Actor Hexen2BloodFireProjPent : Hexen2BloodFireProjectile { Damage (random(2,7)*3) }

Actor Hexen2WhitePentacle : Hexen2BrownPentacle {
	Tag "Ice Pentacle"
	States
	{
		Death:
	//		TNT1 A 0 A_JumpIfHealthLower(-20,"XDeath")
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			HX01 A 0 A_NoBlocking
			HX00 A 0 A_Scream
			TNT1 A 0 A_SpawnItemEx("Hexen2WhitePentacleDeath",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)		
			TNT1 A 5
			Stop			
	}
 }

Actor Hexen2BrownPentacleDeath : Hexen2CorpseActor
{
	Radius 15
	Height 18
	+NOBLOOD
	+NOICEDEATH
	Health 20
	-PUSHABLE
    Tag "Dying Stone Pentacle"
	States
	{
		Spawn:
			HX00 ABCDEFGHIJ 2
			HX00 K 4
			TNT1 A 0 A_SpawnItemEx("Hexen2BrownPentacleCorpse",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
			Stop
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
			TNT1 A 5 A_FadeOut
			Stop
	}
}

Actor Hexen2BrownPentacleCorpse : Hexen2CorpseActor
{
	Radius 15
	Height 18
	+NOBLOOD
	+NOICEDEATH
	Health 20
	-PUSHABLE
    Tag "Remains of Stone Pentacle"
	States
	{
	Spawn:
		HX00 KK 0 ACS_NamedExecuteAlways("Hexen2_CorpseRemove")
	SpawnLoop:
		HX00 K 35
		Loop
	Death:
		TNT1 A 0 A_RemoveMaster
		TNT1 A 0 A_ChangeFlag("SOLID",false)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Hexen2MeteorGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut
		Stop
	Decay:
		"####" "#" 0 A_TakeInventory("Hexen2CorpseTimer", 20)
		"####" "#" 3 A_SpawnItemEx("Hexen2RedFlash",0,10)
		"####" "#" 3 A_SetTranslucent(0.9,0)
		"####" "#" 0 A_SetScale(0.9,0.9)
		"####" "#" 3 A_SetTranslucent(0.8,0)
		"####" "#" 0 A_SetScale(0.8,0.8)
		"####" "#" 0 A_NoBlocking
		"####" "#" 3 A_SetTranslucent(0.7,0)
		"####" "#" 0 A_SetScale(0.7,0.7)
		"####" "#" 3 A_SetTranslucent(0.6,0)
		"####" "#" 0 A_SetScale(0.6,0.6)
		"####" "#" 3 A_SetTranslucent(0.5,0)
		"####" "#" 0 A_SetScale(0.5,0.5)
		"####" "#" 3 A_SetTranslucent(0.4,0)
		"####" "#" 0 A_SetScale(0.4,0.4)
		"####" "#" 3 A_SetTranslucent(0.3,0)
		"####" "#" 0 A_SetScale(0.3,0.3)
		"####" "#" 3 A_SetTranslucent(0.2,0)
		"####" "#" 0 A_SetScale(0.2,0.2)
		"####" "#" 3 A_SetTranslucent(0.1,0)
		"####" "#" 0 A_SetScale(0.1,0.1)
		Stop				
		}
}		

Actor Hexen2WhitePentacleDeath : Hexen2CorpseActor
{
	Radius 15
	Height 18
	+NOBLOOD
	+NOICEDEATH
	Health 50
	-PUSHABLE
    Tag "Dying Ice Pentacle"
	States
	{
		Spawn:
			HX00 ABCDEFGHIJ 2
			HX00 K 4
			TNT1 A 0 A_SpawnItemEx("Hexen2WhitePentacleCorpse",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
			Stop	
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItemEx("Hexen2CrystalGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
			TNT1 A 5 A_FadeOut
			Stop
	}
}		

Actor Hexen2WhitePentacleCorpse : Hexen2CorpseActor
{
	Radius 15
	Height 18
	+NOBLOOD
	+NOICEDEATH
	Health 50
	-PUSHABLE
    Tag "Remains of Ice Pentacle"
	States
	{
	Spawn:
		HX00 KK 0 ACS_NamedExecuteAlways("Hexen2_CorpseRemove")
	SpawnLoop:	
		HX00 K 35
		Loop	
	Death:
		TNT1 A 0 A_RemoveMaster
		TNT1 A 0 A_ChangeFlag("SOLID",false)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Hexen2IceGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut
		Stop	
		}
}		

Actor Hexen2MeteorGibsSpawner : Hexen2StoneGibsSpawner
{
	States
	{
		Spawn:
			TNT1 A 0	
			TNT1 A 0 A_SpawnItemEx("Hexen2PuffGenerator",random(-6,6),random(-6,6),random(-4,4),0,0,frandom(-8,8),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SetUserVar("user_count", CallACS("Hexen2_Decorate",25))
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("Hexen2GibCounter",user_count,"SpawnRemove")
			TNT1 A 0 A_GiveInventory("Hexen2GibCounter",1)
			TNT1 A 0 A_Jump(256,"Chunk1","Chunk2","Chunk3")
		Chunk1:
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorChunk1",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk2:
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorChunk2",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk3:
			TNT1 A 0 A_SpawnItemEx("Hexen2MeteorChunk3",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		SpawnRemove:
			TNT1 A 1
			Stop
	}
}

Actor Hexen2MeteorChunk1 : Hexen2StoneChunk1 { }
Actor Hexen2MeteorChunk2 : Hexen2MeteorChunk1 { }
Actor Hexen2MeteorChunk3 : Hexen2MeteorChunk1 { }

Actor Hexen2IceChunk1 : Hexen2StoneChunk1 { RenderStyle Add Alpha 0.5 }
Actor Hexen2IceChunk2 : Hexen2IceChunk1 { }
Actor Hexen2IceChunk3 : Hexen2IceChunk1 { }

Actor Hexen2IceGibsSpawner : Hexen2StoneGibsSpawner
{
	States
	{
		Spawn:
			TNT1 A 0	
			TNT1 A 0 A_SpawnItemEx("Hexen2PuffGenerator",random(-6,6),random(-6,6),random(-4,4),0,0,frandom(-8,8),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SetUserVar("user_count", CallACS("Hexen2_Decorate",25))
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("Hexen2GibCounter",user_count,"SpawnRemove")
			TNT1 A 0 A_GiveInventory("Hexen2GibCounter",1)
			TNT1 A 0 A_Jump(256,"Chunk1","Chunk2","Chunk3")
		Chunk1:
			TNT1 A 0 A_SpawnItemEx("Hexen2IceChunk1",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk2:
			TNT1 A 0 A_SpawnItemEx("Hexen2IceChunk2",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk3:
			TNT1 A 0 A_SpawnItemEx("Hexen2IceChunk3",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop			
		SpawnRemove:
			TNT1 A 1
			Stop
			}
}	

Actor Hexen2Pentacle : RandomSpawner { DropItem "Hexen2BrownPentacle" DropItem "Hexen2WhitePentacle" }

Actor Hexen2FakeExplosion
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	Renderstyle Translucent
	Alpha 0.6
	Projectile
	Speed 0 
	Damage (0)
	States
	{
		Spawn:
			HX05 ABCDEFGHIJKL 2 Bright A_FadeOut		
			Stop
			}
}

Actor Hexen2PentacleSlammer : FastProjectile
{
	Radius 6
	Height 8
	Speed 56
	Damage (0)
	DamageType "Hexen2Damage"
	+NOEXTREMEDEATH
	+HITTRACER
	States
	{
		Spawn:
			TNT1 AA 1
			stop
		Crash:
		XDeath:	
			TNT1 A 0 A_GiveToTarget("Hexen2PentacleHitTarget", 1)
			TNT1 A 1
			Stop		
		Death:
			TNT1 A 0 A_GiveToTarget("Hexen2PentacleHitWall", 1)
			TNT1 A 1
			Stop
	}
}	

Actor Hexen2PentacleHitWall : Inventory { Inventory.MaxAmount 1 }
Actor Hexen2PentacleHitTarget : Inventory { Inventory.MaxAmount 1 }		