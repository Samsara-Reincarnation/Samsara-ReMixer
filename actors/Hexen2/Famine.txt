Actor Hexen2Famine : Hexen2BossBase
{
	Tag "Famine"
	DeathSound "Hexen2/Famine/Death"
	Health 2200
	PainChance 255
	Speed 5
	Radius 48
	Height 64
	Mass 30000
	Species "Hexen2Famine"
	-NOPAIN
	States
	{
		Spawn:
			HX02 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			HX02 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			HX02 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			HX03 A 2 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("SamsaraBossSighted",1,"Chase")
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
			Goto Chase+7
		/*
		See2:
			HX00 ABCDEFGH 2
			HX00 IJKLMNOPQ 2
			HX00 RSTUVWXYZ 2
			HX01 ABCDEFGH 2
			HX01 IJKLMNOP 2
			HX01 QRSTUVWX 2
			Goto Chase
		*/
		Chase:
			HX03 J 0 A_StopSound(CHAN_WEAPON)
			HX03 JKLMNO 2 A_Chase
			HX04 A 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX04 A 0 A_StopSound(CHAN_WEAPON)
			HX04 ABCDEF 2 A_Chase
			HX04 F 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX04 G 0 A_JumpIf(frandom(0.0, 10) < 0.05, "Snort1")
			HX04 GHIJKL 2 A_Chase
			HX04 L 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX04 L 0 A_JumpIf(frandom(0.0, 10) < 0.05, "Snort2")
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.20, "Lol")
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.50, "WarpStart")
			Goto Chase+7
		Pain:
			HX05 A 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.50, "WarpStart")
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.20, "Lol")
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.60, "Shot")
			Goto Chase+7
		Missile:
			HX05 A 0 A_JumpIfCloser(1024,1)
			Goto Shot
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.30, "Pull")
			HX05 A 0 A_JumpIf(frandom(0.0, 10) < 0.70, "Shot")
			Goto Pull
		WarpStart:
			HX02 A 0 A_ChangeFlag(NOPAIN, TRUE)
			HX02 ABCDEFGHIJ 1
			TNT1 A 0 A_PlaySound("Hexen2/Horse/Whinny", CHAN_VOICE)
			HX02 KLMNOPQ 1
			Goto WarpOut
		WarpOut:
			TNT1 A 0 A_JumpIf(frandom(0.0, 1.0) < 0.05, 1)
			HX04 A 0 A_PlaySound("Hexen2/Famine/Laugh", CHAN_VOICE)
			HX00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
			HX00 A 0 A_SpawnItemEx("Hexen2RedCloud", 0, 0, 40)
			HX01 A 0 A_SetTranslucent(0.8,0)
			HX01 A 0 A_SetScale(0.8,0.8)
			HX01 A 0 A_PlaySound("Hexen2/Wizard/BlinkOut", CHAN_BODY)
			HX01 A 0 A_SetInvulnerable
			HX01 A 0 A_ChangeFlag("SOLID",false)
			HX01 A 0 A_UnsetShootable
			HX01 A 0 A_SetTranslucent(0.6,0)
			HX01 A 0 A_SetScale(0.6,0.6)
			HX01 A 0 A_SetTranslucent(0.4,0)
			HX01 A 0 A_SetScale(0.4,0.4)
			HX01 A 0 A_SetTranslucent(0.2,0)
			HX01 A 0 A_SetScale(0.2,0.2)
			HX01 A 0 A_SetTranslucent(0.1,0)
			HX01 A 0 A_SetScale(0.1,0.1)
			HX01 ABCDEFGHIJKLMNOPQRSTUVWX 1
			Goto WanderLoop
		WanderLoop:
			TNT1 A 1
			TNT1 A 0 A_JumpIf(CallACS("Samsara_SetSpeed",16) == 1, 1)
			TNT1 AAAAAAAAAAAAAA 7 A_Wander
			TNT1 A 0 A_Jump(128,"WarpIn")
			TNT1 AAAAAAAAAAAAAA 4 A_Wander
			Goto WarpIn
		WarpIn:
			HX00 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1
			HX00 I 0 A_SpawnItemEx("Hexen2RedCloud", 0, 0, 40)
			HX01 A 0 A_SetTranslucent(0.2,0)
			HX01 A 0 A_SetScale(0.2,0.2)
			HX00 R 0 A_PlaySound("Hexen2/Wizard/BlinkIn", CHAN_BODY)
			TNT1 A 0 A_SetShootable
			TNT1 A 0 A_UnsetInvulnerable
			TNT1 A 0 A_ChangeFlag("SOLID",true)
			HX01 A 0 A_SetTranslucent(0.4,0)
			HX01 A 0 A_SetScale(0.4,0.4)
			HX01 A 0 A_SetTranslucent(0.6,0)
			HX01 A 0 A_SetScale(0.6,0.6)
			HX01 A 0 A_SetTranslucent(0.8,0)
			HX01 A 0 A_SetScale(0.8,0.8)
			HX01 A 0 A_SetTranslucent(1.0,0)
			HX01 A 0 A_SetScale(1.0,1.0)
			HX01 ABCDE 1
			HX01 E 0 A_SpawnItemEx("Hexen2RedFlash", 0,0,90)
			HX01 E 0 A_PlaySound("Hexen2/Famine/Shot", CHAN_WEAPON)
			TNT1 A 0 A_JumpIfCloser(512, 4)
			HX01 E 1 A_SpawnItemEx("Hexen2FamineShotSpawner2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			Goto WarpEnd
			HX01 E 1 A_SpawnItemEx("Hexen2FamineShotSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		WarpEnd:
			HX01 FGHIJKLMNOPQRSTUVWX 1
			TNT1 A 0 A_JumpIf(frandom(0.0, 1.0) < 0.05, "Lol")
			HX03 ABCDEFGHI 1
			HX03 I 0 A_ChangeFlag(NOPAIN, FALSE)
			Goto Chase
		Shot:
			HX05 ABCDEFGHI 2 A_Wander //A_Recoil(-0.4)
			HX05 I 0 A_PlaySound("Hexen2/Famine/Shot", CHAN_WEAPON)
			TNT1 A 0 A_JumpIfCloser(512, 5)
			HX05 I 0 A_FaceTarget
			HX05 I 1 A_SpawnItemEx("Hexen2FamineShotSpawner2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			Goto Chase
			HX05 I 1 A_SpawnItemEx("Hexen2FamineShotSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			Goto Chase
		Pull:
			HX05 ABCDEF 2 A_Wander //A_Recoil(-0.4)
			HX05 G 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX05 GHIJ 2 A_Wander //A_Recoil(-0.4)
			HX05 J 0 A_SpawnItemEx("Hexen2FaminePuller",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			HX05 J 0 A_GiveInventory("Hexen2FaminePullingToken",1)
		PullLoop:
			HX05 J 0 //A_SpawnItemEx("Hexen2FamineOrb", 0,0,90)
			HX05 J 0 //A_CustomMissile("Hexen2FaminePull", 90, 0, 0)
			HX05 J 0 A_PlaySound("Hexen2/Famine/Pull", CHAN_WEAPON, 1.0, True)
			HX05 KL 1 A_Wander //A_Recoil(-0.4)
			HX05 M 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX05 MNOPQ 1 A_Wander //A_Recoil(-0.4)
			HX05 R 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX05 RSTUVWX 2 A_Wander //A_Recoil(-0.4)
			HX05 J 0 A_PlaySound("Hexen2/Horse/Clop", CHAN_VOICE)
			HX05 J 0 A_JumpIfCloser(100, "PullEnd")
			HX05 J 0 A_JumpIfCloser(1024, 1)
			Goto PullEnd
			HX05 J 0 A_JumpIf(CallACS("Hexen2_Decorate_Monster", 4), "PullEnd")
			Goto PullLoop
		PullEnd:
			HX05 J 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			HX05 J 0 A_StopSound(CHAN_WEAPON)
			HX05 JIHGFEDCBA 2 A_Wander //A_Recoil(-0.4)
			Goto Chase+7
		Snort1:
			HX04 G 0 A_StopSound(CHAN_WEAPON)
			HX04 G 0 A_PlaySound("Hexen2/Horse/Snort", CHAN_AUTO)
			Goto Chase +15
		Snort2:
			HX04 A 0 A_StopSound(CHAN_WEAPON)
			HX04 A 0 A_PlaySound("Hexen2/Horse/Snort", CHAN_AUTO)
			Goto Chase+7
		Lol:
			HX03 A 0 A_StopSound(CHAN_WEAPON)
			HX03 A 0 A_PlaySound("Hexen2/Famine/Laugh", CHAN_VOICE)
			HX03 ABCDEFGHI 2 A_Recoil(-0.05)
			Goto Chase
		Death:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_RemoveChildren(true)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking
			"####" "#" 0 A_NoGravity
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			"####" "########" 2
			"####" "#######################" 2
			"####" "################################################################" 2
			"####" "#" 0 A_GiveInventory("Hexen2PraevusFlashToken",1)
			"####" "####" 2
			TNT1 A 105
			TNT1 A 0 A_KillMaster
			Stop

		// SamsaraMonsterDemonicBoss states
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Death.Zorch
		Death.DukePlasma:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Death.DukePlasma
		Death.RottDisintegrate:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Death.RottDisintegrate
		Death.Disintegrate:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Disintegrate
		Death.Nuke:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Death.Nuke
		Death.DukeIce:
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:
		Death.DoomDamageTypeIce:
		Death.Ice:
			"####" "#" 0 A_TakeInventory("Hexen2FaminePullingToken",1)
			"####" "#" 0 ACS_NamedExecuteAlways("Hexen2_BossDeath")
			Goto Super::Death.Ice
	}
}

Actor Hexen2FaminePuller
{
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+THRUACTORS
	+NOGRAVITY
	var int user_damagetime;
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("Hexen2FaminePullingToken",1,1,AAPTR_MASTER)
			Goto Remove
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,90,0,WARPF_NOCHECKPOSITION,1)
			TNT1 A 0 A_SpawnItemEx("Hexen2FamineOrb",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_JumpIf(user_damagetime >= 17,1)
			Goto SpawnLoop+6
			TNT1 A 0 A_SetUserVar("user_damagetime",0)
			TNT1 A 0 A_CustomMissile("Hexen2FaminePullDamage",0,0,0)
			TNT1 A 0 A_SetUserVar("user_damagetime",user_damagetime+1)
			TNT1 A 0 A_CustomMissile("Hexen2FaminePull",0,0,0)
			TNT1 A 2
			Loop
		Remove:
			TNT1 A 1
			stop
	}
}

Actor Hexen2FaminePullDamage : FastProjectile
{
	Height 2
	Radius 1
	Damage (2)
	Speed 256
	Species "Hexen2Famine"
	+THRUSPECIES
	+MTHRUSPECIES
	States
	{
		Spawn:
			TNT1 AA 2
		Death:
			TNT1 A 1
			stop
	}
}

Actor Hexen2FamineShotSpawner
{
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+THRUACTORS
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 AA 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_FaceTarget(0,0)
			TNT1 A 0 A_CustomMissile("Hexen2FamineShot", 90, -3, -25, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			TNT1 A 0 A_CustomMissile("Hexen2FamineShot", 90, 0, 0, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			TNT1 A 0 A_CustomMissile("Hexen2FamineShot", 90, 3, 25, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			stop
	}
}

Actor Hexen2FamineShotSpawner2
{
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+THRUACTORS
	+NOGRAVITY
	States
	{
		Spawn:
			TNT1 AA 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
			TNT1 A 0 A_FaceTarget(0,0)
			HX01 E 0 A_CustomMissile("Hexen2FamineShot2", 90, -3, -25, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			HX01 E 1 A_CustomMissile("Hexen2FamineShot2", 90, 0, 0, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			HX01 E 0 A_CustomMissile("Hexen2FamineShot2", 90, 3, 25, CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION, pitch)
			stop
	}
}

Actor Hexen2FamineShot
{
	Projectile
	Radius 5
	Height 3
	Speed 11
	Species "Enemies"
	Damage (Random(8,16))
	+THRUSPECIES
	+SEEKERMISSILE
	States
	{
	Spawn:
		HX00 AAAAAAA 2 BRIGHT A_SpawnItemEx("Hexen2GemPuffGenerator", frandom(-4,4),frandom(-4,4),frandom(-4,4),0,0,0,0,SXF_NOCHECKPOSITION)
	SpawnLoop:
		HX00 A 2 BRIGHT A_SpawnItemEx("Hexen2GemPuffGenerator", frandom(-4,4),frandom(-4,4),frandom(-4,4),0,0,0,0,SXF_NOCHECKPOSITION)
		HX00 AAAAAA 0 BRIGHT A_SeekerMissile(25, 25, SMF_LOOK|SMF_PRECISE)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Hexen2/Explode", CHAN_WEAPON)
		TNT1 A 0 A_SpawnItemEx("Hexen2RedSpark")
		TNT1 A 5 A_FadeOut
		Stop
		}
}

Actor Hexen2FamineShot2 : Hexen2FamineShot { Speed 13 }

Actor Hexen2RedCloud
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Renderstyle Translucent
	Alpha 0.5
	Radius 2
	Height 4
	States
	{
	Spawn:
		TNT1 A 0
		HX36 ABCDEFGHIJ 2 BRIGHT
		Stop
		}
}

Actor Hexen2FamineOrb
{
	Renderstyle Translucent
	Alpha 0.5
	Radius 12
	Height 24
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0
		HX00 A 2 BRIGHT
		Stop
	}
}

Actor Hexen2FaminePull : FastProjectile
{
	Speed 512
	Radius 8
	Height 4
	Damagetype "Hexen2Electric"
	Damage (0)
	Species "Hexen2Famine"
	+SKYEXPLODE
	+DONTSPLASH
	+CANNOTPUSH
	+NODAMAGETHRUST
	+THRUSPECIES
	+MTHRUSPECIES
	+HITTRACER
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 AA 1
			Goto Death
		Death:
			TNT1 A 0 A_JumpIf(CallACS("Samsara_Laser",7,32,0) == 1, 1)
			TNT1 A 5
			stop
		Xdeath:
			TNT1 A 0 A_JumpIf(CallACS("Hexen2_Decorate_Monster",5,2,4)==1,1)
			TNT1 A 0 A_JumpIf(CallACS("Samsara_Laser",7,32,0) == 1, 1)
			TNT1 A 5
			stop
	}
}
/*
Actor Hexen2FaminePull
{
	Radius 8
	Height 4
	Speed 28
	Damage (0) // make this 0 so that a periodic puller can deal damage
	Projectile
	SeeSound ""
	ActiveSound ""
	Species "Hexen2Famine"
	+THRUSPECIES
	+MTHRUSPECIES
	//+HITTRACER
	States
	{
	Spawn:
		TNT1 AA 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_TRACER,AAPTR_TRACER)
	Moving:
		TNT1 A 0 A_JumpIfInventory("Hexen2LightningArcLength",64,"Remove")
		TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", false)
	MovingBoltChoose:
		TNT1 A 0 A_Jump(256,"MovingBolt1","MovingBolt2","MovingBolt3","MovingBolt4","MovingBolt5","MovingBolt6")
	MovingBolt1:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment1",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBolt2:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment2",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBolt3:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment3",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBolt4:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment4",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBolt5:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment5",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBolt6:
		TNT1 A 0 A_SpawnItemEx("Hexen2FamineBeamSegment6",0,0,0,(MomX*0.001), (MomY*0.001), (MomZ*0.001), Angle, SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE)
		Goto MovingBoltContinue
	MovingBoltContinue:
		TNT1 A 0 A_ChangeFlag("CLIENTSIDEONLY", true)
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Remove
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Remove
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Remove
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Remove
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,MomX/5,MomY/5,MomZ/5,0,WARPF_ABSOLUTEOFFSET,1) Goto Remove
		TNT1 A 0 A_GiveInventory("Hexen2LightningArcLength",1)
		Goto Moving
	Remove:
	XDeath:
		TNT1 A 0 A_JumpIf(CallACS("Hexen2_Decorate_Monster",5)==1,1)
		TNT1 A 2 A_Stop
		Stop
		}
}
*/
Actor Hexen2FamineBeamSegment
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	Radius 8
	Height 3
	Renderstyle Translucent
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256,"Beam1","Beam2","Beam3","Beam4","Beam5","Beam6")
		Beam1:
			HX00 A 2 Bright
			HX00 A 2 Bright A_Fadeout(0.1)
			stop
		Beam2:
			HX00 B 2 Bright
			HX00 B 2 Bright A_Fadeout(0.1)
			stop
		Beam3:
			HX00 C 2 Bright
			HX00 C 2 Bright A_Fadeout(0.1)
			stop
		Beam4:
			HX00 D 2 Bright
			HX00 D 2 Bright A_Fadeout(0.1)
			stop
		Beam5:
			HX00 E 2 Bright
			HX00 E 2 Bright A_Fadeout(0.1)
			stop
		Beam6:
			HX00 F 2 Bright
			HX00 F 2 Bright A_Fadeout(0.1)
			stop
	}
}

ACTOR Hexen2FaminePullingToken : Hexen2Boolean {}