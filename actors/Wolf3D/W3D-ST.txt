ACTOR SuperOtto : SamsaraMonsterHumanoidTotalGib
{
    Tag "Super Otto"
	Health 850
	Radius 26
	Height 69
	Speed 6
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+LOOKALLAROUND
	+BOSSDEATH
	SeeSound "otto/sight"
	PainSound ""
	DeathSound "otto/death"
	ActiveSound ""
	AttackSound "wolfen/missilefire"
	//Dropitem "DefaultPinataPop"
	Obituary "%o was blown away by Super Otto's rocket."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	//Dropitem "WolfGoldKey"
	Decal Scorch
	States 
	{
		Spawn:
			WOTT A 0
			WOTT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			WOTT A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			WOTT A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			WOTT A 1 A_Look
			Loop
		See:
			WOTT AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WOTT EEEEE 3 A_FaceTarget
			WOTT F 5 Bright
			TNT1 A 0 A_CustomMissile("OttoWolfRocket",32,16,16,8)
			TNT1 A 0 A_CustomMissile("OttoWolfRocket",32,16,0,8)
			TNT1 A 0 A_CustomMissile("OttoWolfRocket",32,16,-16,8)
			Goto See
		Pain:
			WOTT D 3
			WOTT D 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			WOTT D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(8)
			WOTT D 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",2) == 1, "FastDeath")
			WOTT A 70 A_Scream
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			WOTT G 5
			WOTT H 5
			WOTT I 5 A_NoBlocking
			WOTT J -1 A_KillMaster
			Stop
		FastDeath:
			WOTT A 5 A_Scream
			Goto Death+3
	}
}

ACTOR SOttoGiftmacherDoom : OttoGiftMacher
{
	Height 63
	Scale 1.0
}
