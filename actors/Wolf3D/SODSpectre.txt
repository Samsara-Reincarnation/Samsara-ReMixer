ACTOR SODSpectre : SamsaraMonsterGhost
{
	Health 60
	Radius 16	
	Height 50
	DeathHeight 50
	Speed 2
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+FLOAT
	+SPAWNFLOAT
	+NOGRAVITY
	+NOBLOOD
	+LOOKALLAROUND
	SeeSound "wolfspectre/sight"
	DeathSound "wolfspectre/death"
	Obituary "%o got spooked by a ghost."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
    DamageFactor "PoisonBolt", 0.0		
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	DamageFactor "WolfEnemyBullet", 0.0
	Tag "Ghost"
	States 
	{
		Spawn:
			SSPE A 0
			SSPE A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SSPE A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			SSPE A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			SSPE ABCD 5 A_Look
			loop
		See:
			SSPE AAAAABBBBBCCCCCDDDDD 1 A_Chase
			loop
		Melee:
			SSPE A 1 A_CustomMeleeAttack(3,0,0,0,0)
			Goto See
		Death:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",11) == 0, "Dormant")
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SSPE E 5 A_Scream
			SSPE FG 5 A_NoBlocking
			SSPE H 5
			Stop
		Dormant:
			TNT1 A 0 A_Stop
			TNT1 A 0 A_Scream
			SSPE E 5 A_UnsetShootable
			SSPE F 5
			SSPE G 5
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			SSPE H 155
			SSPE GFE 5
			TNT1 A 0 A_SpawnItemEx("SODSpectreling",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR SODSpectreling : SODSpectre
{
	SeeSound ""
	-COUNTKILL
	States 
	{
		Death:
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",11) == 0, "Dormant")
			//TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			SSPE E 5 A_Scream
			SSPE FG 5 A_NoBlocking
			SSPE H 5
			Stop
	}
}
