ACTOR DevilIncarnate : SamsaraMonsterDemonicBoss
{
	Health 2000
	Radius 33
	Height 70
	Speed 12
	Mass 1000
	PainChance 0
	Scale 1.1
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+BOSS
	+NORADIUSDMG
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+DONTRIP	
	+LOOKALLAROUND
	Tag "Devil Incarnate"
	SeeSound "devilinc/sight"
	PainSound ""
	DeathSound "devilinc/death"
	ActiveSound ""
	AttackSound "boss/fire"
	Obituary "%o was smited by the Devil Incarnate."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	Decal Scorch
	BloodColor Green
	States 
	{
		Spawn:
			DEIN A 0
			DEIN A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			DEIN A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			DEIN A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			DEIN A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			DEIN AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			DEIN E 5 A_FaceTarget
		Blast:
			TNT1 A 0 A_FaceTarget
			DEIN FFFFF 2 A_FaceTarget
			DEIN E 5 Bright A_CustomMissile("DevilBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			TNT1 A 0 A_Jump(128,1)
			Goto Blast2
			TNT1 A 0
			Goto See2
		Blast2:
			TNT1 A 0 A_FaceTarget
			DEIN FFFFF 2 A_FaceTarget
			DEIN E 5 Bright A_CustomMissile("DevilBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			TNT1 A 0 A_Jump(128,1)
			Goto Blast3
			TNT1 A 0
			Goto See2
		Blast3:
			TNT1 A 0 A_FaceTarget
			DEIN FFFFF 2 A_FaceTarget
			DEIN E 5 Bright A_CustomMissile("DevilBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			Goto See2
		Exhaust:
			TNT1 A 0 A_TakeInventory("OrbsFired",6)
			DEIN H 40
			DEIN G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			DEIN H 20
			DEIN G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			DEIN H 20 
			DEIN G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			Goto See
		Pain:
			DEIN D 3
			DEIN D 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",2) == 1, "FastDeath")
			DEIN A 52 A_Scream
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			DEIN I 5 A_PlaySoundEx("devilinc/melt","soundslot5",0,0)
			DEIN J 5 
			DEIN K 5 A_NoBlocking
			DEIN LMNO 5
			DEIN P 30
			DEIN P -1 A_KillMaster
			Stop
		FastDeath:
			DEIN A 5 A_Scream
			Goto Death+3
	}
}
