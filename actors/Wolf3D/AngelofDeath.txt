ACTOR AngelOfDeath : SamsaraMonsterDemonicBoss
{
	Health 2000
	Radius 33
	Height 70
	Speed 12
	Mass 1000
	PainChance 0
	Scale 1.1
	Species Nazi
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+BOSS
	+NORADIUSDMG
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+DONTRIP	
	+BOSSDEATH
	+LOOKALLAROUND
	SeeSound "angel/sight"
	PainSound ""
	DeathSound "angel/death"
	ActiveSound ""
	AttackSound "boss/fire"
	Tag "Angel of Death"
	Obituary "%o was smited by the Angel of Death."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	Decal Scorch
	Bloodcolor "c8 00 cc"
	States 
	{
		Spawn:
			WADH A 0
			WADH A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			WADH A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			WADH A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			WADH A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			WADH AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			WADH E 5 A_FaceTarget
		Blast:
			TNT1 A 0 A_FaceTarget
			WADH FFFFF 2 A_FaceTarget
			WADH E 5 Bright A_CustomMissile("AngelBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			TNT1 A 0 A_Jump(128,1)
			Goto Blast2
			TNT1 A 0
			Goto See2
		Blast2:
			TNT1 A 0 A_FaceTarget
			WADH FFFFF 2 A_FaceTarget
			WADH E 5 Bright A_CustomMissile("AngelBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			TNT1 A 0 A_Jump(128,1)
			Goto Blast3
			TNT1 A 0
			Goto See2
		Blast3:
			TNT1 A 0 A_FaceTarget
			WADH FFFFF 2 A_FaceTarget
			WADH E 5 Bright A_CustomMissile("AngelBlast",32,16,0,8)
			TNT1 A 0 A_PlaySound("angel/fire", CHAN_WEAPON)
			TNT1 A 0 A_GiveInventory("OrbsFired",1)
			TNT1 A 0 A_JumpIfInventory("OrbsFired",6,"Exhaust")
			Goto See2
		Exhaust:
			TNT1 A 0 A_TakeInventory("OrbsFired",6)
			WADH H 40
			WADH G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			WADH H 20
			WADH G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			WADH H 20 
			WADH G 20 A_PlaySound("angel/tired", CHAN_WEAPON)
			Goto See
		Pain:
			WADH D 3
			WADH D 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",2) == 1, "FastDeath")
			WADH A 52 A_Scream
			TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
			WADH I 5 A_PlaySoundEx("hitler/melt","soundslot5",0,0)
			WADH J 5 
			WADH K 5 A_NoBlocking
			WADH LMNO 5
			WADH P 30
			WADH P -1 A_KillMaster
			Stop
		FastDeath:
			WADH A 5 A_Scream
			Goto Death+3
	}
}
