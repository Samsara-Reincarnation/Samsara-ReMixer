Actor SWCoolieGhost : SamsaraMonsterBeast
{
  Health 65
  Radius 22
  Height 36
  Mass 256
  Speed 8
  PainChance 128
  Scale 0.71
  Alpha 1.0
  Renderstyle Translucent
  Monster
  +FLOAT
  +NOGRAVITY
  +NOINFIGHTING
  +DONTHARMCLASS
  +DONTHARMSPECIES
  -COUNTKILL
  +NOBLOODDECALS
  SeeSound "Ghost/See"
  PainSound "Ghost/Pain"
  DeathSound "Ghost/Die"
  ActiveSound "Ghost/Act"
  Obituary "%o lost %p soul to a ghost."
  Species "SWEnemies"
  +THRUSPECIES
  Tag "Coolie Ghost"
  BloodType "SWDefaultBloodSpawner"
  PainChance "MightyBoot", 255
  PainChance "MarathonStun", 255
  PainChance "ShrinkerDMG", 0
  States
  {
  Spawn:
	GHOS A 0
	GHOS A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
	GHOS A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
	GHOS A 0 A_GiveInventory("AlreadySpawned",1)
  Spawn2:
    GHOS A 10 A_Look
    Loop
  See:
    GHOS AA 2 A_Chase
	GHOS AB 2 A_Chase("","",CHF_NOPLAYACTIVE)
	GHOS BB 2 A_Chase
	TNT1 A 0 A_Jump(32,"Vanish")
	GHOS CC 2 A_Chase
	GHOS CD 2 A_Chase("","",CHF_NOPLAYACTIVE)
	GHOS DD 2 A_Chase
    Loop
  Missile:
	TNT1 A 0 A_ChangeFlag("NOPAIN",0)
    GHOS EF 7 A_FaceTarget
	TNT1 A 0 A_PlaySound("Ghost/Attack")
    GHOS G 7 A_CustomMissile("GhostProjectile",0)
	TNT1 A 0 A_Jump(64,"Missile")
	GHOS G 1
    Goto See
  Pain:
    GHOS H 3
    GHOS H 3 A_Pain
    Goto See
  Pain.MightyBoot:
	TNT1 A 0 A_ChangeFlag("NOPAIN",1)
	TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
	GHOS H 2 A_FaceTarget
	TNT1 A 0 A_Recoil(14)
	GHOS H 16 A_Pain
	Goto See
  Vanish:
    TNT1 A 0 A_playSound("Ghost/Vanish")
	TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.9, 0)
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.8, 0)
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.7, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.6, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.5, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.4, 0)
	TNT1 A 0 A_UnsetShootable
	GHOS C 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.3, 0)
	GHOS C 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.2, 0)
	GHOS C 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.1, 0)
	GHOS D 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.0, 0)
	TNT1 A 0 A_Jump(128, "Agressive")
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Wander
	TNT1 AAAAAAA 1 A_Chase(0,0,CHF_NOPLAYACTIVE)
  Agressive:
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(0,0,CHF_NOPLAYACTIVE)
	TNT1 A 16
	GHOS D 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.1, 0)
	GHOS D 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.2, 0)
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.3, 0)
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.4, 0)
	TNT1 A 0 A_SetShootable
	GHOS A 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.5, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.6, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.7, 0)
	GHOS B 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.8, 0)
	GHOS C 2 A_Wander
	TNT1 A 0 A_SetTranslucent(0.9, 0)
	GHOS C 2 A_Wander
	TNT1 A 0 A_SetTranslucent(1.0, 0)
	TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
	Goto See
  Death:
	TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)    
	TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreDeathEffect")
    GHOS I 5 A_StopSound(CHAN_BODY)	 
    GHOS J 5 A_Scream
    GHOS K 5 A_NoBlocking
	GHOS L 5
  CheckFloor:
    GHOS M 4 A_CheckFloor("Landed")
	Loop
  Landed:
    TNT1 A 0 A_PlaySound("Body/Land")
    GHOS N 0 A_SetFloorClip
    GHOS N 0 A_SpawnItemEx("SWCoolieGhostCorpse", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
	TNT1 A -1
	stop
  XDeath:
	TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)    
	TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
	TNT1 A 0 A_NoBlocking
	TNT1 A 5 A_SpawnItemEx("SWEnemy_DemonGibSpawner")
	TNT1 A -1
	Stop
  Death.SWFist:
	TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
	TNT1 A 0 A_Jump(218, "Death")
	TNT1 A 0 A_SpawnItemEx("SWFisted")
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
	TNT1 A -1 A_Jump(256,"Death")
	wait	
  Death.SWKatana:
	TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
	TNT1 A 0 A_Jump(218, "Death")
	TNT1 A 0 A_SpawnItemEx("SWKatanad")
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
	TNT1 A -1 A_Jump(256,"Death")
	wait
  Death.SWShuriken:
	TNT1 A 0 A_Jump(218, "Death")
	TNT1 A 0 A_SpawnItemEx("SWShurikend")
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
	TNT1 A -1 A_Jump(256,"Death")
	wait
  Death.SWSticky:
  Death.SWStickyCoop:		
  Death.SWGrenade:
  Death.SWNuke:
  Death.SWRocket:
  Death.SWRailgun:
  Death.SWRailgunCoop:
  Death.SWNukeCoop:
	TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
  Death.SWBullet:	  
	TNT1 A 0 A_Jump(218, "Death")
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
	TNT1 A -1 A_Jump(256,"Death")
	wait
  Death.SWFlame:
  Death.SWFlame2:
  Death.SWFlame2Coop:
  Death.SWFlame3:	
	TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
	TNT1 A 0 A_Jump(218, "Burn")
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
	TNT1 A -1 A_Jump(256,"Burn")
	wait
  XDeathForWang:
	TNT1 A 0 A_ChangeFlag("SOLID",false)
	TNT1 A 0 A_Jump(218, "XDeath")	
	TNT1 A 0 A_GiveToTarget("LoWangDMTaunt2",1)
	TNT1 A -1 A_Jump(256,"XDeath")
	wait
  }
}

Actor SWCoolieGhostCorpse : SamsaraCorpseActor
{
	Health 25
	Height 8
	Radius 16
	Mass 5000
	BloodType "SWDefaultBloodSpawner"
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+VULNERABLE
	-SOLID
	+NOTARGET
	-FRIENDLY
	+NOBLOODDECALS
	States
	{
		Spawn:
			GHOS N 4
			Loop
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_PlaySound("sw/gib_land1",CHAN_BODY)
			TNT1 AAAAAAA 0 A_SpawnItemEx("SWBlood",random(-12,12),random(-12,12),random(0,40),random(-5,5),random(-5,5),random(4,10),0,SXF_USEBLOODCOLOR|SXF_TRANSFERTRANSLATION)
			stop
	}
}

Actor CoolieGhostNonSolid : SWCoolieGhost
{
	+LOOKALLAROUND
	-SOLID
	States
	{
		Spawn:
			TNT1 A 0
			GHOS A 2
			TNT1 A 0 A_ChangeFlag("SOLID", 1)
			Goto Super::Spawn
	}
}

Actor GhostProjectile
{
	PROJECTILE
	Damage (random(15,30))
	Speed 22
	Radius 6
	Height 8
	Scale 0.35
	Species "SWEnemies"
	+THRUSPECIES
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("Weapons/FireCircle",5)
		SpawnLoop:	
			CGPR ABCD 3 Bright
			Loop
		XDeath:
			TNT1 A 1 A_PlaySound("Ghost/Hit")
			Stop
		Death:
			TNT1 A 0 A_PlaySound("Ghost/Hit")
			SWTE ABCDEFGHIJKLMNOPQR 2 Bright
			Stop
	}
}