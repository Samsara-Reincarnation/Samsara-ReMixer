actor SWHornetWasp : SamsaraMonsterBeastTotalGib {
	MONSTER
	Health 4
	SeeSound ""
	AttackSound ""
	PainSound ""
	DeathSound "hornet/die"
	ActiveSound ""
	Species "SWEnemies"
	//DropItem "DefaultPinataPop"
	Speed 17
	Height 24
	Radius 10
    Tag "Hornet"	
	Scale 0.75
	Mass 16
	FloatSpeed 10
	Obituary "%o discovered an allergy to wasps."
	BloodColor yellow
	BloodType "SWDefaultBloodSpawner"
	PainChance "ShrinkerDMG", 0
    +NOPAIN
	+NOGRAVITY
	+FLOAT
	+SPAWNFLOAT
	+NOTARGET
	+NOINFIGHTING
	+NOBLOODDECALS
    States {
      Spawn:
		SW27 A 0
		SW27 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		SW27 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		SW27 A 0 A_GiveInventory("AlreadySpawned",1)
      Spawn2:
		TNT1 A 0 A_Jump(256,"SpawnLoop")
		TNT1 A 0 A_PlaySound("hornetw/loop",CHAN_BODY,0.4,1,ATTN_NORM)
	  SpawnLoop:	
        SW27 A 2 A_Look
		SW27 A 2 A_Look
		SW27 B 2 A_Look
		SW27 B 2 A_Look
        Loop
      See:
        SW27 AA 2 A_Chase("Melee","")
		SW27 BB 2 A_Chase("Melee","")
		SW27 AA 2 A_Chase("Melee","")
		SW27 BB 2 A_Chase("Melee","",CHF_FASTCHASE)
		SW27 AA 2 A_Chase("Melee","")
		SW27 BB 2 A_Chase("Melee","")
		SW27 AA 2 A_Chase("Melee","")
		SW27 BB 2 A_Chase("Melee","",CHF_FASTCHASE)
        Loop
	  Melee:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMeleeAttack(Random(5,10),"hornetw/att","")
		Goto WanderAround
	  WanderAround:
		TNT1 A 0 A_Jump(128,"DerpAround","See")
	  WanderLoop:	
        SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","")
		SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","",CHF_FASTCHASE)
		SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","")
		SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","",CHF_FASTCHASE)
		TNT1 A 0 A_ChangeFlag(FRIGHTENED,0)		
        Goto See
	  DerpAround:
		TNT1 A 0 A_ChangeFlag(FRIGHTENED,1)	
        SW27 AA 2 A_Wander
		SW27 BB 2 A_Wander
		SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","",CHF_FASTCHASE)
		SW27 AA 2 A_Wander
		SW27 BB 2 A_Wander
		SW27 AA 2 A_Chase("","")
		SW27 BB 2 A_Chase("","",CHF_FASTCHASE)
		TNT1 A 0 A_ChangeFlag(FRIGHTENED,0)		
		Goto See
	  Death:
		TNT1 A 0 A_StopSound(CHAN_BODY)
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
	  FallLoop:	
        SW27 C 1 A_CheckFloor(1)
		Loop
	  LandCorpse:	
        SW27 D -1
        Stop
	  Death.SWFist:
		TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWFisted")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Death")
		wait	
	  Death.SWKatana:
		TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWKatanad")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Death")
		wait
	  Death.SWShuriken:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_SpawnItemEx("SWShurikend")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Death")
		wait
	  Death.SWSticky:
	  Death.SWStickyCoop:		
	  Death.SWGrenade:
	  Death.SWNuke:
	  Death.SWRocket:
	  Death.SWRailgun:
	  Death.SWRailgunCoop:
	  Death.SWNukeCoop:
		TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
	  Death.SWBullet:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Death")
		wait
	  Death.SWFlame:
	  Death.SWFlame2:
	  Death.SWFlame2Coop:
	  Death.SWFlame3:	
		TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
		TNT1 A 0 A_Jump(218, "Burn")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Burn")
		wait
	  XDeathForWang:
		TNT1 A 0 A_Jump(218, "Death")	
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1",1)
		TNT1 A -1 A_Jump(256,"Death")
		wait
    }
}
