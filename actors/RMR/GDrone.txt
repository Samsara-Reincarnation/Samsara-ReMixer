Actor RMRGrenadeDrone : SamsaraMonsterRobotic {
	Health 30
	Species "RMREnemies"
	SeeSound "RMR/GProbe/See"
	Painsound "RMR/GProbe/Ouw"
	DeathSound "RMR/GProbe/Die"
	ActiveSound "RMR/GProbe/See"
	Painchance 70
	FloatSpeed 10
	Obituary "%o didn't see a Grenade Probe's ball."
	PainChance "ShrinkerDMG", 0
	Radius 16
	Height 44	
	Speed 13
	Scale 0.65
	Tag "Grenade Drone"	
    +SPAWNFLOAT	
	MONSTER	
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+MISSILEEVENMORE
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	+NOINFIGHTING
	Mass 250
	PainSound ""
	ActiveSound ""
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.0		
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.11
    PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States {
		Spawn:
			RM72 A 0
			RM72 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			RM72 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			RM72 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			RM72 A 5 A_Look
			Loop
		See:
			RM72 AA 2 A_Chase("","",CHF_NOPLAYACTIVE)
			RM72 AA 2 A_Chase
			RM72 AA 2 A_Chase("","",CHF_NOPLAYACTIVE)
			RM72 AA 2 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			RM72 AAA 5 A_FaceTarget
			TNT1 A 0 A_PlaySound("RMR/PIGFIRE")
			TNT1 A 0 A_CustomMissile("RMREnemyGrenade_Drone",2,0,0)
			RM72 B 10			
			Goto See
		Pain:
			RM72 C 4 A_Pain
			RM72 C 4
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			RM72 C 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			RM72 C 16 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			RM72 C 2
			RM72 D 5 A_Scream
			RM72 E 5 A_Fall
			RM72 FGHIJKLM 5
			TNT1 A -1
			Stop
  }
}