Actor MiniMech : SamsaraMonsterRobotic
{
	Monster
	Health 285
	Radius 18
	Height 46
	Scale 0.75
	Speed 6
	Mass 5000
	+FLOORCLIP
	+NOTDMATCH
	+FLOAT
	+DONTGIB	
	+NOGRAVITY
	+SPAWNFLOAT	
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOBLOOD
	Tag "Mini Mech"		
	PainChance 200
	Species "EradEnemies"
	SeeSound "minimech/amb"
	ActiveSound "minimech/amb"
	PainSound "minimech/fire"
	PainChance "ShrinkerDMG", 0
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0
	States
	{
		Spawn:
			_EMI A 0
			_EMI A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			_EMI A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			_EMI A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			TNT1 A 0 
			TNT1 A 0 A_StopSound(CHAN_BODY)
		SpawnLoop:	
			_EMI ABCDEFGH 4 A_Look
			Loop
		See:
			TNT1 A 0 A_PlaySound("minimech/act",CHAN_BODY,0.1,1,ATTN_NORM)			
		SeeLoop:
			_EMI AABBCCDDEEFFGGHH 2 A_Chase
			Loop
		Pain:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			_EMI I 4 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			_EMI I 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			_EMI I 16 A_Pain
			Goto See
	    Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			_EMI I 1 A_FaceTarget
			_EMI I 1 A_Die("NotSuicide")
			Wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		Death.NotSuicide:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnItem("MiniMechBomb")
			TNT1 A 5
			Stop
	}
}	

Actor MiniMechBomb
{
	PROJECTILE
	Radius 1
	Height 1
	Speed 0
	+NOCLIP
	States
	{
		Spawn:
			TNT1 A 0
			Goto Death
		Death:
			TNT1 A 0 A_PlaySound("minimech/die") 
			TNT1 A 0 A_Explode(random(22,40)/Random(2,1),138)
			TNT1 A 0 A_SpawnITemEx("EradPlasmaExplosion1")
			TNT1 A 5 A_FadeOut
			stop
  }
}