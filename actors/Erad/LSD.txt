ACTOR LSD : SamsaraMonsterRobotic
{
	Health 67
	Radius 20
	Height 47
	Speed 4
	Scale 0.60
	PainChance 50
	Species "EradEnemies"	
	Monster
	SeeSound "lsd/sight"
	PainSound "lsd/ouch"
	DeathSound "lsd/die"
	ActiveSound ""
    Tag "LSD"
	+FLOORCLIP
	+NOTDMATCH
	+MISSILEMORE	
	+NOBLOOD
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0	
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	Obituary "%o was disassembled by an LSD bot."
	PainChance "ShrinkerDMG", 0
	States
	{
	Spawn:
		LSD1 A 0
		LSD1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		LSD1 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		LSD1 A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		TNT1 A 0
		TNT1 A 0 A_StopSound(CHAN_BODY)
	SpawnLoop:	
		LSD1 A 5 A_Look
		Loop
	See:
		TNT1 A 0 A_PlaySound("lsd/active",CHAN_BODY,0.3,1,ATTN_NORM)
	SeeLoop:			
		LSD1 BB 2 A_Chase("","")
		LSD1 CC 2 A_Chase
		LSD1 DD 2 A_Chase("","")
		LSD1 EE 2 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_StopSound(CHAN_BODY)	
		LSD1 AA 6 A_FaceTarget
		TNT1 A 0 A_PlaySound("lsd/fire")
		LSD1 I 8 A_CustomMissile("LSDLaser",45,4,0,0)
		LSD1 A 5 A_FaceTarget
		Goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_StopSound(CHAN_BODY)	
		TNT1 A 0 A_PlaySound("lsd/melee")		
		LSD1 FG 7 A_FaceTarget
		TNT1 A 0 A_CustomMeleeAttack(5*random(random(1,3),3),"","")
		LSD1 H 6 A_FaceTarget
		Goto See
	Pain:
		TNT1 A 0 A_StopSound(CHAN_BODY)
		LSD1 J 4 A_Pain
		LSD1 J 4
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_StopSound(CHAN_BODY)	
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)	
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		LSD1 J 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		LSD1 J 16 A_Pain
		Goto See
	Death:
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)	  	
		TNT1 A 0 A_StopSound(CHAN_BODY)	
		LSD1 K 7 A_Scream
		LSD1 L 6 A_NoBlocking
		LSD1 MNO 8
		LSD1 O -1
		Stop		
	XDeath:
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)	  	
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_ChangeFlag("SOLID",false)
		TNT1 A 0 A_SpawnItemEx("LSDGun",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)					
		TNT1 A 0 A_SpawnItemEx("EradMetalDebris_5",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("EradMetalDebris",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDShoulder",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDHead",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDArm",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDForeArm",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDPiece1",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)		
		TNT1 A 0 A_SpawnItemEx("LSDPiece2",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)				
		TNT1 A 0 A_SpawnItemEx("LSDWheel",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)
		TNT1 A 1 A_SpawnITemEx("EradPlasmaExplosion1",0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 5 A_NoBlocking
		TNT1 A -1
		Stop
	}
}	

Actor LSDGun : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD1 XYZ 4 
        loop
      Death:
	  Crash:
		TNT1 A 1 A_FadeOut
        Loop
    }
}

Actor LSDShoulder : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD2 KLMNO 4 
        loop
      Death:
	  Crash:
		TNT1 A 1 A_FadeOut
        Loop
    }
}

Actor LSDHead : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD1 STUVW 4 
        loop
      Death:
	  Crash:
		LSD1 W 500 A_SetFloorClip
		LSD1 W 1 A_FadeOut
        Goto Death+1
    }
}

Actor LSDArm : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD2 PQRST 4 
        loop
      Death:
	  Crash:
		LSD2 T 500 A_SetFloorClip
		LSD2 T 1 A_FadeOut
        Goto Death+1
	}
}

Actor LSDForeArm : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD1 PQ 4 
        loop
      Death:
	  Crash:
		LSD1 R 500 A_SetFloorClip
		LSD1 R 1 A_FadeOut
        Goto Death+1
    }
}

Actor LSDPiece1 : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD2 UVWXY 4 
        loop
      Death:
	  Crash:
		TNT1 A 1 A_FadeOut
        Loop
    }
}

Actor LSDPiece2 : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD2 ABCDE 4 
        loop
      Death:
	  Crash:
		TNT1 A 1 A_FadeOut
        Loop
    }
}

Actor LSDWheel : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        LSD2 FGHI 4 
        loop
      Death:
	  Crash:
		LSD2 J 500 A_SetFloorClip
		LSD2 J 1 A_FadeOut
        Goto Death+1
    }
}

Actor LSDLaser
{
  Radius 5
  Height 5
  Speed 22
  Scale 0.50
  Damage (5)
  Projectile
  +THRUGHOST
  +FORCEXYBILLBOARD
  Species "Enemies"  
  DeathSound "erad/expshot3"
  States
  {
  Spawn:
    LSD2 Z 3 Bright
	LSD_ A 3 Bright
    Loop
  Death:
    LSD_ BCDEF 3 Bright
	LSD_ F 1 Bright A_FadeOut
    Stop
  }
}	