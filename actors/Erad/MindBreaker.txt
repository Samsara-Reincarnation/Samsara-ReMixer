Actor EradMindBreaker : SamsaraMonsterCyberneticTotalGib
{
	Health 150
	Radius 20
	Height 56
	Speed 8
	Scale 0.60
	FloatSpeed 6
	Mass 5000
	Monster
	PainChance 1
	Species "EradEnemies"	
	+FLOAT
	+NOGRAVITY
	+NOPAIN
	+SPAWNFLOAT	
	+DONTRIP
	SeeSound "mindb/see"
	ActiveSound "mindb/active"
	PainSound "mindb/ouch"
	DeathSound ""
	Obituary "%o's mind was broken."
	Tag "Mind Breaker"	
	PainChance "ShrinkerDMG", 0
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
		Spawn:
			ZBRO A 0
			ZBRO A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			ZBRO A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			ZBRO A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			ZBRO ABCDEF 4 A_Look
			Loop
		See:
			ZBRO A 2 A_Chase("","Missile")
			ZBRO A 2 A_Chase("","")
			ZBRO B 2 A_Chase("","Missile",CHF_FASTCHASE)
			ZBRO B 2 A_Chase("","") 
			ZBRO C 2 A_Chase("","Missile")
			ZBRO C 2 A_Chase("","")
			ZBRO D 2 A_Chase("","Missile")
			ZBRO D 2 A_Chase("","")
			ZBRO E 2 A_Chase("","Missile")
			ZBRO E 2 A_Chase("","")
			ZBRO F 2 A_Chase("","Missile",CHF_FASTCHASE)
			ZBRO F 2 A_Chase("","")
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			TNT1 A 0 A_PlaySound("mindb/attack")
			TNT1 A 0 A_Jump(128,"Attack1","Attack2","Attack3")		
			ZBRO EE 6 A_FaceTarget
			ZBRO E 6 A_CustomMissile("EradIonBoltBall",0,0,0,0)
			Goto See
		Pain:
			ZBRO D 1 A_Pain
			ZBRO DDD 1
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)	
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			ZBRO D 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			ZBRO D 16 A_Pain
			Goto See
		Attack1:
			ZBRO EE 6 A_FaceTarget
			TNT1 A 0 A_PlaySound("erad/laser3_b")
			ZBRO E 4 A_CustomMissile("EradMindBreakBall2",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			TNT1 A 0 A_Jump(200,"See")
			Goto Attack1+1
		Attack2:
			ZBRO EE 6 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1Small",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1Small",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			TNT1 A 0 A_Jump(170,"See")
			Goto Attack2+1
		Attack3:
			ZBRO EE 6 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1Small",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			ZBRO E 3 A_CustomMissile("EradMindBreakBall1",0,0,0,0)
			ZBRO E 2 A_FaceTarget
			TNT1 A 0 A_Jump(160,"See")
			Goto Attack3+1			
		XDeathForCatBitch:
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_Jump(96,"Death")
			TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1",1)
			TNT1 A -1 A_Jump(256,"Death")
			wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)	  		
		    TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_PlaySound("Eleena/Gibbed")
			TNT1 AAA 0 A_SpawnItemex("EleGibs1", 0, 0, random(8, 56), random(-4, 4), 0, random(0, 6))
			TNT1 AAAA 0 A_SpawnItemex("EradBloodDrop",0, 0, 32, random(3.0,5.0),random(3.0,5.0),random(0.0,10.0),random(0.0,360.0), SXF_ABSOLUTEANGLE , 0)
			TNT1 A 0 A_SpawnItemEx("MindBreakerStick",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)
			TNT1 A 0 A_SpawnItemEx("MindBreakerTail",0,0,0,random(-30,30)/10.00,random(-30,30)/10.00,random(50,150)/10.00)			
			TNT1 A 5 A_FadeOut
			Stop
	}
}

Actor MindBreakerStick : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        ZBRO GJ 4 
        loop
      Death:
	  Crash:
		ZBRO L 500 A_SetFloorClip
		ZBRO L 1 A_FadeOut
        Goto Death+1
    }
}

Actor MindBreakerTail : EradBaseGib
{
	Scale 0.60
    States
    {
      Spawn:
        ZBRO IH 4 
        loop
      Death:
	  Crash:
		ZBRO H 500 A_SetFloorClip
		ZBRO H 1 A_FadeOut
        Goto Death+1
    }
}
	
Actor EradIonBoltBall
{
	Radius 5
	Height 5
	Speed 22
	Scale 0.60
	Damage (random(3,10))
	Projectile
	+THRUGHOST
	+FORCEXYBILLBOARD
	DeathSound "Bolt/Spark1"
	States
	{
		Spawn:
			E4R_ BCEGHIJKLXY 3 Bright
			Loop
		Death:
			TNT1 A 0 A_Explode(random(2,28),random(60,40))
			4ER_ Z 4 Bright
			4E_R ABCDE 4 Bright
			Stop
	}
}

Actor EradMindBreakBall1
{
	Radius 8
	Height 8
	Speed 24
	Scale 0.65
	Damage (Random(10,15)/Random(2,1))
	Projectile
	+THRUGHOST
	+FORCEXYBILLBOARD
	DeathSound "erad/expshot2"
	States
	{
		Spawn:
			ERG_ JKLM 3 Bright
			Loop
		Death:
			TNT1 A 5 Bright A_SpawnItemEx("EradPlasmaExplosion3_2",0,0,0,0)
			Stop
	}
}

Actor EradMindBreakBall1Small
{
	Radius 3
	Height 3
	Speed 24
	Scale 0.60
	Damage (random(1,5))
	Projectile
	+THRUGHOST
	+FORCEXYBILLBOARD
	DeathSound "erad/expshot3"
	States
	{
		Spawn:
			ERG_ NOPQ 3 Bright
			Loop
		Death:
			TNT1 A 5 Bright A_SpawnItemEx("EradPlasmaExplosion3_1",0,0,0,0)
			Stop
	}
}

Actor EradMindBreakBall2
{
	Radius 5
	Height 5
	Speed 35
	Scale 0.60
	Damage (Random(2,12)+Random(3,0))
	Projectile
	+THRUGHOST
	+FORCEXYBILLBOARD
	SeeSound ""
	DeathSound "mindb/proht"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128,"Sound2")
			TNT1 A 0 A_PlaySound("mindb/proa2",CHAN_BODY,1.0,1,ATTN_NORM)
			Goto SpawnLoop
		Sound2:
			TNT1 A 0 A_PlaySound("mindb/proac",CHAN_BODY,1.0,1,ATTN_NORM)
		SpawnLoop:	
			ERG_ EFGHI 3 Bright
			Loop
		Death:
			TNT1 A 0 A_StopSound(CHAN_BODY)
		DeathLoop:	
			ERG_ EFGHI 3 Bright A_FadeOut
			Goto DeathLoop
		XDeath:
			TNT1 A 0 A_StopSound(CHAN_BODY)
			TNT1 E 3 Bright A_GiveToTarget("MindBreakerDizzy",1)
		XDeathLoop:
			ERG_ FGHIE 3 Bright A_FadeOut
			Goto XDeathLoop
	}
}

ACTOR MindBreakerDizzy : CustomInventory
{
    Inventory.MaxAmount 0
    +AUTOACTIVATE
    States
    {
	Spawn:
		TNT1 A 0
		Stop
    Pickup:
        TNT1 A 0
		TNT1 A 0 A_Jump(128,"Blend2","Blend3","Blend4","Blend5")
	Blend1:	
        TNT1 A 0 A_SetBlend("255 250 250",1,50)
        stop
	Blend2:
		TNT1 A 0 A_SetBlend("99 20 20",1,50)
		Stop
	Blend3:
		TNT1 A 0 A_SetBlend("6 184 200",1,50)
		Stop
	Blend4:
		TNT1 A 0 A_SetBlend("190 8 215",1,50)
		Stop
	Blend5:
		TNT1 A 0 A_SetBlend("201 254 100",1,50)		
		Stop		
    UseStop:
        TNT1 A 0
        stop
    }
}