ACTOR MMRandomizer_Annihilator : CyberDemon3
{
	Obituary "The Cyber Annihilator blew %o away with its rockets."
	Health 5000
	PainChance 10
	SeeSound "vdr/annihilator/sight"
	DeathSound "vdr/annihilator/death"
	Tag "Annihilator"
	States
	{
		Spawn:
			VD80 A 0
			VD80 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD80 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD80 A 0 A_GiveInventory("AlreadySpawned",1)
			VD80 A 0
		Spawn2:
			VD80 AB 10 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			TNT1 A 0 A_PlaySound("vdr/annihilator/hoof")
			VD80 AABBCC 3 A_Chase
			TNT1 A 0 A_PlaySound("vdr/annihilator/hoof")
			VD80 DD 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump(128,"HomingMissile")
			VD80 E 6 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,-27)
			VD80 E 12 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,-27)
			VD80 E 12 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorRocket",40,-27)
			TNT1 A 0 A_JumpIfTargetInLOS(1,0,JLOSF_NOSIGHT|JLOSF_DEADNOJUMP)
			Goto See2
			TNT1 A 0 A_Jump(128,"HomingMissile")
			Goto See2
		HomingMissile:
			VD80 E 6 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,-27)
			VD80 E 12 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,-27)
			VD80 E 12 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,27)
			VD80 F 12 Bright A_CustomMissile("MMRandomizer_AnnihilatorHomingRocket",40,-27)
			Goto See2
		Pain:
			VD80 G 10 A_Pain
			Goto See2
		Death:
			VD80 H 10 Bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD80 I 10 Bright A_Scream
			VD80 J 10 Bright A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD80 KL 10 Bright
			VD80 M 10 Bright A_NoBlocking
			VD80 NO 10 Bright
			VD80 P 30
			VD80 P -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
			Stop
	}
}

Actor MMRandomizer_AnnihilatorRocket : Rocket_Samsara
{
	-DEHEXPLOSION
	States
	{
	Death:
		TNT1 A 0 A_SetTranslucent(0.67,1)
		Goto Super::Death
	}
}

Actor MMRandomizer_AnnihilatorHomingRocket : MMRandomizer_AnnihilatorRocket
{
	Radius 8
	Height 11
	Speed 28
	Damage 10
	SeeSound "vdr/annihilator/hrfire"
	DeathSound "vdr/annihilator/hrexp"
	+SEEKERMISSILE
	+THRUGHOST
	States
	{
	Spawn:
		VE30 A 0 Bright
		VE30 AA 4 Bright A_SeekerMissile(18,27,SMF_PRECISE)
		TNT1 A 0 A_PlaySound("vdr/annihilator/hrbeep", CHAN_BODY, 0.6)
		VE30 AA 4 Bright A_SeekerMissile(18,27,SMF_PRECISE)
		Goto Homing
	Homing:
		TNT1 A 0 A_PlaySound("vdr/annihilator/hrbeep", CHAN_BODY, 0.6)
		VE30 A 4 Bright A_SeekerMissile(6,18,SMF_PRECISE)
		TNT1 A 0 A_JumpIfTargetInLOS(1,170,JLOSF_PROJECTILE|JLOSF_NOSIGHT)
		Goto NoHoming
		VE30 A 4 Bright A_SeekerMissile(6,18,SMF_PRECISE)
		TNT1 A 0 A_JumpIfTargetInLOS("Homing",170,JLOSF_PROJECTILE|JLOSF_NOSIGHT)
		Goto NoHoming
	NoHoming:
		VE30 A 1 Bright
		Loop
	Death:
		TNT1 A 0 A_SetTranslucent(0.67,1)
		VE25 C 8 Bright A_Explode(64,96,0)
		VE25 DE 5 Bright
		Stop
	}
}