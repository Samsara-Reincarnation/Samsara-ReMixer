Actor MMRandomizer_DarkCyberdemon : CyberDemon3
{
	Health 5000
	PainChance 10
	BloodColor "00 aa 00"
	+DONTHARMSPECIES
	SeeSound "vdr/darkcyberdemon/sight"
	PainSound "vdr/darkcyberdemon/pain"
	DeathSound "vdr/darkcyberdemon/death"
	ActiveSound "vdr/darkcyberdemon/active"
	Obituary "%o was pulverized by a dark cyberdemon."
	Tag "Dark Cyberdemon"
	States
	{
	Spawn:
		VD83 A 0
		VD83 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		VD83 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		VD83 A 0 A_GiveInventory("AlreadySpawned",1)
		VD83 A 0
	Spawn2:
		VD83 AB 10 A_Look
		Loop
	See:
		TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
	See2:
		VD83 AABB 3 A_Chase
		TNT1 A 0 A_PlaySound("vdr/darkcyberdemon/step", CHAN_7)
		VD83 CCDD 3 A_Chase
		TNT1 A 0 A_PlaySound("vdr/darkcyberdemon/step", CHAN_7)
		Loop
	Missile:
		TNT1 A 0 A_Jump(108, "HomingBall")
		Goto BFG10K
	BFG10K:
		VD83 E 6 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27)
		VD83 E 3 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27,random(-3,3) * 1.5)
		VD83 E 3 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27,random(-3,3) * 1.5)
		VD83 E 3 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27)
		VD83 E 3 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27,random(-3,3) * 1.5)
		VD83 E 3 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 6 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonBFG10kBall",47,-27,random(-3,3) * 1.5)
		Goto See2
	HomingBall:
		VD83 EEE 5 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		VD83 F 10 Bright A_CustomMissile("MMRandomizer_DarkCyberdemonHomingBall",56,-27)
		VD83 E 15 A_FaceTarget
		Goto See2
	Pain:
		VD83 G 10 A_Pain
		Goto See2
	Death:
		VD83 H 7 Bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		VD83 I 7 Bright A_Scream
		VD83 J 7 Bright A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		VD83 KL 7 Bright
		VD83 M 7 Bright A_NoBlocking
		VD83 NO 7 Bright
		VD83 P 30
		VD83 P -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
		Stop
	}
}

Actor MMRandomizer_DarkCyberdemonBFG10kBall
{
	Radius 11
	Height 8
	Speed 33
	Damage 13
	RenderStyle Add
	Alpha 0.75
	Scale 0.6
	PROJECTILE
	+BRIGHT
	DamageType "Explosive"
	Decal BFGLightning
	SeeSound "vdr/darkcyberdemon/bfg10kfire"
	DeathSound "doom/weapons/rocklx"
	States
	{
	Spawn:
		DBFS AABB 2 A_SpawnItemEx("MMRandomizer_DarkCyberdemonBFG10kBallTrail",-velx,-vely,-velz,0,0,0,0,SXF_ABSOLUTEPOSITION|SXF_CLIENTSIDE)
		Loop
	Death:
		DBFE A 3 A_Explode(100,128)
		DBFE BCDEF 3
		Stop
	}
}

Actor MMRandomizer_DarkCyberdemonBFG10kBallTrail
{
	RenderStyle Add
	Alpha 0.6
	Scale 0.6
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		DBFS AABB 2 A_FadeOut(0.15)
		Stop
	}
}

Actor MMRandomizer_DarkCyberdemonHomingBall
{
	Radius 12
	Height 12
	Speed 17
	Damage 23
	RenderStyle Add
	PROJECTILE
	+BRIGHT
	+FORCERADIUSDMG
	+SEEKERMISSILE
	+THRUGHOST
	DamageType "Explosive"
	Decal BFGLightning
	SeeSound "vdr/darkcyberdemon/homingbfgfire"
	DeathSound "vdr/darkcyberdemon/homingbfgexp"
	States
	{
	Spawn:
		VE38 A 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 B 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 C 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 D 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 E 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 F 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 G 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 H 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 I 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		VE38 J 2 A_SeekerMissile(1,18)
		TNT1 A 0 A_SpawnItemEx("MMRandomizer_DarkCyberdemonHomingBallTrail",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		Loop
	Death:
		VE38 K 2 A_Explode(140,140)
		VE38 LMNOPQRSTUVW 2
		Stop
	}
}

Actor MMRandomizer_DarkCyberdemonHomingBallTrail
{
	RenderStyle Add
	Alpha 0.5
	Scale 0.8
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 4
		VE38 L 2
		VE38 MNOP 2 A_FadeOut
		Stop
	}
}
