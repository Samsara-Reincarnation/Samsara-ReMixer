Actor MMRandomizer_Demolisher : SpiderMastermind3
{
	Obituary "%o felt the power of the Spider-Demolisher."
	Health 6000
	Speed 16
	PainChance 20
	+MISSILEEVENMORE
	SeeSound "vdr/demsit"
	DeathSound "vdr/demdth"
	Tag "Spider-Demolisher"
	Species "SamsaraDemolisher"
	States
	{
		Spawn:
			VD82 A 0
			VD82 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD82 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD82 A 0 A_GiveInventory("AlreadySpawned",1)
			VD82 A 0
		Spawn2:
			VD82 AB 10 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD82 AABB 3 A_Chase
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD82 CCDD 3 A_Chase
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD82 EEFF 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump(128,"ChaingunAttack")
			VD82 A 20 A_FaceTarget
			VD82 T 1 Bright A_PlaySound("doomguy/bfgf")
			VD82 TT 4 Bright A_FaceTarget
			VD82 TTTT 5 Bright A_FaceTarget
			TNT1 A 0 A_FaceTarget
			VD82 U 10 Bright A_CustomMissile("MMRandomizer_DemolisherBFGBall")
			TNT1 A 0 A_Jump(128,"ChaingunAttack")
			VD82 T 1 Bright A_PlaySound("doomguy/bfgf")
			VD82 TT 4 Bright A_FaceTarget
			VD82 TTTT 5 Bright A_FaceTarget
			TNT1 A 0 A_FaceTarget
			VD82 U 10 Bright A_CustomMissile("MMRandomizer_DemolisherBFGBall")
			TNT1 A 0 A_Jump(192,"ChaingunAttack")
			Goto See2
		ChaingunAttack:
			VD82 A 20 A_FaceTarget
			TNT1 AA 0 A_PlaySound("doom/chainguy/attack")
			TNT1 A 0 A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD82 G 4 Bright A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			TNT1 AA 0 A_PlaySound("doom/chainguy/attack")
			TNT1 A 0 A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD82 H 4 Bright A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD82 H 1 Bright A_SpidRefire
			Goto ChaingunAttack+1
		Pain:
			VD82 I 3
			VD82 I 3 A_Pain
			Goto See2
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD82 J 20 Bright A_Scream
			VD82 K 10 Bright A_NoBlocking
			VD82 L 10 Bright A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD82 MNOPQR 10 Bright
			VD82 S 30
			VD82 S -1 A_KillMaster
			Stop
	}
}

actor MMRandomizer_DemolisherBFGBall : BFGBall
{
	Decal BFGLightning
	+BRIGHT
	DeathSound "doomguy/bfgx"
    DamageType "BFG"
	States
	{
	Spawn:
		DBFS AB 4
		loop
	Death:
		DBFE AB 8
		DBFE C 8 A_BFGSpray("MMRandomizer_DemolisherBFGExtra")
		DBFE DEF 8
		stop
	}
}

actor MMRandomizer_DemolisherBFGExtra : DoomBFGSplash { DamageType "BFGSplash" }
