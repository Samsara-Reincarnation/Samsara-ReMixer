Actor MMRandomizer_ForgottenOne : SamsaraMonsterGhost
{
	Obituary "A Forgotten One rammed %o."
	Health 40
	Radius 14
	Height 30
	Mass 50
	Speed 8
	PainChance 230
	Scale 0.7
	PainSound "VDR/Forgotten/Pain"
	DeathSound "VDR/Forgotten/Death"
	ActiveSound "VDR/Forgotten/Active"
	Damage 3
	Monster
	+FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+FLOATBOB
	+NOBLOOD
	Tag "Forgotten One"
	Species "SamsaraForgottenOne"
	DamageFactor "LostSoul64Explosion", 0
	States
	{
		Spawn:
			VD63 A 0 Bright
			VD63 A 1 Bright A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD63 A 1 Bright ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD63 A 0 Bright A_GiveInventory("AlreadySpawned",1)
			VD63 A 0 Bright A_GiveInventory("IsAFlyingActor")
			VD63 A 0 Bright
		Spawn2:
			TNT1 A 0
			TNT1 A 0 A_ChangeFlag("FLOATBOB", 1)
			VD63 AAAAAABBBBBB 1 Bright A_Look
			Loop
		See:
			TNT1 A 0 A_ChangeFlag("FLOATBOB", 1)
			VD63 AABB 3 Bright A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("FLOATBOB", 0)
			VD63 CCDDC 2 Bright A_FaceTarget
			TNT1 A 0 A_PlaySound("VDR/Forgotten/Attack")
			Goto HomingCharge
		HomingCharge:
			VD63 D 2 Bright A_SkullAttack
			VD63 C 2 Bright
			TNT1 A 0 A_JumpIfTargetInLOS("HomingCharge", 75)
			Goto Charge
		Charge:
			VD63 D 2 Bright A_Jump(24,"StopCharge")
			VD63 C 2 Bright
			Loop
		StopCharge:
			TNT1 A 0 A_Stop
			Goto See
		Pain:
			VD63 E 3 Bright A_ChangeFlag("FLOATBOB", 1)
			VD63 E 3 Bright A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			VD63 E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD63 E 16 A_Pain
			Goto See
		Pain.DoomDamageTypeStunner:
			VD63 E 2 A_ChangeFlag("NOPAIN",true)
			VD63 E 300 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",false)
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			TNT1 A 0 A_ChangeFlag("FLOATBOB", 0)
			VD63 E 4 Bright A_Stop
			VD63 F 4 Bright
			VD63 G 6 Bright A_Scream
			VD63 H 6 Bright
			VD63 I 6 Bright A_NoBlocking
			VD63 J 6 Bright
			Stop		
	}
}