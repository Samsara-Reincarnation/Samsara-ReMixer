ACTOR MMRandomizer_StoneImp : SamsaraMonsterFlemoid
{
	Health 180
	Radius 20
	Height 56
	Speed 6
	Mass 400
	Monster
	BloodType "MMRandomizer_StoneBitsSpawner"
	DamageFactor "Poison", 0
	DamageFactor "PoisonBolt", 0
	DamageFactor "PoisonCloud", 0
	DamageFactor "Electric", 0
	DamageFactor "ElectricBolt", 0
	DamageFactor "Lightning", 0
	DamageFactor "Fire", 0.5
	DamageFactor "MarathonFire", 0.5
	DamageFactor "MarathonPlasmaFire", 0.5
	DamageFactor "Ice", 1.2
	RadiusDamageFactor 1.25
	SeeSound "doom/imp/sight"
	PainSound "doom/imp/pain"
	DeathSound "doom/imp/death"
	ActiveSound "doom/imp/active"
	Obituary "%o was smashed by a stone imp."
	Tag "Stone Imp"
	Species "SamsaraDoomImp"
	+FLOORCLIP
	+NOBLOODDECALS
	+NOPAIN
	States
	{
		Spawn:
			VD42 A 0
			VD42 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD42 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD42 A 0 A_GiveInventory("AlreadySpawned",1)
			VD42 A 0
		Spawn2:
			VD42 AB 10 A_Look
			Loop
		See:
			VD42 AABBCCDD 3 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			VD42 EF 8 A_FaceTarget
			VD42 G 6 A_CustomMeleeAttack(12 * random(1, 4), "VDR/stoneimp/hit")
			Goto See
		Pain:
			VD42 H 2
			VD42 H 2 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			VD42 H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD42 H 16 A_Pain
			Goto See
		Pain.DoomDamageTypeStunner:
			VD42 H 2 A_ChangeFlag("NOPAIN",true)
			VD42 H 300 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",false)
			goto See
		Death:
			VD42 I 6 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD42 J 6 A_Scream
			VD42 K 5
			VD42 L 5 A_Fall
			VD42 M 5
			VD42 N -1
			Stop
		StakeDeath:
			VD42 H 2
			Loop
		Raise:
			VD42 A 0
			"####" "#" 0 A_JumpIfInventory("EnemyZorched",1,"ZorchRaise")
			VD42 ML 8
			VD42 KJI 6
			Goto See
	}
}

// Stone bits SFX - by Blue Shadow (the spawning code is based on Nash's nashgore code)

ACTOR MMRandomizer_StoneBitsSpawner
{
	Mass 5
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTELEPORT
	States
	{
		Spawn:
			TNT1 A 0
			Goto High
			TNT1 A 0
			Goto Medium
			TNT1 A 0
			Goto Low
		Low:
			TNT1 AA 0 A_SpawnItemEx("MMRandomizer_StoneBitsSmall",0,0,8,random(-4,4),random(-4,4),random(2,5),0,SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEMOMENTUM|SXF_CLIENTSIDE)
			Goto SpawnPuff
		Medium:
			TNT1 AAA 0 A_SpawnItemEx("MMRandomizer_StoneBitsMedium",0,0,8,random(-4,4),random(-4,4),random(2,5),0,SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEMOMENTUM|SXF_CLIENTSIDE)
			Goto SpawnPuff
		High:
			TNT1 AAAAA 0 A_SpawnItemEx("MMRandomizer_StoneBitsLarge",0,0,8,random(-4,4),random(-4,4),random(2,5),0,SXF_ABSOLUTEPOSITION|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEMOMENTUM|SXF_CLIENTSIDE)
			Goto SpawnPuff
		SpawnPuff:
			TNT1 A 0 A_PlaySound("VDR/stoneimp/impact")
			TNT1 A 1 A_SpawnItemEx("DoomBulletPuff_Samsara",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			Stop
	}
}

ACTOR MMRandomizer_StoneBitsSmall
{
	Radius 2
	Height 2
	+NOTELEPORT
	+NOBLOCKMAP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(128, "SpawnB")
			Goto SpawnA
		SpawnA:
			VE08 A 0 A_Jump(192, 1, 2, 3)
			Goto Fade
			VE08 E 0
			Goto Fade
			VE08 I 0
			Goto Fade
			VE08 M 0
			Goto Fade
		SpawnB:
			VE08 B 0 A_Jump(128, 1)
			Goto Fade
			VE08 F 0
			Goto Fade
		Fade:
			"####" "#" 10
			"####" "#" 1 A_FadeOut(0.25)
			Wait
	}
}

ACTOR MMRandomizer_StoneBitsMedium : MMRandomizer_StoneBitsSmall
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256, "SpawnA", "SpawnB", "SpawnC")
			Goto SpawnA
		SpawnC:
			VE08 C 0 A_Jump(192, 1, 2, 3)
			Goto Fade
			VE08 G 0
			Goto Fade
			VE08 K 0
			Goto Fade
			VE08 O 0
			Goto Fade
	}
}

ACTOR MMRandomizer_StoneBitsLarge : MMRandomizer_StoneBitsMedium
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(256, "SpawnB", "SpawnC", "SpawnD")
			Goto SpawnB
		SpawnD:
			VE08 D 0 A_Jump(192, 1, 2, 3)
			Goto Fade
			VE08 H 0
			Goto Fade
			VE08 L 0
			Goto Fade
			VE08 P 0
			Goto Fade
	}
}