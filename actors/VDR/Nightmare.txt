ACTOR MMRandomizer_Nightmare : SamsaraMonsterGhost
{
	Health 180
	Radius 20
	Height 56
	Speed 15
	PainChance 144
	Monster
	+FLOORCLIP
	+STEALTH
	+NOBLOOD
	+GHOST
	-COUNTKILL
	RenderStyle Add
	Alpha 0.4
	SeeSound "VDR/nightmare/sight"
	PainSound "VDR/nightmare/pain"
	DeathSound "VDR/nightmare/death"
	ActiveSound "VDR/nightmare/active"
	MeleeSound "VDR/nightmare/melee"
	HitObituary "%o was tormented by a nightmare."
	MeleeDamage 4
	Tag "Nightmare"
	Species "SamsaraDemon"
	States
	{
		Spawn:
			VD50 A 0
			VD50 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD50 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD50 A 0 A_GiveInventory("AlreadySpawned",1)
			VD50 A 0
		Spawn2:
			VD50 AB 10 A_Look
			Loop
		See:
			VD50 AABBCCDD 3 A_Chase
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_NightmareWisp",0,0,0,0,0,0,0,SXF_CLIENTSIDE,224)
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			VD50 EF 9 A_FaceTarget
			VD50 G 9 A_MeleeAttack
			Goto See
		Pain:
			VD50 H 2
			VD50 H 10 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			VD50 H 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD50 H 16 A_Pain
			Goto See
		Pain.DoomDamageTypeStunner:
			VD50 H 2 A_ChangeFlag("NOPAIN",true)
			VD50 H 300 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",false)
			goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD50 I 3 A_ScreamAndUnblock
			VD50 JKLMNO 3
			Stop
	}
}

ACTOR MMRandomizer_NightmareWisp
{
	RenderStyle Add
	Alpha 0.4
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			VD50 IJKLMNO 3
			Stop
	}
}
