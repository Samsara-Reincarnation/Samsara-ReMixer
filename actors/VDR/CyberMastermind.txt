Actor MMRandomizer_CyberMastermind : SpiderMastermind3
{
	Obituary "%o was destroyed by a cyber mastermind."
	Health 5000
	PainChance 30
	SeeSound "doom/cyber/sight"
	DeathSound "doom/cyber/death"
	Tag "Cyber Mastermind"
	Species "SamsaraCyberMastermind"
	States
	{
		Spawn:
			VD91 A 0
			VD91 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD91 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD91 A 0 A_GiveInventory("AlreadySpawned",1)
			VD91 A 0
		Spawn2:
			VD91 AB 10 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD91 AABB 3 A_Chase
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD91 CCDD 3 A_Chase
			TNT1 A 0 A_PlaySound("doom/spider/walk", CHAN_BODY)
			VD91 EEFF 3 A_Chase
			Loop
		Missile:
			VD91 A 20 A_FaceTarget
			VD91 GH 4 BRIGHT A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD91 H 1 BRIGHT A_FaceTarget
			VD91 GH 4 BRIGHT A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD91 H 1 BRIGHT A_FaceTarget
			TNT1 A 0 A_CustomMissile("Rocket_Samsara")
			VD91 IJ 4 BRIGHT A_CustomBulletAttack(22.5, 0, 3, random(1,5) * 3, "DoomBulletPuff_Samsara", 0, CBAF_NORANDOM)
			VD91 J 1 BRIGHT A_SpidRefire
			Goto Missile+1
		Pain:
			VD91 K 3
			VD91 K 3 A_Pain
			Goto See2
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			VD91 L 8 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffectForBosses",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD91 M 8 A_Scream
			VD91 N 6
			VD91 O 6 A_Fall
			VD91 PQRST 6
			VD91 U -1 A_KillMaster
			Stop
	}
}