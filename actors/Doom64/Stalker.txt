ACTOR Stalker64 : LostSoul64
{
	Health 200
	Tag "Stalker"
	Species "SamsaraStalker64"
	-NOTARGET
	-MISSILEEVENMORE
	+MISSILEMORE
	MinMissileChance 180
	AttackSound ""
	SeeSound "64stalker/sight"
	PainSound "64stalker/pain"
	Obituary "%o was flambeed by a Stalker."
	HitObituary "%o was flambeed by a Stalker."
	States
	{
		Spawn:
			64ST A 0 bright
			64ST A 1 bright A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			64ST A 1 bright ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			64ST A 0 bright A_GiveInventory("AlreadySpawned",1)
			64ST A 0 bright A_GiveInventory("IsAFlyingActor")
		Spawn2:
			64ST A 2 bright A_Look
			Loop
		See:
			64ST A 2 bright A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			64ST B 15 bright A_FaceTarget
			TNT1 A 0 A_JumpIfInventory("Stalker64TeleportCooldown",1,"Tracer")
			TNT1 A 0 A_JumpIfCloser(1024,1)
			Goto Tracer
			TNT1 A 0 A_Jump(128,"Teleport")
			Goto Tracer
		Tracer:
			TNT1 A 0 A_FaceTarget
			64ST B 5 bright A_CustomMissile("StalkerTracer64",12)
			64ST B 15 bright A_FaceTarget
			Goto See
		Teleport:
			TNT1 A 0 A_SpawnItemEx("StalkerTeleportFog64",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_Jump(256, 1, 13, 25)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Wander
			TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
			TNT1 AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
			TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
			TNT1 AAAAAAAAAAAAAAAAAAAAA 1 A_Wander
			TNT1 AAAAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
			TNT1 A 0 A_GiveInventory("Stalker64TeleportCooldown",1)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_SetShootable
			TNT1 A 0 A_SpawnItemEx("StalkerTeleportFog64",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			64ST B 5 bright A_CustomMissile("StalkerTracer64",12)
			64ST B 10 bright A_FaceTarget
			Goto See
		Pain:
			64ST C 3 bright
			64ST C 3 bright A_Pain
			Goto See
		Pain.Vertigo:
			TNT1 A 0 ThrustThingZ(0, 55, 0, 0)
			Goto Pain
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
			64ST C 2 bright A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			64ST C 16 bright A_Pain
			Goto See
		Death:
			64ST D 6 bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			64ST E 6 bright A_Scream
			64ST F 4 bright
			64ST G 3 bright A_NoBlocking
			64ST HIJKLM 3 bright A_FadeOut(0.20)
			Stop
		Crush:
			TNT1 A 1
			Stop
	}
}

ACTOR StalkerTracer64 : RevenantTracer64
{
	Damage 8
	Speed 10
	States
	{
		Spawn:
			64RB AB 2 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB A 1 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB A 1 A_Tracer2
			64RB BA 2 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB B 0 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB B 2 A_Tracer2
			64RB A 2 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB B 1 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB B 1 A_Tracer2
			64RB AB 2 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB A 0 A_SpawnItemEx("Doom64RevenantSmokePuff",0,0,0,0,0,1,0,SXF_CLIENTSIDE)
			64RB A 2 A_Tracer2
			Goto Spawn+1
	}
}

ACTOR StalkerTeleportFog64
{
	+BRIGHT
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 0.8
	States
	{
		Spawn:
			64HF D 0
			64HF D 4 A_PlaySound("64stalker/warp")
			64HF CBABCDEFGH 4
			Stop
	}
}

Actor Stalker64TeleportCooldown : Powerup
{
	Powerup.Duration 87
}