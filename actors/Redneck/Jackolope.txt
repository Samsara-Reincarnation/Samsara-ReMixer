ACTOR Jackelope : SamsaraMonsterBeastTotalGib
{
    Tag "Jackelope"
	Radius 16
	Height 44
	Speed 1
	Health 50
	PainChance 100
	Mass 50
	Scale 0.48
	Damage 3
	MONSTER
	Species "RedneckEnemies"
	+FLOORCLIP
	+MISSILEEVENMORE
	+NOINFIGHTING
	+NOBLOCKMONST
	-NORADIUSDMG
	+NOBLOODDECALS
	Obituary "%o was bitten by a jackelope."
	SeeSound "jackelope/active"
	ActiveSound "jackelope/active"
	AttackSound "jackelope/attack"
	PainSound "jackelope/pain"
	MeleeSound "jackelope/attack"
	BloodType "LeonardBlood_RR"
	Bloodcolor "ba 2c 10"
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	States
	{
	Spawn:
		JY_7 M 0
		JY_7 M 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		JY_7 M 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		JY_7 M 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		JY_7 M 5 A_Look
		Loop
	Pain:
		TNT1 A 0 A_Pain
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",6,"LandCheck")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",5,"MidJump5")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",4,"MidJump4")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",3,"MidJump3")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",2,"MidJump2")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck1",1,"MidJump1")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",7,"MissileLandCheck")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",6,"MidMissileJump6")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",5,"MidMissileJump5")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",4,"MidMissileJump4")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",3,"MidMissileJump3")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",2,"MidMissileJump2")
		TNT1 A 0 A_JumpIfInventory("JackelopeJumpCheck2",1,"MidMissileJump1")
		Goto See
	See:
		JY_7 MMM 1 A_Chase("Melee","")
		JY_7 MMM 1 A_Chase
		JY_7 MNO 2
		TNT1 A 0 A_Recoil(-8)
		TNT1 A 0 A_PlaySound("jackelope/jump",CHAN_AUTO)
		JY_7 P 2 ThrustThingZ(0,32,0,0)
		JY_7 Q 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto MidJump1
	MidJump1:
		JY_7 R 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto MidJump2
	MidJump2:
		JY_7 S 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto MidJump3
	MidJump3:
		JY_7 T 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto MidJump4
	MidJump4:
		JY_7 U 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto MidJump5
	MidJump5:
		JY_7 V 2 A_GiveInventory("JackelopeJumpCheck1",1)
		Goto LandCheck
	LandCheck:
		JY_7 V 1 A_CheckFloor(1)
		Loop
		TNT1 A 0 A_Stop
		TNT1 A 0 A_TakeInventory("JackelopeJumpCheck1",6)
		Goto See
	Missile:
		TNT1 A 0 A_JumpIfCloser(350,"FuckMissile")
		Goto See
	FuckMissile:
		JY_7 BCD 2 A_FaceTarget
		TNT1 A 0 A_PlaySound("jackelope/jump2",CHAN_AUTO)
		JY_7 E 1 A_SkullAttack
		JY_7 E 1 ThrustThingZ(0,32,0,0)
		JY_7 F 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump1
	MidMissileJump1:
		JY_7 G 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump2
	MidMissileJump2:
		JY_7 H 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump3
	MidMissileJump3:
		JY_7 I 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump4
	MidMissileJump4:
		JY_7 J 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump5
	MidMissileJump5:
		JY_7 K 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MidMissileJump6
	MidMissileJump6:
		JY_7 L 2 A_GiveInventory("JackelopeJumpCheck2",1)
		Goto MissileLandCheck
	MissileLandCheck:
		JY_7 L 1 A_CheckFloor(1)
		Loop
		TNT1 A 0 A_Stop
		TNT1 A 0 A_TakeInventory("JackelopeJumpCheck2",7)
		Goto See
	Melee:
		JY_7 BCD 3 A_FaceTarget
		JY_7 E 3 A_CustomMeleeAttack(5*random(1,8))
		JY_7 DC 3 A_FaceTarget
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
		TNT1 A 0 A_ChangeFlag("SOLID",false)
		TNT1 A 0 A_PlaySound("Leonard/XDeath",CHAN_BODY)
		TNT1 A 0 A_CustomMissile("JackelopeGib1",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("JackelopeGib2",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("JackelopeGib2",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("JackelopeGib3",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("JackelopeGib3",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib1_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib2_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib3_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib4_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib5_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 0 A_CustomMissile("MiscGib6_RR",0+Random(30,60),0+Random(35,-35),0+Random(0,370))
		TNT1 A 5 A_NoBlocking
		Stop
	Death.LeonardDamage2:
	Death.LeonardDamage3:
		TNT1 A 0 A_JumpIfHealthLower(-20, "XDeathForLeonard")
	Death.RRBullet:
	Death.LeonardDamage0:
	Death.LeonardDamage1:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LeonardDMTaunt1", 1)
		TNT1 A -1 A_Jump(256,"Death")
		Wait
	XDeathForLeonard:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LeonardDMTaunt1",1)
		TNT1 A -1 A_Jump(256,"Death")
		Stop
	}
}

ACTOR JackelopeJumpCheck1 : Inventory { Inventory.Amount 1 Inventory.MaxAMount 6 }
ACTOR JackelopeJumpCheck2 : Inventory { Inventory.Amount 1 Inventory.MaxAMount 7 }

ACTOR JackelopeGib1
{
	Scale 0.15
	Radius 6
	Height 5
	Speed 10
	+MISSILE
	+CLIENTSIDEONLY
	-NOGRAVITY
	-NOBLOCKMAP
	-SOLID
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,random(10,40),0,1)
	SpawnLoop:
		JY_9 ABCDE 4
		Loop
	Death:
		TNT1 A 5
		Stop
	}
}

ACTOR JackelopeGib2 : JackelopeGib1
{
	Scale 0.19
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,random(10,40),0,1)
	SpawnLoop:
		JY_9 FGHIJ 4
		Loop
	}
}

ACTOR JackelopeGib3 : JackelopeGib1
{
	Scale 0.19
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,random(10,40),0,1)
	SpawnLoop:
		JKL3 KLMNO 4
		Loop
	}
}