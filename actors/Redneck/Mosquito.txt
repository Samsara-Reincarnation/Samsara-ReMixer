Actor Mosquito : SamsaraMonsterGhost
{
    Tag "Mosquito"
	Health 1
	MONSTER
	SeeSound ""
	ActiveSound ""
	DeathSound ""
	PainSound ""
	AttackSound ""
	Speed 20
	PainChance 0
	Height 36
	Species "RedneckEnemies"
	Radius 20
	Obituary "%o was stung by a mosquito."
	Mass 10
	+NOPAIN
	+NOBLOOD
	+NOGRAVITY
	+FLOAT
	+FLOORCLIP
	+NOTARGET
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOINFIGHTING
	+NOBLOODDECALS
	Scale 0.30
	//Dropitem "DefaultPinataPop"
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	States
	{
	Spawn:
		MOSQ A 0
		MOSQ A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		MOSQ A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		MOSQ A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		TNT1 A 0 A_PlaySound("Mosquito/Fly",CHAN_BODY,0.7,1)
		Goto Idle
	Idle:
		MOSQ ABCDEF 3 A_Look
		Loop
	See:
		MOSQ A 3 A_Chase
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		MOSQ BC 3 A_Chase
		TNT1 A 0 A_Chase("","")
		MOSQ DE 3 A_Chase
		TNT1 A 0 A_Chase("","",CHF_FASTCHASE)
		MOSQ F 3 A_Chase
		Loop
	Missile:
		MOSQ A 1 A_FaceTarget
		MOSQ A 1 A_SkullAttack
		MOSQ A 1 A_FaceTarget
		MOSQ BCDEF 3 A_FaceTarget
		Goto Missile+2
	Melee:
		MOSQ A 2 A_FaceTarget
		TNT1 A 0 A_CustomMeleeAttack(4)
		MOSQ F 2 A_FaceTarget
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_StopSound(CHAN_BODY)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("RRMOSQUITOWING1",0,0,0,random(-320,320)/10.00,random(-320,320)/10.00,random(50,200)/10.00)
		TNT1 A 5 A_SpawnItemEx("RRMOSQUITOWING2",0,0,0,random(-320,320)/10.00,random(-320,320)/10.00,random(50,200)/10.00)
		Stop
	}
}

Actor RRMOSQUITOWING1 : RRCOCKFEATHER
{
	Scale 0.30
	Gravity 0.087
	+WINDTHRUST
	States
	{
		Spawn:
			MOSQ GGGGHHHHIIIIJJJJ 1 A_Weave(1,0,12,0)
			Loop
		Death:
			TNT1 A 5
			Stop
	}
}

Actor RRMOSQUITOWING2 : RRMOSQUITOWING1
{
	States
	{
		Spawn:
			MOSQ KKKKLLLLMMMMNNNN 1 A_Weave(1,0,12,0)
			Loop
	}
}