Actor StrifeStalker : SamsaraMonsterRobotic
{
	Tag "Stalker"
	Species "StrifeEnemies"
	Health 80
	Painchance 40
	Speed 16
	Radius 30
	Height 25
	DamageFactor "CorvusCrossbow", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	MONSTER
	+DROPOFF
	+NOBLOOD
	+FLOORCLIP
	+NOINFIGHTING
	MaxDropOffHeight 32
	MinMissileChance 150
	SeeSound "stalker/sight"
	PainSound "stalker/pain"
	DeathSound "stalker/death"
	ActiveSound "stalker/active"
	HitObituary "$OB_STALKER"
	States
	{
		Spawn:
			STLK K 0
			STLK K 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			STLK K 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			STLK K 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			STLK K 5 A_Look
			Loop
		See:
			STLK J 3 A_Chase
			STLK KK 3 A_Chase
			TNT1 A 0 A_PlaySound("stalker/walk",5)
			STLK L 3 A_Chase
			STLK L 3 A_Chase
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			STLK J 3 A_FaceTarget
			STLK K 3 A_CustomMeleeAttack(2*random(1,8),"stalker/attack")
			Goto See
		Pain:
			STLK L 1 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			STLK O 4
			STLK P 4 A_Scream
			STLK QRST 4
			STLK U 4 A_NoBlocking
			STLK VW 4
			STLK "XYZ[" 4 Bright
			Stop
		Crush:
			TNT1 A -1
			Stop
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
			STLK L 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			STLK L 16 A_Pain
			Goto See

		// SamsaraMonsterHumanoid states
		Disintegrate:
		XDeath.PunchDagger:
		XDeath.ElectricBolt:
		XDeath.PoisonBolt:
		XDeath.StrifeExplosive:
		XDeath.StrifeSigil:
			TNT1 A -1 A_Jump(256,"Death")
			Wait
	}
}

Actor ShadowStalker : StrifeStalker
{
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	Obituary "%o thought %g saw a stalker."
	States
	{
		Death:
			TNT1 A 0 A_ClearShadow
			goto "Super::Death"
	}
}

Actor StrifeStalkerCeiling : StrifeStalker
{
	+NOGRAVITY
	+SPAWNCEILING
	+NOVERTICALMELEERANGE
	+CEILINGHUGGER
	+MOVEWITHSECTOR
	States {
		Spawn:
			TNT1 A 0 A_Jump(256,"LookFloor")
			TNT1 A 0 A_CheckFlag(NOGRAVITY,"CeilingSet")
		LookFloor:
			STLK K 5 A_Look
			Loop
		CeilingSet:
			TNT1 A 0 A_GiveInventory("IsOnCeiling",1)
			6K4B B 5 ThrustThingZ(0,500,0,1)
			TNT1 A 0 A_ChangeFlag("CEILINGHUGGER",0)
		LookCeiling:
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			6K4B B 5 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("IsOnCeiling",1,"SeeCeiling")
		SeeLoop:
			STLK J 3 A_Chase
			STLK KK 3 A_Chase
			STLK L 0 A_PlaySound("stalker/walk",CHAN_5)
			STLK L 3 A_Chase
			STLK L 3 A_Chase
			Loop
		SeeCeiling:
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			TNT1 A 0 A_CheckCeiling(1)
			Goto Drop
			6K4B A 3 A_Chase
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			TNT1 A 0 A_CheckCeiling(1)
			Goto Drop
			6K4B B 3 A_Chase
			TNT1 A 0 A_Jump(2,"Drop")
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			TNT1 A 0 A_CheckCeiling(1)
			Goto Drop
			6K4B B 3 A_Chase
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			TNT1 A 0 A_CheckCeiling(1)
			Goto Drop
			TNT1 A 0 A_PlaySound("stalker/walk",5)
			6K4B C 3 A_Chase
			TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "Drop")
			TNT1 A 0 A_CheckCeiling(1)
			Goto Drop
			6K4B C 3 A_Chase
			Loop
		Drop:
			TNT1 A 0 A_ChangeFlag("NOVERTICALMELEERANGE",0)
			TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
			TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR",0)
			TNT1 A 0 A_TakeInventory("IsOnCeiling",1)
			6K4B DEFGHI 3
		Landing:
			6K4B I 1 A_CheckFloor("See")
			Loop
		Melee:
			TNT1 A 0 A_JumpIfInventory("IsOnCeiling",1,"Drop")
			STLK J 3 A_FaceTarget
			STLK K 3 A_CustomMeleeAttack(2*random(1,8),"stalker/attack")
			Goto See
		Pain:
			TNT1 A 0 A_JumpIfInventory("IsOnCeiling",1,"CeilingPain")
			STLK L 1 A_Pain
			Goto See
		CeilingPain:
			6K4B C 1 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_JumpIfInventory("IsOnCeiling",1,"CeilingDeath")
			STLK O 4
			STLK P 4 A_Scream
			STLK QRST 4
			STLK U 4 A_NoBlocking
			STLK VW 4
			STLK "XYZ[" 4 Bright
			Stop
		CeilingDeath:
			6K4B O 4
			6K4B P 4 A_Scream
			6K4B QRST 4
			6K4B U 4 A_NoBlocking
			6K4B VW 4
			6K4B "XYZ[" 4 Bright
			Stop
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_JumpIfInventory("IsOnCeiling",1,"Pain.MightyBootCeiling")
			TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
			STLK L 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			STLK L 16 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
		Pain.MightyBootCeiling:
			TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
			6K4B C 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			6K4B C 16 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			Goto See
	}
}

ACTOR IsOnCeiling : Boolean { }

Actor ShadowStalkerCeiling : StrifeStalkerCeiling
{
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	Obituary "%o thought %g saw a stalker."
	States
	{
		Death:
			TNT1 A 0 A_ClearShadow
			goto "Super::Death"
	}
}