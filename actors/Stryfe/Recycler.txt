ACTOR Recycler : SamsaraMonsterRobotic
{
	Tag "Recycler"
	MONSTER
	Species "Enemies"
	+NOBLOOD
	+MISSILEMORE
	+NOGRAVITY
	+FLOAT
	+NOINFIGHTING
	MeleeRange 73
	PainChance 128
	Speed 14
	Height 48
	Radius 32
	Scale 0.85
	Health 220
	AttackSound "recycler/ar_fire"
	SeeSound "crusader/sight"
	PainSound "sentinel/sight"
	DeathSound "strife/miniexplode"
	Obituary "%o was shot down by a Recycler."
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.0
	PainChance "ShrinkerDMG", 0
	States
	{
	Spawn:
		RECI A 0
		RECI A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		RECI A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		RECI A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:
		RECI ABCDEEEEE 5 A_Look
		Loop
	See:
		TNT1 A 0 A_TakeInventory("TurretFire",10)
		TNT1 A 0 A_Jump(64,"IDLE1","IDLE2")
		RECS AAAAAA 3 A_Chase
		Goto See+1
	Idle1:
		RCR1 AB 5 A_Chase
		RCR1 B 4 A_Chase
		RCR1 CD 3 A_Chase
		Goto See
	Idle2:
		RCR2 ABBCDFEFE 2 A_Chase
		Goto See
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_TakeInventory("TurretFire",10)
		RECM ABCD 3 A_FaceTarget
		RECM E 1 A_CustomMeleeAttack(5*(random(1,3)),"weapons/plasmax")
		RECM FEFEFEFE 1
		Goto See
	Missile:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		TNT1 A 0 A_JumpIfInventory("TurretFire",10,"TurretEnd")
		TNT1 A 0 A_GiveInventory("TurretExtended",1)
		RCR3 ABC 5 A_FaceTarget
	TurretCont:
		TNT1 A 0 A_GiveInventory("TurretFire",1)
		RCR3 E 3 BRIGHT A_CustomBulletAttack(11.0,8.0,1,Random(1,2)*3,"StrifeBulletPuff",2048,CBAF_NORANDOM)
		RCR3 D 3 BRIGHT
		TNT1 A 0 A_JumpIfInventory("TurretFire",10,"TurretEnd")
		TNT1 A 0 A_MonsterRefire(64,"TurretEnd")
		Loop
	TurretEnd:	
		TNT1 A 0 A_TakeInventory("TurretFire",10)
		TNT1 A 0 A_TakeInventory("TurretExtended",1)
		RCR3 CBA 5
		Goto See
	Pain:
		TNT1 A 0 A_TakeInventory("TurretFire",10)
		TNT1 A 0 A_JumpIfInventory("TurretExtended",1,"PainTurretExtend")
		RECP A 3 A_Pain
		RECP BABAB 3
		Goto See
	PainTurretExtend:
		TNT1 A 0 A_TakeInventory("TurretExtended",1)
		RPT1 C 3 A_Pain
		RPT1 DCDEFEFABAB 3
		RCR3 CBA 3
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_TakeInventory("TurretFire",10)
		TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
		RECP A 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		RECP A 16 A_Pain
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		RECD A 4 A_Fall
		RECD A 1 A_Scream
		RECD BCD 5 A_Explode(2,96,XF_NOTMISSILE)
		RECD E 5
		RECD F -1
		Stop

	// SamsaraMonsterRobotic states
	Disintegrate:
	XDeath.PunchDagger:
	XDeath.ElectricBolt:
	XDeath.PoisonBolt:
	XDeath.StrifeExplosive:
	XDeath.StrifeSigil:
		TNT1 A -1 A_Jump(256,"Death")
		Wait
	}
}

ACTOR TurretExtended : Boolean { }
ACTOR TurretFire : Counter { Inventory.MaxAmount 10 }
