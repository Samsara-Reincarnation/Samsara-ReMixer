Actor Q2Flyer : Q2SoldierBase
{
	SeeSound "Flyer/Sight"
	ActiveSound "Flyer/Active"
	PainSound "Flyer/Pain"
	DeathSound "Flyer/Death"
	AttackSound ""
	MeleeDamage 3
	Speed 8
	Mass 50
	+NOGRAVITY 
	+FLOAT 
	+FLOATBOB 
	+DONTFALL 
	+DONTGIB
	Health 50
	PainChance 30
	DropItem Q2CellPickup 128
    Obituary "%o was killed by a flyer."
	Tag "Strogg Flyer"
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	States
	{
	Spawn:
		FLR1 A 0
		FLR1 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		FLR1 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		FLR1 A 0 A_GiveInventory("AlreadySpawned",1)
		FLR1 A 0 ACS_NamedExecuteAlways("Quake2_Enemy")
	Spawn2:
		FLR1 A 0 A_PlaySound("Flyer/Fly",CHAN_BODY,0.5,1)
		FLR1 ABCDEFGHIJKLMNOPQRSTUV 3 A_Look
		Goto Spawn+1
	See:
		FLR1 A 0 A_ChangeFlag(NOPAIN,0)
		FLR1 ABCDEFGHIJKLMNOPQRSTUV 3 A_Chase
		Loop
	Melee:
		FLR3 A 0 A_PlayWeaponSound("Flyer/Melee1")
		FLR1 A 5
		FLR3 ABCDEFGHI 4 A_FaceTarget
		FLR3 J 0 A_PlayWeaponSound("Flyer/Melee2")
		FLR3 J 3 A_MeleeAttack
		FLR3 KLM 4 A_FaceTarget
		FLR3 N 0 A_PlayWeaponSound("Flyer/Melee2")
		FLR3 N 3 A_MeleeAttack
		FLR3 OPQRSTU 4 A_FaceTarget
		Goto See
	Missile:
		FLR1 A 8 A_FaceTarget
		FLR2 ABCD 3 A_FaceTarget
		FLR2 E 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 E 4 A_CustomMissile(FlyerProjectile,15,-15)
		FLR2 F 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 F 4 A_CustomMissile(FlyerProjectile,15,17)
		FLR2 G 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 G 4 A_CustomMissile(FlyerProjectile,15,-15)
		FLR2 H 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 H 4 A_CustomMissile(FlyerProjectile,15,17)
		FLR2 I 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 I 4 A_CustomMissile(FlyerProjectile,15,-15)
		FLR2 J 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 J 4 A_CustomMissile(FlyerProjectile,15,17)
		FLR2 K 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 K 4 A_CustomMissile(FlyerProjectile,15,-15)
		FLR2 L 0 A_FaceTarget TNT1 A 0 A_PlayWeaponSound("Flyer/Attack")
		FLR2 L 4 A_CustomMissile(FlyerProjectile,15,17)
		FLR2 MNOPQ 3
		Goto See
	Pain:
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		TNT1 A 0 A_Jump(256,"Pain1","Pain2","Pain3")
	Pain1:
		FLR4 ABCDEFGH 5
		Goto See
	Pain2:
		FLR4 IJKL 5
		Goto See
	Pain3:
		FLR4 MNOP 5
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		FLR4 I 5 A_FaceTarget
		FLR4 I 0 A_Recoil(14)
		FLR4 J 5 A_Pain
		FLR4 KL 5
		Goto See
	Death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
		TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)		
		FLR1 A 10
		TNT1 A 0 A_Fall
		TNT1 A 0 A_PlaySound("Q2Weapons/RocketExplosion")
		TNT1 A 5 A_SpawnItem(Q2Explosion)
		Stop
	Burn:
		TNT1 A 0 A_Playsound("Strogg/Lava",CHAN_VOICE)
		Goto Death+1
	}
}

Actor FlyerProjectile : HyperBlasterProjectile { Damage (Random(2,4)) }