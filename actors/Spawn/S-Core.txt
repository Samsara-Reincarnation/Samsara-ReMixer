/*
	Template for ReMixer monster spawner. It defines behavior that all spawners share, including handling the
	AMBUSH and DORMANT flags. Both flags are stored inside a user variable referenced in the spawn script.
	The DORMANT flag is handled through inheriting from SwitchableDecoration. Actors with the DORMANT flag
	start in the "Inactive" state. Everything else starts in the "Spawn"/"Active" state.
*/
ACTOR ReMixerBaseSpawner : SwitchableDecoration
{
	Health 0X7FFFFFFF
	Mass 100
	+COUNTKILL
	+DONTBLAST
	+DONTGIB
	+DONTMORPH
	+DONTSPLASH
	+DONTSQUASH
	+INVULNERABLE
	+ISMONSTER
	+NEVERRESPAWN
	+NEVERTARGET
	+NOBLOOD
	+NOICEDEATH
	+NONSHOOTABLE
	+NOTAUTOAIMED
	+NOTDMATCH
	+NOTELEOTHER
	+NOTIMEFREEZE
	+SHOOTABLE
	+SOLID
	+NODECAL //A flag that has no use here. Use this to clear it from visor vision.
	var int user_special;
	var int user_args[5];
	var int user_ambush;
	var int user_dormant;
	var int user_spawner_state;
	States
	{
		Spawn:
		Active:
			TNT1 A 1
			TNT1 A 0 A_JumpIf(user_spawner_state == 2, "SeeWaitLoop")
			TNT1 A 0 A_JumpIf(user_spawner_state == 3, "BossWaitLoop")
			TNT1 A 0 A_SetUserVar("user_dormant",0)
			Goto SpawnCheckAmbush
		Inactive:
			TNT1 A 1
			TNT1 A 0 A_JumpIf(user_spawner_state == 2, "SeeWaitLoop")
			TNT1 A 0 A_JumpIf(user_spawner_state == 3, "BossWaitLoop")
			TNT1 A 0 A_SetUserVar("user_dormant",1)
			Goto SpawnCheckAmbush
		SpawnCheckAmbush:
			TNT1 A 0 A_JumpIf(user_spawner_state == 1, "LookWaitLoop")
			TNT1 A 0 A_CheckFlag("AMBUSH", "SpawnAmbush")
			TNT1 A 0 A_SetUserVar("user_ambush",0)
			Goto SpawnEnd
		SpawnAmbush:
			TNT1 A 0 A_SetUserVar("user_ambush",1)
			TNT1 A 0 A_ChangeFlag("AMBUSH",false)
			Goto SpawnEnd
		SpawnEnd:
			TNT1 A 0 A_SetUserVar("user_special",special)
			TNT1 A 0 A_SetUserArray("user_args",0,args[0])
			TNT1 A 0 A_SetUserArray("user_args",1,args[1])
			TNT1 A 0 A_SetUserArray("user_args",2,args[2])
			TNT1 A 0 A_SetUserArray("user_args",3,args[3])
			TNT1 A 0 A_SetUserArray("user_args",4,args[4])
			TNT1 A 0 A_Jump(256,"Initialize")
		Initialize:
			TNT1 A 0 A_JumpIf(CallACS("ReMixerCVarCheck",13) == 0, "See")
			Goto LookWait
		LookWait:
			TNT1 A 0 A_SetUserVar("user_spawner_state",1)
			Goto LookWaitLoop
		LookWaitLoop:
			TNT1 A 0 A_CheckSightOrRange(512,2)
			TNT1 A 0 A_Jump(256,"See")
			TNT1 A 4 A_TurretLook
			Loop
		SeeWait:
			TNT1 A 0 A_SetUserVar("user_spawner_state",2)
			Goto SeeWaitLoop
		SeeWaitLoop:
		BossWaitLoop:
			TNT1 A 1
			Loop
		BossWait:
			TNT1 A 2 A_JumpIf(CallACS("RemixerInvasionChecker") == 1, "Remove")
			TNT1 A 0 A_ChangeFlag("COUNTKILL",false)
			TNT1 A 0 A_ChangeFlag("SOLID",false) // Zandronum syncs A_ChangeFlag better than A_UnSetSolid
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_ChangeFlag("VULNERABLE",true)
			TNT1 A 0 A_SetUserVar("user_spawner_state",3)
			TNT1 A 0 A_Jump(256,"BossWaitLoop")
		Continue:
		Death:
		Remove:
			TNT1 A 1 Thing_Remove(0)
			Stop
	}
}
