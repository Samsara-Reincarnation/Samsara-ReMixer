ACTOR Quake_HellSpawn : SamsaraMonsterGhost
{
	Tag "Hell Spawn"
	Health 70
	PainChance 0
	Speed 3
	Radius 16
	Height 56
	Mass 99999999
	MONSTER
	Species "QuakeEnemies"
	+FLOORCLIP 
	+FASTER 
	+FASTMELEE 
	+MISSILEMORE 
	+MISSILEEVENMORE
	+NOINFIGHTING
	+THRUSPECIES
	+DONTFALL
	+DONTGIB
	MinMissileChance 1
	Damage (Random(11,20)*1)
	DamageFactor "Slime", 0
	PainChance "ShrinkerDMG", 0
	SeeSound "Qspawn/see"
	DeathSound "Qspawn/die"
	Obituary "%o was slimed by a Hell Spawn."
	BloodType "Quake_NewParticles"
	States
	{
	  Spawn:
		SPWN A 0
		SPWN A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		SPWN A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		SPWN A 0 A_GiveInventory("AlreadySpawned",1)
	  Spawn2:
		SPWN A 10 A_Look
		Loop
	  See:
		SPW2 A 0 A_JumpIfInventory("QFiendLeapCounter",1,"Land")
		SPW2 ABCDEFGHIJKLMNOPQRSTUVWXY 1 A_Chase
		Loop
	  Melee:
		SPW3 AB 2 A_facetarget
		SPW3 B 0 A_CustomMeleeAttack(random(11,20)*1, "Spawn/hit") 
		SPW3 C 0  A_Playsound("Spawn/land")
		SPW3 C 0 ThrustThing(0,0,8)
		SPW3 CDEF 2 A_Recoil(4)
		SPW3 GHIJ 1
		SPW3 EDF 2
		Goto see
	  Missile:
		SPW3 A 0 A_CheckFloor("Leap")
		Goto see
	  Leap:
		SPW3 A 0
		SPW3 A 0 A_GiveInventory("QFiendLeapCounter",1)
		SPW3 A 0 A_FaceTarget
		SPW3 AB 2 A_CustomMeleeAttack(random(2,6)*1, "QSpawn/hit") 
		SPW3 A 0 A_Skullattack
		SPW3 C 0  A_Playsound("QSpawn/land")
		SPW3 CDEF 2 A_CustomMeleeAttack(random(2,6)*1, "QSpawn/hit") 
	  Jump:
		SPW3 GHIJ 2 A_CustomMeleeAttack(random(2,6)*1, "QSpawn/hit") 
		FIN3 F 0 A_ChangeFlag("Nogravity",0)
	  Falling:
		SPW3 GHIJ 2 A_CustomMeleeAttack(random(2,6)*1, "QSpawn/hit") 
		SPW3 F 0 A_CheckFloor("Land")
		Loop
	  Land:
		SPW3 F 0 A_Stop
		SPW3 EDF 3
		SPW3 A 0 A_TakeInventory("QFiendLeapCounter",1)
		SPW3 A 0 A_ChangeFlag("NOPAIN", 0)
		Goto see
	  Death:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)  
		SPWN K 0
		SPWN K 0 A_NoGravity
		SPWN K 4 A_Scream
		TNT1 A 0 A_SpawnItemEx("QuakeExplosion_Mixer")
		TNT1 A 0 Bright A_Explode(10,70)
		TNT1 A 5 A_NoBlocking
		Stop
  }
}

Actor Quake_HellSpawn_2 : Quake_HellSpawn { 
	Tag "Baby Spawn" 
	Health 60 
	Obituary "%o was slimed by a Baby Spawn."
}