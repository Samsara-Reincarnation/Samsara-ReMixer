Actor WitchavenShadowHound : WitchavenEnemyBaseMeleeSkeleton
{
	+DONTMORPH
	+NOFEAR
	+NOPAIN
	+CANPASS
	+BOSSDEATH
	-SHOOTABLE
	-NOBLOOD
	+DONTRIP
	Scale 0.55
	Speed 15
	Health 300
	Mass 1500
	MeleeRange 78
	SeeSound "wthound/act"
	ActiveSound "wthound/act"
	PainSound ""
	DeathSound "wthound/die"
	DropItem "WHShadowAmulet",5,1
	Obituary "%o was lost in the shadows..."
	Tag "Shadow Hound"
	Bloodcolor "Black"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_JumpIfInventory("AlreadySpawned",1,"SpawnStandard")
			TNT1 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			TNT1 A 0 A_GiveInventory("AlreadySpawned",1)
		SpawnStandard:
			TNT1 A 5 A_Look
			Loop
		See:
			WH_H AABB 3 A_Chase("","")
			TNT1 A 0 A_SetShootable
			WH_H CCDDEEFFGGHH 3 A_Chase
			TNT1 A 0 A_UnSetShootable
			WH_H IIJJ 3 A_Chase("","")
			TNT1 AAAAAAAAAAAAAAAAAAAA 2 A_Chase("","")
			WH_H KKLL 3 A_Chase("","")
			TNT1 A 0 A_SetShootable
			WH_H MMNNOOPPQQRR 3 A_Chase
			TNT1 A 0 A_UnSetShootable
			WH_H SSTT 3 A_Chase("","")
			TNT1 AAAAAAAAAAAAAAAAAAAA 2 A_Chase("","")
			Loop
		Melee:
			TNT1 A 0 A_SetShootable
			TNT1 A 0 A_Jump(128,"Style2")
			WH_H CCDDEEFF 2 Bright A_FaceTarget
			WH_H G 4 Bright A_CustomMeleeAttack(Random(3,15),"wthound/crunch","","ShadowHoundBite")
			TNT1 A 0 A_UnSetShootable
			Goto See
		Style2:
			WH_H MMNNOOPP 2 Bright A_FaceTarget
			WH_H Q 4 Bright A_CustomMeleeAttack(Random(3,15),"wthound/crunch","","ShadowHoundBite")
			TNT1 A 0 A_UnSetShootable
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			WH_H F 2 A_KillMaster
			WH_H F 2 A_Scream
			WH_H F 2 A_NoBlocking
			WH_H F 2 A_FadeOut(0.1)
			Wait
	}
}