Actor WTSkeleton : WitchavenEnemyBaseMeleeSkeleton
{
	Health 25
	Scale 0.35
	+NOBLOOD
	SeeSound "wtskele/see"
	ActiveSound "wtskele/see"
	PainSound "wtskele/pain"
	DeathSound "wtskele/die"
	Obituary "%o was traumatized by a skeleton."
	PainChance "ShrinkerDMG", 0
	DamageFactor "PoisonBolt", 0.0
	Tag "Skeleton"
	States
	{
	Spawn:
		WH_F E 0
		WH_F E 1 A_JumpIfInventory("AlreadySpawned",1,"SpawnStandard")
		WH_F E 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		WH_F E 0 A_GiveInventory("AlreadySpawned",1)
	SpawnStandard:
		WH_F E 5 A_Look
		Loop
	See:
		WH_F AABBCCDDEE 3 A_Chase
		Loop
	Melee:
		TNT1 A 0 A_ChangeFlag("NOPAIN",0)
		WH_F FG 4 A_FaceTarget
		WH_F H 3 A_FaceTarget
		WH_F I 5 A_CustomMeleeAttack(Random(3,15),"wtskele/melee","")
		WH_F J 2
		Goto See
	Pain:
		TNT1 A 0 A_Jump(100,1,"Pain2")
		TNT1 AA 0 A_SpawnItemEx("WTGore_Skeleton",0,0,frandom(5.0,35.0),random(-4,4),random(-4,4),random(0,5))
	Pain2:
		WH_F O 8 A_Pain
		Goto See
	Pain.MightyBoot:
		TNT1 A 0 A_Jump(100,1,3)
		TNT1 AA 0 A_SpawnItemEx("WTGore_Skeleton",0,0,frandom(5.0,35.0),random(-4,4),random(-4,4),random(0,5))
		TNT1 A 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		WH_F O 2 A_FaceTarget
		TNT1 A 0 A_Recoil(14)
		WH_F O 16 A_Pain
		Goto See
	Death:
		WH_F P 3 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		WH_F Q 3 A_Scream
		WH_F R 3 A_NoBlocking
		WH_F STUVW 3
		WH_F W -1
		Stop
	Death.Nuke:
		WH_A H 4 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_Scream
		WH_A I 4 A_NoBlocking
		WH_A JKL 4
		TNT1 A -1
		Stop
	}
}