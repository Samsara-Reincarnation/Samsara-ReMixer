Actor WitchavenScaryScreamer : WitchavenEnemyBaseMeleeSkeleton
{
	+FLOAT
	+NOGRAVITY
	+DONTMORPH
	+NOFEAR
	+NOBLOOD
	+DROPOFF
	+NOPAIN
	+CANPASS
	+SPAWNFLOAT
	Scale 0.50
	Mass 1000
	Speed 10
	Health 400
	DropItem "WHShadowAmulet",10,1
	SeeSound "wtscary/see"
	ActiveSound ""
	PainSound ""
	MeleeRange 80
	DeathSound "wtscary/die"
	Obituary "%o couldn't overcome %p fears."
	DropItem "WHShadowAmulet",5,1
	Tag "Demon"
	States
	{
		Spawn:
			WH_P D 0
			WH_P D 1 A_JumpIfInventory("AlreadySpawned",1,"SpawnStandard")
			WH_P D 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			WH_P D 0 A_GiveInventory("AlreadySpawned",1)
		SpawnStandard:
			WH_P D 5 A_Look
			Loop
		See:
			WH_P DEFG 6 Bright
		SeenPlayer:
			TNT1 AAAAAA 2 A_Chase
			TNT1 A 0 A_Jump(20,"ScareMe")
			TNT1 AAAAAA 2 A_Chase
			TNT1 A 0 A_Jump(20,"ScareMe")
			Loop
		ScareMe:
			TNT1 A 0 A_PlaySound("wtscary/see",CHAN_BODY)
			WH_P DDDEEEFFF 2 Bright A_Chase
		DontLookAtMe:
			WH_P GGGGGGGGGG 2 Bright A_Chase
			TNT1 A 0 A_Jump(128,3)
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_CustomMissile("WTFireMaceBall_Enemy_Wisp",5)
			WH_P GGGGGGGGGG 2 Bright A_Chase
			TNT1 A 0 A_Jump(25,"DontLookAtMe")
			Goto SeenPlayer
		Melee:
			TNT1 A 0 A_PlaySound("wtscary/att",CHAN_BODY)
			WH_P DDEEFF 2 Bright A_FaceTarget
			WH_P G 2 Bright A_CustomMeleeAttack(random(30,60)/random(1,2),"","")
			WH_P G 2 Bright A_FaceTarget
		GoAway:
			TNT1 AAAA 2 A_Chase("","")
			TNT1 AAAA 2 A_Chase
			TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
			TNT1 AAAA 2 A_Chase
			TNT1 AAAA 2 A_Wander
			TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
			Goto SeenPlayer
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_KillMaster
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			WH_P G 10 Bright A_NoGravity
			WH_P F 10 Bright A_Scream
			WH_P E 10 Bright A_NoBlocking
			WH_P D 2 Bright A_FadeOut(0.01)
			Wait
	}
}