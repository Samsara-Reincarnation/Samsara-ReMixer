Actor WTMuckDemoness : WitchavenEnemyBaseRangedBeast
{
	Health 50
	Speed 8
	+NOPAIN
	SeeSound "wtlfiend/see2"
	ActiveSound "wtlfiend/see2"
	DeathSound "greywitch/die"
	BloodColor Green
	Obituary "%o was swallowed by the demoness."
	Tag "Muck Demoness"
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.0
	States
	{
		Spawn:
			WH_M A 0
			WH_M A 1 A_JumpIfInventory("AlreadySpawned",1,"SpawnStandard")
			WH_M A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			WH_M A 0 A_GiveInventory("AlreadySpawned",1)
		SpawnStandard:
			WH_M AB 5 A_Look
			Loop
		See:
			WH_M A 3 A_Chase("","",CHF_NOPLAYACTIVE)
			WH_M AB 3 A_Chase
			WH_M B 3 A_Chase("","",CHF_NOPLAYACTIVE)
			WH_M AA 3 A_Chase
			WH_M B 3 A_Chase("","",CHF_NOPLAYACTIVE)
			WH_M B 3 A_Chase
			Goto See+1
		Missile:
			WH_M CCDDEEFF 2 A_FaceTarget
			TNT1 A 0 A_PlaySound("greywitch/toss")
			WH_M GGHHIIJJ 2 A_FaceTarget
			WH_M K 1 A_CustomMissile("MuckDemonessSnot",20,5)
			WH_M KGGFFEEDDCC 2
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			WH_M L 5 A_Scream
			WH_M M 5
			WH_M N 5 A_NoBlocking
			WH_M OPQR 5
			TNT1 A -1
			Stop
		Death.Nuke:
			WH_M S 4 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_Scream
			WH_M T 4 A_NoBlocking
			WH_M UVW 4
			TNT1 A -1
			Stop
	}
}

Actor MuckDemonessSnot
{
	Speed 20
	Radius 7
	Height 6
	PROJECTILE
	DamageType Poison
	+BRIGHT
	Scale 0.70
	Damage (random(5,15))
	States
	{
		Spawn:
			WH_M XY 4
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}