Actor GoldFireBoss : SamsaraMonsterAlienBoss {
	Health 4800
	Radius 40
	Height 100
	Speed 8
	Mass 2048
	PainChance 16
	PainChance "ShrinkerDMG", 0
	Scale 1.7
	Species "Enemies"
	MONSTER
	+FLOORCLIP
    +NOTDMATCH
    +NOICEDEATH
    +THRUGHOST
	+MISSILEMORE
	+DONTGIB
    +NOTARGET
    +NOINFIGHTING
	+DONTMORPH
	+BOSSDEATH
	+LOOKALLAROUND
	+BOSS
	+DONTRIP
	SeeSound ""
	PainSound ""
	DeathSound "genericalien/death1"
	ActiveSound ""
	AttackSound ""
	Tag "Morphed Dr. Goldfire"
	Obituary "%o was wrecked by the final form of Dr. Goldfire."
    DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "WolfEnemyBullet", 0.0
	Decal Scorch
	States {
		Spawn:
			BS31 A 0
			BS31 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			BS31 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			BS31 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			BS31 A 5 A_Look
			Loop
		See:
			TNT1 A 0 A_GiveInventory("SamsaraBossSightTaunt")
		See2:
			BS31 AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			BS31 E 4 A_FaceTarget
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
			BS31 FF 6 A_FaceTarget
			TNT1 A 0 A_PlaySound("plasmaalien/shoot")
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, -20)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, -16)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, -12)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, -8)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, -4)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, 4)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, 8)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, 12)
			TNT1 A 0 A_CustomMissile("GoldFireShot", 40, 0, 16)
			BS31 G 3 A_CustomMissile("GoldFireShot", 40, 0, 20)
			BS31 E 6
			Goto See2
		Pain:
			BS31 H 4 A_Pain
			BS31 H 4
			Goto See2
		Death:
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			BS31 H 8 A_Scream
			BS31 I 9 A_NoBlocking
			BS31 JKLMN 9 Bright
			TNT1 A 0 A_KillMaster
			TNT1 A -1
			Stop		
		}
}

Actor GoldFireShot : SpectorDemonShot {
	Speed 32
	Damage (random(9, 4) * Random(1, 2))
	Decal None
	States {
		Death:
			TNT1 A 0 A_Explode(Random(3, 5) * Random(4, 1), 128, 0)
			BF10 DEF 4 Bright
		Stop
	}
}
