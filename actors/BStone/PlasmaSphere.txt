actor PlasmaSphere : SamsaraMonsterGhost {
	MONSTER
	Health 1
	SeeSound ""
	AttackSound "plasmaalien/hit"
	PainSound ""
	DeathSound "plasmaalien/death"
	ActiveSound ""
	Species "Enemies"
	Speed 10
	FloatSpeed 10
	Painchance 0
	DamageFactor "PlasmaSphere", 0.0
	Height 24
	Radius 12
	Mass 32
	Obituary "%o didn't see the Plasma Sphere nearby."
	Tag "Plasma Sphere"
	RenderStyle Add
    +NOPAIN
	+NOBLOOD
	+NOGRAVITY
	+FLOAT
	+NOTARGET
	-COUNTKILL
	+DONTHARMCLASS
	+DONTHARMSPECIES
    States {
      Spawn:
		BSE9 A 0
		BSE9 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		BSE9 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
		BSE9 A 0 A_GiveInventory("AlreadySpawned",1)
	  Spawn2:
        BSE9 ABC 10 A_Look
        Loop
      See:
	    TNT1 A 0 A_Chase
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
        BSE9 A 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 A 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 A 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 B 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 B 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 B 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 C 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 C 2 A_SpawnItem("PlasmaSphereDamager")
		TNT1 A 0 A_GiveInventory("FaceCheck", 1)
		BSE9 C 2 A_SpawnItem("PlasmaSphereDamager")
        Loop
	  Death:
        TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
        BSE9 D 6 
        BSE9 E 6 A_Scream
        BSE9 F 6 
        BSE9 G 6 A_NoBlocking
        BSE9 H 6
        TNT1 A -1
        Stop
    }
}

Actor PlasmaSphereDamager {
	Height 2
	Radius 2
	+NOGRAVITY
	DamageType "PlasmaSphere"
	States {
		Spawn:
			TNT1 AA 1 A_Explode(Random(1, 4), 32, 0) //Nerfed due to obvious reasons
		Stop
	}
}

Actor FaceCheck : CustomInventory {
	inventory.maxamount 1
	+AUTOACTIVATE
	+ALWAYSPICKUP
	States {
		Pickup:
		Use:
			TNT1 A 0 A_JumpIfTargetInLOS("Face", 0)
			TNT1 A 0 A_Chase
		Stop
		Face:
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 ThrustThing(angle*256/360, 2, 1, 0)
		Stop
	}
}
