ACTOR SentryDrone : SamsaraMonsterRobotic
{
	Health 75
	Radius 16
	Height 30
	Speed 6
	PainChance 256
	Scale 0.65
	MONSTER
	+FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOTDMATCH
	+NOICEDEATH
	+THRUGHOST
	+DONTGIB
	+NOTARGET
	+NOINFIGHTING
	+DONTMORPH
	+NOBLOOD
	SeeSound "sentdrone/sight"
	PainSound "sentdrone/pain"
	DeathSound "sentdrone/detonate"
	ActiveSound "sentdrone/active"
	Tag "Sentry Drone"
	Obituary "A Sentry Drone delivered its payload to %o."
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	States
	{
		Spawn:
			SEDR A 0
			SEDR A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			SEDR A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			SEDR A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			SEDR A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_PlaySound("sentdrone/active", CHAN_6, 1.0, 1, 2)
		See2:
			SEDR A 1 A_Wander
			SEDR A 1 A_Chase
			Loop
		Pain:
			TNT1 A 0 A_Pain
			Goto See
		Missile:
			SEDR AAAAAAAAAAAAAAA 2 A_FaceTarget
			TNT1 A 0 A_PlaySound("sentdrone/charge")
			SEDR A 4 A_SkullAttack
			TNT1 A 0 A_Recoil(5)
			SEDR A 1
			Wait
		Melee:
			SEDR A 1 A_FaceTarget
			SEDR A 1 A_Die("NotSuicide")
			Wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		Death.NotSuicide:
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_Scream
			TNT1 A 5 A_SpawnItemEx("SentryDroneBomb",0,0,32)
			Stop
	}
}

ACTOR SentryDroneBomb
{
	+NOINTERACTION
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("duke/explosion", CHAN_6, 1.0, 0, ATTN_NONE)
			TNT1 A 0 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 AAAAAAAA 0 A_SpawnDebris("DukeShrapnel1")
			TNT1 AAAA 0 A_SpawnDebris("DukeShrapnel2")
			TNT1 AAAAAAA 0 A_SpawnDebris("DukeShrapnel3")
			TNT1 A 5 A_Explode(12, 142, 1)
			Stop
  }
}
