Actor Seagull : SamsaraMonsterBeast
{
	Health 20
	Radius 12
	Height 24
	Mass 15
	Speed 4
	Scale 0.65
	Gravity 0.50
	PainChance 0
	MinMissileChance 248
	MeleeRange 512
	MONSTER
	+FLOORCLIP
	+NOTDMATCH
	+THRUGHOST
	+NOTARGET
	+NOINFIGHTING
	+NOPAIN
	+NOBLOODDECALS
	+NOGRAVITY
	+FLOAT
	DeathSound "seamonster/death"
	ActiveSound "Seagull/Roam"
	Obituary "A seagull pooped on %o."
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	BloodType "DukeBlood"
	BloodColor "ff 40 00"
	Tag "Seagull"
	States
	{
		Spawn:
			DN76 A 5 A_Look
			Loop
		See:
			DN76 AAAAA 1 A_Chase("","")
			DN76 BBBBB 1 A_Chase("","")
			DN76 CCCCC 1 A_Chase("","")
			DN76 C 0 A_Jump(224,"See")
			DN76 C 0 A_Chase("Melee","")
			Loop
		Melee:
			DN76 A 0 A_SpawnItemEx("SeagullShitDrop",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			DN76 A 5 A_CustomBulletAttack(0,0,1,1,"SeagullShitter",512,CBAF_NORANDOM)
			Goto See
		Death.DukeExplosive:
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A -1 A_Jump(256,"Death")
			Wait
		Death.MightyBoot:
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			TNT1 A -1 A_Jump(256,"Death")
			Wait
		Death:
		XDeath:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("DukeJibs1")
			TNT1 A 0 A_SpawnDebris("DukeJibs2")
			TNT1 AA 0 A_SpawnDebris("DukeJibs3")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs4")
			TNT1 AA 0 A_SpawnDebris("DukeJibs5")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs6")
			TNT1 A 5 A_PlaySound("duke/enemygib")
			Stop
	}
}

Actor SeagullShitter
{
	Height 4
	Radius 2
	+NOINTERACTION
	+ALWAYSPUFF
	+PUFFONACTORS
	States
	{
		Spawn:
		Melee:
		Crash:
		Death:
		XDeath:
			TNT1 A 0
			TNT1 A 0 A_SpawnDebris("SeagullShitDebris")
			Stop
	}
}

actor SeagullShitDebris
{
	Health 4
	Speed 2
	Mass 0
	BounceFactor 0.45
	BounceCount 4
	+MISSILE
	+DOOMBOUNCE
	+FLOORCLIP
	+DONTSPLASH
	+CLIENTSIDEONLY
	+MOVEWITHSECTOR
	+NOTELEPORT
	+NOTARGET
	+THRUACTORS
	//Scale 0.75
	States
	{
	Spawn:
	Death:
		TNT1 A 0 A_Jump(255, "Death1","Death2","Death3","Death4")
	Death1:
		DN76 D 5 A_CheckFloor("Remove")
		Loop
	Death2:
		DN76 E 5 A_CheckFloor("Remove")
		Loop
	Death3:
		DN76 F 5 A_CheckFloor("Remove")
		Loop
	Death4:
		DN76 G 5 A_CheckFloor("Remove")
		Loop
	Remove:
		TNT1 A 1
		Stop
	}
}

ACTOR SeagullShitDrop
{
	Height 2
	Radius 2
	Scale 0.1625
	-SOLID
	+THRUACTORS
	+CLIENTSIDEONLY
	-NOGRAVITY
	States
	{
		Spawn:
			DN76 H 5 A_CheckFloor("Death")
			Loop
		Death:
			TNT1 A 1
			Stop
	}
}