ACTOR PigCopTank : SamsaraMonsterRobotic
{
	Health 500
	Radius 32
	Height 64
	Mass 1000
	Speed 10
	PainChance 0
	Scale 0.94
	MONSTER
	+FLOORCLIP
	+NOTDMATCH
	+NOICEDEATH
	+THRUGHOST
	+NOBLOOD
	+NOTARGET
	+NOINFIGHTING
	+DONTMORPH
	+DONTRIP
	AttackSound "duke/m1911"
	Obituary "%o was shredded by a Pig Cop Tank."
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "Tesla", 2.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0
	DamageFactor "HLBullet", 0.2
	DamageFactor "GEBullet", 0.2
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	Tag "Pig Cop Tank"
	Decal DukeBulletchip
	States
	{
		Spawn:
			PIGT A 0
			PIGT A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PIGT A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			TNT1 A 0 A_SpawnItemEx("TankSelfDestruct",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_NOCHECKPOSITION)
			PIGT A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PIGT A 1 A_Look
			Loop
		See:
			TNT1 A 0 A_PlaySound("pigtank/active", CHAN_VOICE)
			PIGT AAAAAAAAAAAAAAA 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_JumpIfInventory("TankMissileToken", 11, "MissileEnd")
			TNT1 A 0 A_GiveInventory("TankMissileToken",1)
			PIGT B 1 A_FaceTarget(22)
			PIGT B 0 A_JumpIfTargetInLOS(1,0,JLOSF_NOSIGHT|JLOSF_DEADNOJUMP)
			Goto See
			PIGT B 2 Bright A_CustomBulletAttack(12,12,1,5,"DukeEnemyBulletPuff",0,CBAF_NORANDOM)
			PIGT C 0 A_JumpIfTargetInLOS(1,0,JLOSF_NOSIGHT|JLOSF_DEADNOJUMP)
			Goto See
			PIGT C 3 Bright A_CustomBulletAttack(12,12,1,5,"DukeEnemyBulletPuff",0,CBAF_NORANDOM)
			Loop
		MissileEnd:
			TNT1 A 0 A_TakeInventory("TankMissileToken", 11)
			TNT1 A 0 A_JumpIfCloser(1024, 3)
			TNT1 A 0 A_PlaySound("duke/rocketfire", CHAN_WEAPON)
			TNT1 A 0 A_CustomMissile("BattleLordMortar1",32,0,0,CMF_CHECKTARGETDEAD|CMF_AIMDIRECTION,random(10,20))
			PIGT A 40
			Goto See
		Death:
			TNT1 A 0 A_RemoveChildren(TRUE)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0, 0, ATTN_NONE)
			TNT1 A 1 A_SpawnItemEx("RPGExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			TNT1 A 1 A_SpawnItemEx("SamsaraMixerPigCop",0,0,0,0,0,0,0,0,127)
			TNT1 A -1 A_KillMaster
			Stop
	}
}

ACTOR TankSelfDestruct : SwitchableDecoration
{
	+THRUACTORS
	+USESPECIAL
	+THRUSPECIES
	+ISMONSTER
	-COUNTKILL
	Activation THINGSPEC_Activate
	Species "Enemy"
	Height 8
	Radius 8
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_Warp(AAPTR_MASTER,-48,0,32,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		Loop
	Active:
		TNT1 A 4 A_PlaySound("PigTank/Beep")
		TNT1 A 0 A_JumpIfInventory("SelfDestructTimer",4,2)
		TNT1 A 0 A_GiveInventory("SelfDestructTimer",1)
		Goto Active
		TNT1 A 1 A_KillMaster
		Stop
	}
}

Actor SelfDestructTimer : Inventory { Inventory.MaxAmount 4 }

ACTOR PigCopTankArch : PigCopTank
{
	Height 56
	Radius 20
	YScale 0.80
	XScale 0.80
}
