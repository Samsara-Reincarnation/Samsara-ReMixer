ACTOR ProtectorDrone : SamsaraMonsterAlien
{
	Health 300
	Radius 20
	Height 56
	Mass 400
	Speed 8
	Scale 0.7
	Gravity 0.50
	Painchance 32
	MONSTER
	+FLOORCLIP
	+NOTDMATCH
	+THRUGHOST
	+NOINFIGHTING
	+NOBLOODDECALS
	SeeSound "prodrone/see"
	PainSound "prodrone/pain"
	DeathSound "prodrone/death"
	ActiveSound "prodrone/roam"
	AttackSound "prodrone/attack"
	Obituary "%o was clawed to death by a Protector Drone."
	MeleeRange 48
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	BloodType "ProDroneBlood"
	BloodColor "aa d1 5c"
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	Tag "Protector Drone"
	States
	{
		Spawn:
			PRDR A 0
			PRDR A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			PRDR A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			PRDR A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			PRDR A 6 A_Look
			Loop
		See:
			PRDR BBBB 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR CC 1 A_Chase("Melee",0)
			PRDR C 1 A_Chase
			PRDR C 1 A_Chase("Melee",0)
			PRDR DDDD 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR EE 1 A_Chase("Melee",0)
			PRDR E 1 A_Chase
			PRDR E 1 A_Chase("Melee",0)
			TNT1 A 0 A_JumpIfCloser(384,"See")
			Goto Run
		Run:
			PRDR B 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR B 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR B 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_JumpIfCloser(384,"Jump")
			PRDR C 1 A_Chase("Melee",0)
			TNT1 A 0 A_Chase
			PRDR C 1 A_Chase("Melee",0)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR C 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0)
			PRDR C 1 A_Chase
			TNT1 A 0 A_Chase("Melee",0)
			TNT1 A 0 A_JumpIfCloser(384,"Jump")
			PRDR D 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR D 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR D 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_JumpIfCloser(384,"Jump")
			PRDR E 1 A_Chase("Melee",0)
			TNT1 A 0 A_Chase
			PRDR E 1 A_Chase("Melee",0)
			TNT1 A 0 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			PRDR E 1 A_Chase("Melee",0,CHF_NOPLAYACTIVE)
			TNT1 A 0 A_Chase("Melee",0)
			PRDR E 1 A_Chase
			TNT1 A 0 A_Chase("Melee",0)
			TNT1 A 0 A_JumpIfCloser(384,"Jump")
			Loop
		Jump:
			PRDR L 1 ThrustThingZ(0,35,0,0)
			TNT1 A 0 ThrustThing(angle*256/360,10,0,0)
			PRDR L 13 A_CheckFloor("See")
			Goto MidJump
		MidJump:
			PRDR M 17 A_CheckFloor("See")
			PRDR N 1 A_CheckFloor("See")
			Goto EndJump
		EndJump:
			PRDR N 1 A_CheckFloor("See")
			PRDR N 4 ThrustThing(angle*256/360,1,0,0)
			Loop
		Melee:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PRDR FFF 2 A_FaceTarget
			PRDR F 1
			PRDR GH 7
			TNT1 A 0 A_CustomMeleeAttack(22,"prodrone/attack1","prodrone/attack2")
			TNT1 A 0 A_JumpIfCloser(64,"See")
			Goto MeleeEnd
		MeleeEnd:
			PRDR A 26
			PRDR A 14 A_PlaySound("prodrone/roam")
			Goto See
		Missile:
			PRDR I 0 A_FaceTarget
			PRDR I 21 A_PlayWeaponSound("prodrone/spit")
			PRDR I 0 A_FaceTarget
   			PRDR I 0 A_JumpIf(ACS_NamedExecuteWithResult("ReMixerCVarCheck",6) == 0, "MissileAlt")
			PRDR J 10 bright A_CustomMissile("AlienShrinkerAttack",32,0,0)
			PRDR J 4
			Goto See
		MissileAlt:
   			PRDR J 10 bright A_CustomMissile("AlienShrinkerAttackAlt",32,0,0)
			PRDR J 4
			Goto See
		Pain:
			PRDR O 3
			PRDR O 3 A_Pain
			Goto See
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			PRDR O 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			PRDR O 16 A_Pain
			Goto See
		Death.DukeExplosive:
			TNT1 A 0 A_JumpIfHealthLower(-3,"DukeGibStart")
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			TNT1 A -1 A_Jump(256,"Death")
			Wait
		Death.MightyBoot:
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			TNT1 A -1 A_Jump(256,"Death")
			Wait
		DukeGibStart:
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_Jump(96,"XDeath")
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A -1 A_Jump(256,"XDeath")
			Wait
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			PRDR P 5 A_Scream
			PRDR Q 5 A_NoBlocking
			PRDR R 4
			PRDR STU 5
			PRDR V 4
			TNT1 A -1 A_SpawnItemEx("ProtectorDroneCorpse", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
			Stop
		XDeath:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs2")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs3")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs4")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs5")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs6")
			TNT1 A 0 A_Jump(32,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs1")
			TNT1 A 0 A_Jump(64,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs7")
			TNT1 A 0 A_Jump(64,2)
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs9")
			TNT1 A 0 A_Jump(64,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs8")
			TNT1 A 5 A_PlaySound("duke/enemygib")
			Stop

		// SamsaraMonsterAlien states
		ExpanderPop:
			"####" "#" 10
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_SpawnItemEx("ExpanderExplosion_Basic",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0)
			TNT1 A 1 ACS_NamedExecuteWithResult("SamsaraDecorate",36,1)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt", 1)
			TNT1 A 0 A_TakeInventory("ExpanderGrowthY", 40)
			TNT1 A 0 A_TakeInventory("ExpanderGrowthX", 40)
			TNT1 A 0 A_TakeInventory("ExpanderDoubleX", 40)
			TNT1 A 0 A_SetScale((user_scalexoriginal)/65536.0, (user_scaleyoriginal)/65536.0)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_SpawnItemEx("DukeGibPopSpawner_Duke2", 0, 0, height*2, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
			Stop
		ExpanderPopCoop:
			"####" "#" 10
			TNT1 A 0 A_GiveInventory("EnemyZorched",1)
			TNT1 A 0 A_SpawnItemEx("ExpanderExplosion_Coop",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
			TNT1 A 0 A_PlaySound("duke/explosion", 6, 1.0)
			TNT1 A 1 ACS_NamedExecuteWithResult("SamsaraDecorate",36,1)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_Jump(96,2)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt", 1)
			TNT1 A 0 A_TakeInventory("ExpanderGrowthY", 40)
			TNT1 A 0 A_TakeInventory("ExpanderGrowthX", 40)
			TNT1 A 0 A_TakeInventory("ExpanderDoubleX", 40)
			TNT1 A 0 A_SetScale((user_scalexoriginal)/65536.0, (user_scaleyoriginal)/65536.0)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_SpawnItemEx("DukeGibPopSpawner_Duke2", 0, 0, height*2, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_GiveToTarget("DukeGibTaunt",1)
			TNT1 A -1 ACS_NamedExecuteWithResult("SamsaraBossDeathToggler")
			Stop
	}
}

Actor ProtectorDroneCorpse
{
	Health 35
	Height 8
	Radius 16
	Mass 5000
	BloodType "ProDroneBlood"
	BloodColor "aa d1 5c"
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+VULNERABLE
	-SOLID
	+NOTARGET
	-FRIENDLY
	+NOBLOODDECALS
	States
	{
		Spawn:
			PRDR W 4
			Loop
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs2")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs3")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs4")
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs5")
			TNT1 AAA 0 A_SpawnDebris("ProtectorDroneGibs6")
			TNT1 A 0 A_Jump(32,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs1")
			TNT1 A 0 A_Jump(64,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs7")
			TNT1 A 0 A_Jump(64,2)
			TNT1 AA 0 A_SpawnDebris("ProtectorDroneGibs9")
			TNT1 A 0 A_Jump(64,2)
			TNT1 A 0 A_SpawnDebris("ProtectorDroneGibs8")
			TNT1 A 5 A_PlaySound("duke/enemygib")
			Stop
	}
}

actor AlienShrinkerAttack : ShrinkMissile { Speed 16 }
actor AlienShrinkerAttackAlt : AlienShrinkerAttack { Damage (40) DamageType "Normal" }

ACTOR NewProtectorDrone
{
	Height 56
	Radius 20
	Health 1
	Scale 0.65
	BloodColor "aa d1 5c"
    Tag "Protector Drone"
	+SOLID
	+SHOOTABLE
	States
	{
		Spawn:
			PRDR K 280
		Death:
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_PlaySound("prodrone/pain")
			TNT1 A 1 A_SpawnItemEx("ProtectorDrone")
			Stop
	}
}

ACTOR NewProtectorDroneDoom : NewProtectorDrone
{
	States
	{
		Death:
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_PlaySound("prodrone/pain")
			TNT1 A 1 A_SpawnItemEx("ProtectorDroneDoom")
			Stop
	}
}

ACTOR ProtectorDroneDoom : ProtectorDrone { Height 56 }

ACTOR SpectreDrone : ProtectorDrone
{
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
}

ACTOR SpectreDroneDoom : ProtectorDroneDoom
{
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
}

ACTOR InvisibleDroneDoom : ProtectorDroneDoom
{
	+SHADOW
	RenderStyle Translucent
	Alpha 0.0
	States
	{
		Melee:
			TNT1 A 0 A_SetTranslucent(1,0)
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			PRDR FFF 2 A_FaceTarget
			PRDR F 1
			PRDR GH 7
			TNT1 A 0 A_CustomMeleeAttack(22,"prodrone/attack1","prodrone/attack2")
			TNT1 A 0 A_JumpIfCloser(64,"See")
			Goto MeleeEnd
		MeleeEnd:
			PRDR A 26 A_SetTranslucent(0,0)
			PRDR A 14 A_PlaySound("prodrone/roam")
			Goto See
		Death:
			TNT1 A 0 A_SetTranslucent(1,0)
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			PRDR P 3 A_Scream
			PRDR Q 5 A_NoBlocking
			PRDR R 4
			PRDR STU 5
			PRDR V 4
			TNT1 A -1 A_SpawnItemEx("ProtectorDroneCorpse", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
			Stop
	}
}