Actor ProtozoidEgg : SamsaraMonsterAlien
{
	Health 20
	Radius 16
	Height 32
	Mass 20
	Speed 0
	Scale 0.65
	MONSTER
	+FLOORCLIP
	+NOTDMATCH
	+NOINFIGHTING
	+THRUGHOST
	-COUNTKILL
	Obituary "%o died somehow."
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "ShrinkerDMG", 0
	BloodType "DukeBlood"
	BloodColor "ff 40 00"
	Tag "Protozoid Egg"
	States
	{
		Spawn:
			DN49 A 5 A_Look
			Loop
		See:
			DN49 A 35
			DN49 B 0 A_PlaySound("slimer/hatch",CHAN_BODY)
			DN49 BC 5
			DN49 C 1 A_Die("Hatch")
			Stop
		Death.Hatch:
			DN49 C 0 A_SpawnItemEx("ProtozoidEggCorpse", 0, 0, 0, velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
			DN49 CCC 0 A_SpawnItemEx("Duke3D_Protozoid",0,0,0,random(-2,2),random(-2,2),random(2,4),0,SXF_SETMASTER|SXF_TRANSFERSPECIAL,0,tid)
			TNT1 A 1
			Stop
		Death:
		XDeath:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("DukeJibs1")
			TNT1 A 0 A_SpawnDebris("DukeJibs2")
			TNT1 AA 0 A_SpawnDebris("DukeJibs3")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs4")
			TNT1 AA 0 A_SpawnDebris("DukeJibs5")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs6")
			TNT1 A 5 A_PlaySound("duke/enemygib")
			Stop
	}
}

Actor ProtozoidEggCorpse : SamsaraCorpseActor
{
	Health 20
	Height 8
	Radius 16
	Mass 5000
	BloodType "DukeBlood"
	BloodColor "ff 40 00"
	DamageFactor "ZorchPistol", 0.0
	DamageFactor "Zorch", 0.0
	DamageFactor "ZorchKamikaze", 0.0
	DamageFactor "PhasingZorch", 0.0
	DamageFactor "ZorchSpectral", 0.0
	+ISMONSTER
	-COUNTKILL
	-SHOOTABLE
	+VULNERABLE
	-SOLID
	+NOTARGET
	-FRIENDLY
	+NOBLOODDECALS
	States
	{
		Spawn:
			DN49 C 4
			Loop
		Death:
			TNT1 A 0 A_RemoveMaster
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreXDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			TNT1 A 0 A_ChangeFlag("SOLID",false)
			TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_SpawnDebris("DukeJibs1")
			TNT1 A 0 A_SpawnDebris("DukeJibs2")
			TNT1 AA 0 A_SpawnDebris("DukeJibs3")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs4")
			TNT1 AA 0 A_SpawnDebris("DukeJibs5")
			TNT1 AAA 0 A_SpawnDebris("DukeJibs6")
			TNT1 A 5 A_PlaySound("duke/enemygib")
			Stop
	}
}

actor Duke3D_Protozoid : SamsaraMonsterGhost
{
	Health 1
	Radius 13
	Height 15
	Mass 1000
	Speed 2
	SeeSound "slimer/sight"
	DeathSound "slimer/death"
	ActiveSound "slimer/active"
	+FLOORCLIP
	+SHOOTABLE
	-SOLID
	+CANPUSHWALLS
	+CANUSEWALLS
	+CANPASS
	+ISMONSTER
	+NOBLOCKMONST
	+NOICEDEATH
	+NOTDMATCH
	+NOINFIGHTING
	+THRUGHOST
	-COUNTKILL
	+LOOKALLAROUND
	Tag "Protozoid"
	Species "Enemies"
	Scale 0.7
	BloodColor "aa d1 5c"
	MeleeRange 18
	var int user_scaletimer;
	States
	{
	Spawn:
		DK67 BBCCDDEEFFEEDDCC 2 A_Look
		Loop
	See:
		DK67 B 1 A_Chase
		DK67 B 0 A_SetUserVar("user_scaletimer", user_scaletimer + 15)
		DK67 B 0 A_SetScale(0.6 + cos(user_scaletimer * 1.0) * 0.2, 0.35 + sin(user_scaletimer * 1.0) * 0.08)
		Loop
	See2:
		DK67 BBCCDDEEFFEEDDCC 2 A_Chase("","",CHF_NOPLAYACTIVE)
		Goto See
	Melee:
		TNT1 A 0 A_JumpIfInTargetInventory("GettingSlimed",1,"See2")
		TNT1 A 0 A_SpawnItemEx("ProtozoidHitBox",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		TNT1 A 1 Thing_Remove(0)
		Stop
	Death.DukeExplosive:
	Death.DukeDamage:
	Death.DukePistol:
		TNT1 A 0 A_Jump(218,"Death")
		TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		TNT1 A -1 A_Jump(256,"Death")
		Wait
	Death.MightyBoot:
		TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
		TNT1 A 0 A_Jump(64,"Death")
		TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		TNT1 A -1 A_Jump(256,"Death")
		Wait
	Death:
	XDeath:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Jump(128,1,2)
		TNT1 AAAAA 0 A_SpawnDebris("SlimerDukeGibs")
		TNT1 A 5 A_NoBlocking
		TNT1 A -1
		Stop
	}
}

actor SlimerDukeGibs : DukeGibs1
{
	Scale 0.55
	States
	{
	Spawn:
		DK67 VWXY 5
		Loop
	Death:
		TNT1 A 0 A_Gravity
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
		DK67 Z 5
		Stop
	Death2:
		DK67 Z 2100
		DK67 Z 10 A_JumpIf(ACS_NamedExecuteWithResult("SamsaraClientDecorate", 13), "Death2")
		stop
	}
}

actor SlimerExploded
{
	Radius 1
	Height 1
	Speed 0
	-SOLID
	PROJECTILE
	Scale 0.0
	States
	{
	Spawn:
		TNT1 A 5 ThrustThingZ(0,10,0,0)
		TNT1 AAA 0 A_SpawnDebris("SlimerDukeGibs")
		Stop
	Death:
		TNT1 A 5 ThrustThingZ(0,10,0,0)
		TNT1 AAA 0 A_SpawnDebris("SlimerDukeGibs")
		Stop
	}
}

Actor ProtozoidHitBox
{
	Height 48
	Radius 24
	Health 1
    Tag "Protozoid"
	+NOBLOOD
	+ISMONSTER
	-SOLID
	+SHOOTABLE
	+COUNTKILL
	States
	{
		Spawn:
			TNT1 A 0 NoDelay ACS_NamedExecuteWithResult("DukeProtozoidDamage",0,0,0,0)
			TNT1 A 0 A_GiveToTarget("GettingSlimed",1)
		SpawnLoop:
			TNT1 A 1 A_Warp(AAPTR_TARGET,8*cos(CallACS("DukeProtozoidPitch")),0,36-(8*sin(CallACS("DukeProtozoidPitch"))),0,WARPF_NOCHECKPOSITION)
			Loop
		Death:
			TNT1 A 2 A_SpawnItemEx("SlimerExploded",24,0,0,1,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}